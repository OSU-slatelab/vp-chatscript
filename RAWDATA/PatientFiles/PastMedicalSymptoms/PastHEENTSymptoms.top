
topic: ~pastheadache keep repeat (headache)

u: ($present) respond(~currentheadache) ^fail(TOPIC)
u: ($family) respond(~familyheadache) ^fail(TOPIC)

#! Have you ever had headaches before
u: EVER_HAD_HEADACHES_BEFORE (headache) ^factanswer("I have never had headaches.")

# Neck Stiffness - I have defined neck_stiffness as a concept in MedicalConcepts.top

topic: ~pastneckstiffness keep repeat (~neck_stiffness bend)

u: ($present) respond(~currentneckstiffness) ^fail(TOPIC)
u: ($family) respond(~familyneckstiffness) ^fail(TOPIC)

#! Have you ever had neck stiffness before
u: EVER_HAD_NECK_STIFFNESS_BEFORE (~neck_stiffness)    ^factanswer("Not that I recall.")


topic: ~pastnasal keep repeat (nose sinus sinusitis "nasal passages" mucus snot drainage infection infections)

u: ($present) respond(~currentnasal) ^fail(TOPIC)
u: ($family) respond(~familynasal) ^fail(TOPIC)

#! Have you ever had drainage down your throat
u: EVER_HAD_DRAINAGE_DOWN_YOUR_THROAT (ever * [drainage mucus snot] * throat)  ^factanswer("I never had drainage down my throat.")

#! Have you ever had sinus problems
u: EVER_HAD_SINUS_PROBLEMS (ever * [sinus sinusitis "nasal passages"] * [problem issue symptom infection infections])  ^factanswer("My sinuses have been fine.")


topic: ~pasteye keep repeat (eye eyes vision visual)

u: ($present) respond(~currenteye) ^fail(TOPIC)
u: ($family) respond(~familyeye) ^fail(TOPIC)

#! Have you ever had double vision
u: EVER_HAD_DOUBLE_VISION (!blurry [have experience notice] * ["double vision" two twice]) ^factanswer("I never had double vision.")

#! Have you ever had blurry vision
u: EVER_HAD_BLURRY_VISION
	([
		(!double [have experience notice] * [blurry blurred fuzzy foggy blur] * [eye eyes vision])
		(!double  [eye eyes vision][have experience notice] * [blurry blurred fuzzy foggy blur])
	])
	^factanswer("I have never had blurry vision.")

#! Have you ever had vision changes
u: EVER_HAD_VISION_CHANGES
	([
		([eye vision] * [change changes problems issues symptoms])
		([change changes problems issues symptoms] * [eye vision])
	])
	^factanswer("I have never had any vision changes.")
