
# ***************************************************************************************************

# Back Pain - I have defined back_pain as a concept in MedicalConcepts.top


topic: ~pastbackpain keep repeat (~back_pain similar furniture) # added similar to catch when have you had similar pain which seems like past history

	u: ($present) respond(~currentbackpain) ^fail(TOPIC)
	u: ($family) respond(~familybackpain) ^fail(TOPIC)


#! When have you had back pain like this before
	u: When_have_you_had_back_pain_before (when have you had back pain like this before)     ^factanswer("Never.")
	u: (when << ~back_pain [before prior previously past history ever "last time"] >>) ^reuse (When_have_you_had_back_pain_before)
	u: (when << similar ~back_pain  >>) ^reuse (When_have_you_had_back_pain_before)

#! Have you had back pain before
	u: Have_you_had_back_pain_before (have you had back pain before)     ^factanswer("No, never.")
		a: What_caused_it ([why what was] * [cause happen did brought] * [~itwords ~back_pain lifting]) ^factanswer ("I don't remember.")
	u: (!when [~back_pain ~itwords] * [before prior past history ever previously]) ^reuse (Have_you_had_back_pain_before)
	u: (!when ever * [~back_pain ~itwords] * [before prior past history previously]) ^reuse (Have_you_had_back_pain_before)
	u: (!when ever * [~back_pain ~itwords] >) ^reuse (Have_you_had_back_pain_before)

#! Have you lifted heavy furniture before
	u: Have_you_lifted_heavy_furniture_before (have you lifted heavy furniture before)     ^factanswer("No, never.")
	u: (!when !how !why [move lift] * [furniture couch] * [before prior past history ever "as well"]) ^reuse (Have_you_lifted_heavy_furniture_before)

#! When were you lifting the furniture
	u: When_were_you_lifting_the_furniture (when were you lifting the furniture)  ^factanswer("I wasn't lifting anything.")
	u: (![hurt back what] [when "how long ago"] * [lift move] * [furniture couch things]) ^reuse (When_were_you_lifting_the_furniture)
	u: (![hurt back what] [when "how long ago"] * help * friend) ^reuse (When_were_you_lifting_the_furniture)

# ***************************************************************************************************

topic: ~pastbackinjury keep repeat (~back_injury surgery)


	u: ($present) respond(~currentbackinjury) ^fail(TOPIC)
	u: ($family) respond(~familybackinjury) ^fail(TOPIC)


#! Have you had back injury before
	u: Have_you_had_back_injury_before (have you had back injury before)   ^factanswer("No, never.")
	u: (<< ~back_injury [before prior past history ever] >>) ^reuse (Have_you_had_back_injury_before)


#! Have you had back surgery before
	u: Have_you_had_back_surgery_before (have you had back surgery before)   ^factanswer("No, never.")
	u: (<< surgery back [before prior past history ever] >>) ^reuse (Have_you_had_back_surgery_before)


# ***************************************************************************************************

topic: ~pastmusclepain keep repeat (~muscle_pain !pain stretch )


	u: ($present) respond(~currentmusclepain) ^fail(TOPIC)
	u: ($family) respond(~familymusclepain) ^fail(TOPIC)


#! Have you had muscle pain before
	u: Have_you_had_muscle_pain_before (have you had muscle pain before)   ^factanswer("No, never.")
	u: (!back << ~muscle_pain [before prior past history ever] >>) ^reuse (Have_you_had_muscle_pain_before)


# ***************************************************************************************************

topic: ~pastbackstiffness keep repeat (back stiff "back stiffness")


	u: ($present) respond(~currentbackstiffness) ^fail(TOPIC)
	u: ($family) respond(~familybackstiffness) ^fail(TOPIC)


#! Have you had back stiffness before
	u: Have_you_had_back_stiffness_before (have you had back stiffness before)     ^factanswer("No, never.")
	u: (<< "back stiffness"[before prior past history ever] >>) ^reuse (Have_you_had_back_stiffness_before)


# ***************************************************************************************************

topic: ~pastspinepain keep repeat (~spine_pain !pain)


	u: ($present) respond(~currentspinepain) ^fail(TOPIC)
	u: ($family) respond(~familyspinepain) ^fail(TOPIC)


#! Have you had spine pain before
	u: Have_you_had_spine_pain_before (have you had spine pain before)     ^factanswer("No, never.")
	u: (!back << ~spine_pain [before prior past history ever] >>) ^reuse (Have_you_had_spine_pain_before)


# ***************************************************************************************************

topic: ~pastlegpain keep repeat (~leg_pain !pain)

	u: ($present) respond(~currentlegpain) ^fail(TOPIC)
	u: ($family) respond(~familylegpain) ^fail(TOPIC)


#! Have you had leg pain before
	u: Have_you_had_leg_pain_before (have you had leg pain before)     ^factanswer("No, never.")
	u: (!back << ~leg_pain [before prior past history ever] >>) ^reuse (Have_you_had_leg_pain_before)

# ***************************************************************************************************

topic: ~pastjointpain keep repeat (~joint_pain thumb joint joints)

	u: ($present) respond(~currentjointpain) ^fail(TOPIC)
	u: ($family) respond(~familyjointpain) ^fail(TOPIC)

#! Have you had joint pain before
	u: Have_you_had_joint_pain_before (have you had joint pain before)     ^factanswer("No, never.")
	u: (![back abdominal belly] << ~joint_pain [before prior past history ever] >>) ^reuse (Have_you_had_joint_pain_before)
	u: (![back abdominal belly] "have you had" * ~joint_pain) ^reuse (Have_you_had_joint_pain_before)
