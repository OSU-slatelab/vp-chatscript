topic: ~currentgeneralmatches keep repeat deprioritize nostay ()

	u: ($pasthistory) respond(~pastgeneralmatches)  ^fail(TOPIC)
	u: ($family) respond(~familygeneralmatches) ^fail(TOPIC)

#!!S Can you move your hip
	?: CAN_YOU_MOVE_YOUR_BODYPART ( can *~3 move your _~bodyparts)
	^factanswer(^"[I have no problems with my '_0][I can move my '_0 just fine]")

#!!S Do you have any pain in your hip
	?: ANY_PAIN_IN_YOUR_BODYPART (!back [ache pain] *~3 _~bodyparts)
	^factanswer(^"[I have no pain in my '_0]	[Nope, no pain in my '_0]")

#!!S Do you have any hip pain
	?: DO_YOU_HAVE_ANY_BODYPART_PAIN (![~medicines long where why how back] [have experience feel felt any] *~3 _~bodyparts pain)
	^factanswer(^"[I have no '_0 pain]	[No, no '_0 pain]")

#!!S Have you had any surgeries
	?: HAVE_YOU_HAD_ANY_MEDICALTREATMENTS (had any _~medical_treatments)
	^factanswer(^"No, no '_0")

#!!S Does your hip hurt
	?: DOES_YOUR_BODYPART_HURT (does your _~bodyparts hurt)
	^factanswer(^"[No, my '_0 is fine]	[My '_0 feels fine]")

#!!S Do your toes hurt
	?: DO_YOUR_BODYPARTS_HURT (do your _~bodyparts hurt)
	^factanswer(^"[No, my '_0 are fine]	[My '_0 feel fine]")

#!!S How is your hip
	?: HOW_IS_YOUR_BODYPART (!pain how is your _[~bodyparts ~all_systems])
	^factanswer(^"My '_0 is fine")

#!!S How does your hip feel
	?: HOW_DOES_YOUR_BODYPART_FEEL ( how [has does] your _~bodyparts [been feel])
	^factanswer(^"My '_0 feels fine")

#!!S Do you have any problems with your hip
	?: ANY_PROBLEMS_WITH_YOUR_BODYPART ( any [problems issues difficulty] with your _*~2)
	^factanswer(^"No problems with my '_0")

#!!S Do you have any problems with urination
	?: ANY_PROBLEMS_WITH_BODILYFUNCTION ( any [problems issues difficulty] with _*~2)
	^factanswer(^"No problems with _0")

#!!S Do you have any problems urinating
	?: ANY_PROBLEMS_BODILYFUNCTIONING ( any [problems issues difficulty] _*1)
	^factanswer(^"No problems '_0")

#!!S Can you control your urination
	?: CAN_YOU_CONTROL_YOUR_BODILYFUNCTION ([are can able] * control your _*1)
	^factanswer(^"No problems with my '_0")

#!!S Is your urination normal
	?: IS_YOUR_BODILYFUNCTION_NORMAL ([are is] [your you] _*1 [normal normally usual usually])
	^factanswer(^"No problems with my '_0")

#!!S Have you tried heat
	?: HAVE_YOU_TRIED_SOMETREATMENT (!~medicines [have did] *~3 [try] *~2 _~medical_treatments)
	^factanswer(^"No, I have not tried '_0")

#!!S Have you used heat
	?: HAVE_YOU_USED_SOMETREATMENT (!~medicines [use] _~medical_treatments)
	^factanswer(^"No, I have not used '_0")

#!!S How about your Hip
	?: HOW_ABOUT_YOUR_BODYPART ( how about 'your _~bodyparts)
	^factanswer(^"No problems with my '_0")

#!!S How about your breathing
	?: HOW_ABOUT_YOUR_BODILYFUNCTION ( how about 'your _~activity)
	^factanswer(^"No problems with my '_0")

#!!S Does it hurt when you breathe
	?: DOES_IT_HURT_WHEN_YOU_ACTIVITY ([does is] it [hurt painful] *~2 [when for] you *~1 _~activity)
	^factanswer(^"No, I can '_0  OK")

#!!S Does lying down help
	?: DOES_SOMEPOSITION_HELP (does _~position *~3 [help better]) ^factanswer(^"'_0  doesn't seem to help much")
	?: (_~position * does [help better]) ^reuse (DOES_SOMEPOSITION_HELP)

#! What about anyone in your family
	u: (< what about _*) ^factanswer("What do you mean?")

# ***************************************************************************************************

topic: ~pastgeneralmatches keep repeat deprioritize (~medical_treatments ~medical_tests ~all_systems ~pain_descriptors)


	u: ($present) respond(~currentgeneralmatches) ^fail(TOPIC)
	u: ($family) respond(~familygeneralmatches) ^fail(TOPIC)

#!!S Have you had any pain in your hip
	?: HAVE_YOU_HAD_ANY_PAIN_IN_YOUR_BODYPART (pain [in on] [your the] _~bodyparts)
	^factanswer(^"[I have not had any pain in my '_0]	[Nope, never had pain in my '_0]")

#!!S Have you had any hip pain
	?: HAVE_YOU_HAD_ANY_BODYPART_PAIN (![back the ~joint] had * _~bodyparts pain)
	^factanswer(^"[I have no '_0 pain]	[No, no '_0 pain]")

#!!S Have you had any problems with urination
	?: HAD_PROBLEMS_WITH_BODILYFUNCTION (![~joint back] any [problems issues] with _*1)
	^factanswer(^"Not had any problems with '_0")

#!!S Have you had any problems urinating
	?: HAD_PROBLEMS_BODILYFUNCTIONING (![~joint back] any [problems issues] _*1)
	^factanswer(^"No problems '_0")

#!!S Have you ever been treated for whiplash
	?: EVER_BEEN_TREATED_FOR_SOMECONDITION ([treat treated treatment] for _*1)
	^factanswer(^"No '_0 treatments or anything like that")

#!!S Have you ever had an MRI
	?: EVER_HAD_AN_MEDICALTEST (ever had 'an _~medical_tests)
	^factanswer(^"No, I have never had an '_0")

#!!S Have you ever had a spinal tap
	?: EVER_HAD_A_MEDICALTEST (ever had 'a _~medical_tests)
	^factanswer(^"No, I have never had a '_0")


# ***************************************************************************************************

topic: ~familygeneralmatches keep repeat deprioritize (~medical_treatments ~all_systems ~pain_descriptors)


	u: ($present) respond(~currentgeneralmatches) ^fail(TOPIC)
	u: ($pasthistory) respond(~pastgeneralmatches) ^fail(TOPIC)

#!!S Anyone in your family have hip problems
	?: ANY_FAMILY_BODYPART_PROBLEMS (have {any} _*~2 problems)
	^factanswer(^"[No family history of '_0 problems]	[Nope, nobody in my family has '_0 problems]")
