# Range of Motion

topic: ~currentrangeofmotion keep repeat
	(range "range of motion" motion move movement motion crouch lean bend bending toe lift stand twist jump
	stiff stiffness lean straighten run "lie down" action open grip drop "lose grip" difficult tightness)

u: ($pasthistory) respond(~pastrangeofmotion) ^fail(TOPIC)

#! What movements make it hurt
u: WHAT_MOVEMENTS_MAKE_IT_HURT
	([
		([action movement what] * [make cause] * [hurt pain painful worse])
		([action movement what] * [hurt pain painful worse])
	])
	^factanswer("It is difficult to say.")

#! Does it hurt when you bend
u: DOES_IT_HURT_WHEN_YOU_BEND (<< [hurt painful worse] [bend crouch lean] >>)  ^factanswer("I don't have any problems with that.")

#! Does it hurt when you move
u: DOES_IT_HURT_WHEN_YOU_MOVE
	([
		(<< [hurt painful worse] [move movement] >>)
		([~itwords $chiefcomplaint?] *~3 change *~3 [move movement])
	])
		^factanswer("I don't have any problems with that.")

#! Did you bend down
u: DID_YOU_BEND_DOWN (!can did * bend [down over])  ^factanswer("I am not sure.")

#! How far can you bend
u: HOW_FAR_CAN_YOU_BEND (how far *~3 bend) ^factanswer("I don't have any problems with that.")

#! Can you bend down
u: CAN_YOU_BEND_DOWN (!worse [can are difficult] *~3 bend) ^factanswer("I don't have any problems with that.")

#! Have you tried to bend down
u: HAVE_YOU_TRIED_TO_BEND (try *~3 [bend bending]) ^factanswer("I don't have any problems with that.")

#! Can you straighten up
u: CAN_YOU_STRAIGHTEN_UP ([can are] *~3 ["get up" straighten]) ^factanswer("I don't have any problems with that.")

#! Can you lean backwards
u: CAN_YOU_LEAN ([can are] *~3 lean) ^factanswer("I don't have any problems with that.")

#! Can you jump
u: CAN_YOU_JUMP ([can are] *~3 jump)  ^factanswer("I don't have any problems with that.")

#! Can you run
u: CAN_YOU_RUN (!family [can are] *~3 run)  ^factanswer("I can do that just fine.")

#! Can you lie down
u: CAN_YOU_LIE_DOWN ([can are] *~3 "lie down")  ^factanswer("I can do that just fine.")

#! Can you open a jar
u: CAN_YOU_OPEN ([open twist screw unscrew] *~3 [jar bottle thing anything])  ^factanswer("I don't have any problems with that.")

#! Have you been dropping things
u: HAVE_YOU_BEEN_DROPPING_THINGS  ([drop "lose grip"])  ^factanswer("I don't have any problems with that.")

#! Do you have any decreased range of motion
u: DO_YOU_HAVE_DECREASED_MOTION (~decrease_count * [motion move movement ~itwords]) ^factanswer("I don't have any problems with that.")

#! Can you touch your toes
u: CAN_YOU_TOUCH_YOU_TOES ([put reach touch] * [toe shoe])  ^factanswer("I don't have any problems with that.")

#! Do you know how to lift properly
u: DO_YOU_KNOW_HOW_TO_LIFT_PROPERLY (lift * [properly correctly "with your legs" "with your back"]) ^factanswer("I know how to lift.")

#! Were you lifting anything
u: WERE_YOU_LIFTING_ANYTHING
	([
		(![when are can able work job] [were have do] *~3 [lift pick])
		(![when are can able work job] << heavy [lift lifting] >>)
	])
	^factanswer("Not that I recall.")

#! Can you lift anything
u: CAN_YOU_LIFT_ANYTHING ([able can] *~4 lift)  ^factanswer("I don't have any problems with that.")

#! Were you twisting
u: WERE_YOU_TWISTING (twist) ^factanswer("Not that I am aware of.")

#! Can you move your back
u: CAN_YOU_MOVE_YOUR_BACK (![I we] move *~3 [back ~itwords]) ^factanswer("I can do that just fine.")

#! Have you been moving OK
u: HAVE_YOU_BEEN_MOVING (![were ~bodyparts][can been able] *~3 move) ^factanswer("I have been moving just fine.")

#! Are you able to stand
u: ARE_YOU_ABLE_TO_STAND (!does [able can] *~3 [stand "stand up" "get up"])  ^factanswer("I don't have any problems with that.")

#! Is standing a problem
u: IS_STANDING_A_PROBLEM (!long-standing standing *~2 [problem issue concern difficult painful hurt])  ^factanswer("I don't have any problems with that.")

#! Are you stiff
u: ARE_YOU_STIFF (!neck [stiff stiffness tight tightness]) ^factanswer("I am not stiff.")

# ***************************************************************************************************
topic: ~pastrangeofmotion keep repeat (motion move movement bend bending stiffness stiff)

u: ($present) respond (~currentrangeofmotion) ^fail(TOPIC)

#! Have you lost range of motion before
u: HAVE_YOU_LOST_RANGE_OF_MOTION_BEFORE (<< "range of motion" [before prior past history ever] >>) ^factanswer("I've never had any problems with that.")

#! Have you had stiffness before
u: HAVE_YOU_HAD_STIFFNESS_BEFORE (<< stiffness [before prior past history ever] >>) ^factanswer("I've never had any problems with that.")
