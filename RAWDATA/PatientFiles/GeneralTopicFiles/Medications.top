# CURRENT MEDICATIONS This is only for medications taken for the chief complaint

topic: ~currentmedications keep repeat (~medicines "how much" drug dose strength dosage ~drugs_legal)

	u: ($pasthistory) respond(~pastmedications) ^fail(TOPIC)
	u: ($family) respond(~familymedicalproblems) ^fail(TOPIC) # don't have a familymedicaitons topic so will put the odd family medication question in familymedicalproblems

#! Are you taking any medication for the 1 $chiefcomplaint=1
	u: TAKING_MEDICATION_FOR_THE_1
		([
 			(![how long other] [use take on try] * [alleviate get_rid_of reduce diminish "help with" for] *~2 [~itwords $chiefcomplaint? symptom])
 			(![how long other] [use take on try "no"] * ~medicines * [~itwords $chiefcomplaint? symptom])
			(![how long other] [use take on try "no"] * [~itwords $chiefcomplaint? symptom] * ~medicines)
		])
	^factanswer("I don't take any medications.")
			a: DOSE_1 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! Are you taking any medication for the 2 $complaint2=2
u: TAKING_MEDICATION_FOR_THE_2
	([
		(![how long other] [use take on try] * [alleviate get_rid_of reduce diminish "help with" for] *~2 [~itwords $chiefcomplaint? symptom])
		(![how long other] [use take on try "no"] * ~medicines * [~itwords $chiefcomplaint? symptom])
		(![how long other] [use take on try "no"] * [~itwords $chiefcomplaint? symptom] * ~medicines)
	])
^factanswer("I don't take any medications.")
		a: DOSE_2 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! Are you taking any medication for the 3 $complaint3=3
u: TAKING_MEDICATION_FOR_THE_3
	([
		(![how long other] [use take on try] * [alleviate get_rid_of reduce diminish "help with" for] *~2 [~itwords $chiefcomplaint? symptom])
		(![how long other] [use take on try "no"] * ~medicines * [~itwords $chiefcomplaint? symptom])
		(![how long other] [use take on try "no"] * [~itwords $chiefcomplaint? symptom] * ~medicines)
	])
^factanswer("I don't take any medications.")
		a: DOSE_3 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! When did you take the medicine $currentmedication1=medicine
	?: WHEN_DID_YOU_TAKE_MEDICINE (when * [~medicines $currentmedication1?])   ^factanswer("I didn't take medication.")

#! Any reactions to medications
	u: ANY_REACTIONS_TO_MEDICATIONS (reaction to ~medicines) ^factanswer("Not that I am aware of.")

#! Do you need to take the medicine that often  $currentmedication1=medicine
	?: NEED_TO_TAKE_THE_MEDICINE_THAT_OFTEN ([need "have to"] * [take use] * [$currentmedication1? ~itwords] * [often hours])   ^factanswer("I don't take any medications, supplements, or vitamins.")

#! How often did you take the medicine $currentmedication1=medicine
	?: HOW_OFTEN_DID_YOU_TAKE_THE_MEDICINE (<< [often frequently many] [~medicines ~itwords $currentmedication1?]>> )   ^factanswer("I didn't take medication.")
		a: DOSE_1 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! How long have you been taking the medicine $currentmedication1=medicine
	?: HOW_LONG_TAKING_MEDICINE
		([
			(how long * [take on] * [~medicines $currentmedication1? ~itwords])
 			(how many * [doses days])
		])
	^factanswer("I haven't taken any medicine.")
		a: DOSE_1 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! How much medicine have you taken $currentmedication1=medicine
	?: HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN
		([
 			(how [much many] *~3 [~medicines $currentmedication1?] *~3 [take use])
			(how [much many] *~3 [~medicines $currentmedication1?])
		])
	^factanswer("I haven't taken any medicine.")

#! Would you like a stronger prescription
	?: WOULD_YOU_LIKE_A_STRONGER_PRESCRIPTION ([stronger powerful] * [medication prescription])   ^factanswer("I don't know.")

#! What is the strength of the medicine $currentmedication1=medicine
	?: WHAT_IS_THE_STRENGTH_OF_THE_MEDICINE
		([
			([strength strong] *~3 [~medicines $currentmedication1?])
			([what know] *~2 [strength strong])
		])
	^factanswer("I haven't taken any medicine.")

#! Did the medicine help $currentmedication1=medicine
	?: DID_THE_MEDICINE_HELP
		([
			(!position !try !anything [~medicines $currentmedication1? ~itwords] * [help away work better])
			([how much did] * [help away work better relief] * [~medicines $currentmedication1? ~itwords])
 			([how much did] * [~medicines $currentmedication1? ~itwords] * [help away work better relief anything])
		])
	^factanswer("I haven't taken any medicine.")
			a: DOSE_1 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! Did you take the medicine today $currentmedication1=medicine
	?: DID_YOU_TAKE_MEDICINE_TODAY
		([
			(take *~3 ~medicines *~3 [today morning afternoon now])
			([last recent most] *~3 [~medicines $currentmedication1? anything])
		])
	^factanswer("I haven't taken any medicine.")
		a: DOSE_1 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! Are you still taking medicine $currentmedication1=medicine
	?: ARE_YOU_STILL_TAKING_MEDICINE ([still continue] *~3 [take use] *~3 $currentmedication1?)   ^factanswer("I haven't taken any medicine.")
		a: DOSE_1 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! What pills did you take
	?: WHAT_PILLS_DID_YOU_TAKE
		([
			(!dose !strength [what about] *~3 ~medicines did *~3 [use take])
			(!dose !strength [what about] *~3 [sort kind type] *~3 ~medicines did *~3 [use take])
		])
	^factanswer("I didn't take medication.")
		a: DOSE_1 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! What kind of medicine is that
	?: WHAT_KIND_OF_MEDICINE_IS_THAT
		([
 			([kind type] * ~medicines)
 			(what * ~medicines * that)
		])
	^factanswer("I didn't take medication.")
		a: DOSE_1 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! Would you like some pain medication
	?: WOULD_YOU_LIKE_SOME_PAIN_MEDICATION (<< [like want need] [~medicines drug] >>)   ^factanswer("I don't know.")

#! Did your doctor prescribe the medicine
	?: DOCTOR_PRESCRIBE_THE_MEDICINE (<< [Doctor physician] prescribe ~medicines >>) ^factanswer("No")

#! Who prescribed the medicine
	?: WHO_PRESCRIBED_THE_MEDICINE (<< [who anyone] prescribe ~medicines >>) ^factanswer("It is not prescribed.")

#! What is medicine $currentmedication1=medicine
	?: WHAT_IS_MEDICINE (what is $currentmedication1?)    ^factanswer("I didn't take medication.")
		a: DOSE_1 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! Any prescription medication
	u: ANY_PRESCRIPTION_MEDICATION
		([
			(![how long other herbal ~vitamin ~minerals allergies allergic ~OTCmedicines] [take on about any no] *~3 ~prescribedmedicines)
 			(![how long other herbal ~vitamin ~minerals allergies allergic ~OTCmedicines] [take on] *~3 ~prescribedmedicines *~3 [regular regularly daily])
			(![how long other herbal ~vitamin ~minerals allergies allergic ~OTCmedicines]  ~prescribedmedicines *~3 [take on] *~3 [regular regularly daily])
		])
	^factanswer("I don't take any prescription medications.")
		a: DOSE_1 ([dosage dose "how much"]) ^factanswer("I am not sure.")
		a: IS_MEDICATION_NEW (<< [~itwords ~prescribedmedicines] [new old first] >>) ^factanswer ("I don't take anything.")

#! Are you taking any medication
	?: ARE_YOU_TAKING_ANY_MEDICATION
		([
			(![how long other herbal ~vitamin ~minerals allergies allergic] [take on about any know list] * ~medicines)
 			(![how long other allergies allergic] [do are] * [take prescribed know] ~medicines)
			(![how long other allergies allergic] what * ~medicines * [take prescribed on])
			(![how long other herbal ~vitamin ~minerals allergies allergic] [take on know list] * ~medicines * [regular regularly daily])
 			(![how long other herbal ~vitamin ~minerals allergies allergic]  ~medicines * [take on] * [regular regularly daily])
		])
	^factanswer("I don't take any medications, supplements, or vitamins.")
		a: DOSE_1 ([dosage dose "how much"]) ^factanswer("I am not sure.")
		a: IS_MEDICATION_NEW (<< [~itwords ~prescribedmedicines] [new old first] >>) ^factanswer ("I don't take anything.")
		a: ANY_OTHER_MEDICATION (anything else) ^reuse (~othermedications.TAKE_ANY_OTHER_MEDICATION)

#! What were the pills called
	?: WHAT_WERE_THE_PILLS_CALLED
		([
			(what *~3 ~medicines *~3 [called named])
			(name * ~medicines)
			(what * ~medicines >)
		])
	^factanswer("I don't know.")

# ###############################################################################
# This is the vitamin and mineral supplement section

topic: ~supplements keep repeat (~vitamin minerals supplement herbal OTC "over the counter")


#! Are you taking supplements
	?: ARE_YOU_TAKING_SUPPLEMENTS (![vaccine vaccinated allergy] [mineral supplement ~vitamin herbal ~minerals])   ^factanswer("I don't take any medications, supplements, or vitamins.")
		a: HOW_MUCH_SUPPLEMENTS (how [many much dose]) ^factanswer("I don't know.")
		a: WHY_SUPPLEMENTS ([what why] * take) ^factanswer("I don't know.")

#! Do you know which multivitamin you take
	u: WHICH_MULTIVITAMIN ([which what] * ~vitamin * [take use]) ^factanswer("I don't take any medications, supplements, or vitamins.")

	#! Any OTC medication
		u: ANY_OTC_MEDICATION (![how long other herbal ~vitamin ~minerals allergies allergic ~prescribedmedicines] [take on about any] * ~OTCmedicines) ^factanswer("I don't take any OTC medications.")

# ###############################################################################

# OTHER MEDICATIONS

topic: ~othermedications (~medicines)

#! Are you taking any other medication for 1 $chiefcomplaint=1
 	u: TAKE_OTHER_MEDICATION_FOR_1
		([
			(!ever << other ~medicines $chiefcomplaint? >>)
 			(!ever other * $chiefcomplaint? * ~medicines)
		])
	^factanswer("I haven't taken anything.")
		a: HOW_MUCH_OTHER_MEDICATION_1 (how [many much dose]) ^factanswer("I haven't taken anything.")
		a: HOW_OFTEN_OTHER_MEDICATION_1 (how [often frequently]) ^factanswer("I haven't taken anything.")
		a: WHY_TAKE_OTHER_MEDICATION_1 (why) ^factanswer("I haven't taken anything.")
		a: WHEN_DID_YOU_START_MEDICATION_1 ([start begin])   ^factanswer("I didn't take medication.")
		a: WHAT_DOES_MEDICATION_1_DO (!reason [what purpose])   ^factanswer("I didn't take medication.")

#! Are you taking any other medication for 2 $complaint2=2
u: TAKE_OTHER_MEDICATION_FOR_2
	([
		(!ever << other ~medicines $complaint2? >>)
		(!ever other * $complaint2? * ~medicines)
	])
^factanswer("I haven't taken anything.")
	a: HOW_MUCH_OTHER_MEDICATION_2 (how [many much dose]) ^factanswer("I haven't taken anything.")
	a: HOW_OFTEN_OTHER_MEDICATION_2 (how [often frequently]) ^factanswer("I haven't taken anything.")
	a: WHY_TAKE_OTHER_MEDICATION_2 (why) ^factanswer("I haven't taken anything.")
	a: WHEN_DID_YOU_START_MEDICATION_2 ([start begin])   ^factanswer("I didn't take medication.")
	a: WHAT_DOES_MEDICATION_2_DO (!reason [what purpose])   ^factanswer("I didn't take medication.")

#! Are you taking any other medication for 3 $complaint3=3
u: TAKE_OTHER_MEDICATION_FOR_3
	([
		(!ever << other ~medicines $complaint3? >>)
		(!ever other * $complaint3? * ~medicines)
	])
^factanswer("I haven't taken anything.")
	a: HOW_MUCH_OTHER_MEDICATION_3 (how [many much dose]) ^factanswer("I haven't taken anything.")
	a: HOW_OFTEN_OTHER_MEDICATION_3 (how [often frequently]) ^factanswer("I haven't taken anything.")
	a: WHY_TAKE_OTHER_MEDICATION_3 (why) ^factanswer("I haven't taken anything.")
	a: WHEN_DID_YOU_START_MEDICATION_3 ([start begin])   ^factanswer("I didn't take medication.")
	a: WHAT_DOES_MEDICATION_3_DO (!reason [what purpose])   ^factanswer("I didn't take medication.")

#! Are you taking any other medication
 	u: TAKE_ANY_OTHER_MEDICATION
		([
			(![pain ever try $currentmedication1? $currentmedication3?] other ~medicines)
			(![pain ever does "help you with" bother bothering try $currentmedication1? $currentmedication3? know me] anything else >)
		])
	^factanswer("Nothing.")
		a: HOW_MUCH_OTHER_MEDICATION (![pain ever $currentmedication1? $currentmedication3?] how [many much dose dosage]) ^factanswer("I don'take anything else.")
		a: HOW_OFTEN_OTHER_MEDICATION ( ![pain ever $currentmedication1? $currentmedication3?] how [often frequently]) ^factanswer("I don'take anything else.")
		a: HOW_LONG_OTHER_MEDICATION (![pain $currentmedication1? $currentmedication3?] how [long days]) ^factanswer("I didn't take anything.")
		a: WHY_TAKE_OTHER_MEDICATION (why) ^factanswer("I haven't taken anything.")
		a: WHEN_DID_YOU_START_MEDICATION ([start begin])   ^factanswer("I didn't take medication.")
		a: WHAT_DOES_MEDICATION_DO (!reason [what purpose])   ^factanswer("I didn't take medication.")

#! Are you taking any other medication
 	u: TAKE_ANY_OTHER_MEDICATION1
		([
			(![pain ever try why $currentmedication1? $currentmedication2?] other * ~medicines)
			(![pain ever try why does "help you with" bother bothering $currentmedication1? $currentmedication2?] anything else >)
		])
	^factanswer("Nothing.")
		a: HOW_MUCH_OTHER_MEDICATION_1 (![pain why $currentmedication1? $currentmedication2?] how [many much dose dosage]) ^factanswer("I didn't take anything.")
		a: HOW_OFTEN_OTHER_MEDICATION_1 (![pain why $currentmedication1? $currentmedication2?] how [often frequently]) ^factanswer("I didn't take anything.")
		a: HOW_LONG_OTHER_MEDICATION_1 (![pain why $currentmedication1? $currentmedication2?] how [long days]) ^factanswer("I didn't take anything.")
		a: WHY_TAKE_OTHER_MEDICATION_1 (why) ^factanswer("I haven't taken anything.")
		a: WHEN_DID_YOU_START_MEDICATION_1 ([start begin])   ^factanswer("I didn't take medication.")
		a: WHAT_DOES_MEDICATION_1_DO (!reason [what purpose])   ^factanswer("I didn't take medication.")

#! Are you taking any other medication
 	u: TAKE_ANY_OTHER_MEDICATION2
		([
			(![pain ever] other * ~medicines)
			(![pain ever does "help you with" bother bothering] anything else)
		])
	^factanswer("Nothing.")
		a: HOW_MUCH_OTHER_MEDICATION_2 (how [many much dose]) ^factanswer("I didn't take anything.")
		a: HOW_OFTEN_OTHER_MEDICATION_2 (how [often frequently]) ^factanswer("I didn't take anything.")
		a: HOW_LONG_OTHER_MEDICATION_2 (how [long days]) ^factanswer("I didn't take anything.")
		a: WHY_TAKE_OTHER_MEDICATION_2 (why) ^factanswer("I haven't taken anything.")
		a: WHEN_DID_YOU_START_MEDICATION_2 ([start begin])   ^factanswer("I didn't take medication.")
		a: WHAT_DOES_MEDICATION_2_DO (!reason [what purpose])   ^factanswer("I didn't take medication.")

#! Why do you take the medication
	?: WHY_DO_YOU_TAKE_THE_MEDICATION (why *~3 [take use] *~3 ~medicines) ^factanswer("For my health.")

# ###############################################################################
# PAST MEDICATIONS

topic: ~pastmedications keep repeat ("anything else" ibuprofen ~medicines antibiotics)

	u: ($present) respond (~currentmedications) ^fail(TOPIC)
#	u: ($family) respond(~familymedications) ^fail(TOPIC)

#! Have you ever taken any medication for 1  $chiefcomplaint=1
	?: EVER_TAKEN_MEDICATION_FOR_1  (![how long] ever [use take try on] * ~medicines * $chiefcomplaint?)    ^factanswer("Never.")

#! Have you ever taken any medication for 2  $complaint2=2
	?: EVER_TAKEN_MEDICATION_FOR_2 (![how long] ever [use take try on] * ~medicines * $complaint2?)    ^factanswer("Nothing.")

#! Have you ever taken any medication for 3  $complaint3=3
	?: EVER_TAKEN_MEDICATION_FOR_3 (![how long] ever [use take try on] * ~medicines * $complaint3?)    ^factanswer("Never.")

#! Have you had any reactions to medications
	u: ANY_REACTIONS_TO_MEDICATIONS (reaction to ~medicines) ^factanswer("Not that I am aware of.") # this is repeated here because of "have you had" which triggers past

#! Have you ever taken any other medication
	?: EVER_TAKEN_OTHER_MEDICATION
		([
			(![how long] [recently ever] * [take try on treated] * ~medicines)
			(![how long] [use take try on treated] * ~medicines * [recently ever])
 			(![how long] [use take try on treated] * "anything else" >)
		])
	^factanswer("Nothing else.")

#! Have you ever been prescribed medication before
	?: EVER_PRESCRIBED_MEDICATION
		([
			(![how long] [recently ever] * [take try on treated prescribe] * ~medicines)
			(![how long] [use take try on treated prescribe] * ~medicines * [recently ever])
	 		(![how long] [use take try on treated prescribe] * "anything else" >)
		])
	^factanswer("I have not been prescribed any medications.")
