
# ***************************************************************************************************
# Activity

topic: ~normal_activity keep repeat (exercise ~exerciselist activity rest drive seatbelt seat_belt "work out" active energy bedridden shape tired walk)

	?: ( $$family) ^fail(TOPIC)

#! How often do you exercise
	?: HOW_OFTEN_DO_YOU_EXERCISE (<< [often lot] [exercise "work out"] >>)  ^factanswer("Not very often.")

#! Do you have any trouble exercising
	?: DO_YOU_HAVE_ANY_TROUBLE_EXERCISING ([trouble problems ~breathing_problems] *~3 [exercise walk "workout"]) ^factanswer("I have no problems exercising.")

#! Do you exercise
	?: DO_YOU_EXERCISE (["work out" exercise ~exerciselist "physical activity" shape ]) ^factanswer("I don't exercise that much.")
		a: WHEN_DID_YOU_EXERCISE (when * [last most recent]) ^factanswer("I don't remember.")

#! Are you active
	?: ARE_YOU_ACTIVE
		([
			(![sexually sexual $chiefcomplaint] [are you] *~3 [active sedentary])
			(![sexually sexual $chiefcomplaint] [activity active] *~3 [normally usually level ~timename])
		])
	^factanswer("I get plenty of exercise.")

#! How is your energy
	?: HOW_IS_YOUR_ENERGY ([energy tired])  ^factanswer("I have enough energy.")

#! Have you been resting
	?: HAVE_YOU_BEEN_RESTING (![pain does help] rest) 	 ^factanswer("I rest enough.")
		a: HOW_MUCH_RESTING (how much) Since I can't work, about all I can do is rest.

#! Have you been bedridden
	?: HAVE_YOU_BEEN_BEDRIDDEN (![pain does help] bedridden) 	^factanswer("I have not been bedridden.")

#! Are you able to drive
	?: ARE_YOU_ABLE_TO_DRIVE (!home [able can do] *~3 drive) 	 ^factanswer("I am able to drive.")

#! Are you able to drive home
	?: ARE_YOU_ABLE_TO_DRIVE_HOME ([able can] *~2 drive *~3 home)  ^factanswer("I can drive.")

#! Do you wear a seatbelt
	?: WEAR_A_SEATBELT ([seatbelt seat_belt]) ^factanswer("I always wear a seatbelt.")

# ***************************************************************************************************
# Alcohol use

topic: ~alcoholuse keep repeat (~alcoholic_beverage "eye opener" "eye-opener" drink guilty intake social socially)

	u: ( $$family) ^fail(TOPIC)

#! How much do you drink
	?: HOW_MUCH_DO_YOU_DRINK
		([
			(!water how *~2 [drink ~alcoholic_beverage much often many])
			(!water [drink ~alcoholic_beverage] * [day week month])
			(!water [beer wine] * [day week month]) # don't know why I need this to catch beers and wine
		])
	^factanswer("I don't drink.")

#! Has anyone thought you have an alcohol problem
	?: ANYONE_THOUGHT_ALCOHOL_PROBLEM ([anyone anybody] *~3 have *~3 [drink ~alcoholic_beverage])  ^factanswer("Never.")

#! Do you drink
	?: DO_YOU_DRINK
		([
			([drink use consume any] * [social socially ~alcoholic_beverage])
			([social ~alcoholic_beverage] * [drink use consume consumption intake])
			(![what where when] [drink ~alcoholic_beverage])
		])
	^factanswer("I don't drink.")

#! Do you feel guilty about your drinking
	?: DO_YOU_FEEL_GUILTY_ABOUT_YOUR_DRINKING
		([
			([guilt guilty] * [drink ~alcoholic_beverage])
			([drink ~alcoholic_beverage] * ["too much" "too frequently"])
		])
	^factanswer("Never.")

#! Do you ever need an eye opener
	?: DO_YOU_EVER_NEED_AN_EYE_OPENER
		([
			(drink * morning)
			([eye-opener]["eye opener"])
			(<< alcohol [problem trouble] >>)
		])
	^factanswer("I don't think so.")

#! Have you considered reducing your alcohol use
	?: HAVE_YOU_CONSIDERED_REDUCING_ALCOHOL ([think consider need] * [reduce cut decrease] * [drink ~alcoholic_beverage]) ^factanswer("I don't really drink that much.")

#! Ever felt annoyed by people confronting you about your alcohol use
	?: EVER_FELT_ANNOYED_ABOUT_ALCOHOL ([annoy frustrate] * [drink ~alcoholic_beverage])  ^factanswer("Never.")

#! What kind of drinks
	?: WHAT_KIND_OF_DRINKS ([kind type brand] *~3 [alcohol drinks]) ^factanswer("I don't drink.")

#! How many
	?: HOW_MANY_DRINKS (["how many" number] * drinks) ^factanswer("I don't drink.")

#! What about alcohol
	?: WHAT_ABOUT_ALCOHOL (alcohol) ^factanswer("I don't drink.")

# ***************************************************************************************************
# Caffeine use

topic: ~caffeineuse keep repeat (~caffinated_drinks pop soda coke caffeine drink)

	u: ( $$family) ^fail(TOPIC)

#! Do you drink coffee
	?: DO_YOU_DRINK_COFFEE ([~caffinated_drinks caffeine pop~n soda coke]) ^factanswer("I try to avoid caffeine.")

# ***************************************************************************************************
# Diet

topic: ~yourdiet keep repeat (~beverage diet drink consume eat fluid liquid  hydrate fiber dairy "fast food" "junk food" food snack breakfast lunch dinner appetite fruit vegetable dietary full fill salt sodium salty)

	u: ( $$family) ^fail(TOPIC)

#! Do you drink enough fluid
	?: DO_YOU_DRINK_ENOUGH_FLUID
		([
 			([drink consume take] * [fluid water liquid beverage])
 			(hydrate)
			([fluid water liquid beverage] * [drink consume take])
		])
	^factanswer("I think I do.")
		a: HOW_MUCH_FLUID (how [many much dose]) ^factanswer("I drink the recommended amount of 8 glasses per day.")

#! Do you eat enough dairy
	?: DO_YOU_EAT_DAIRY ([dairy milk "ice cream"]) ^factanswer("I get enough dairy.")

#! Do you eat enough fiber
	?: DO_YOU_EAT_FIBER (fiber) ^factanswer("I get enough fiber.")

#! Do you eat fast food
	?: DO_YOU_EAT_FAST_FOOD ([fast junk] food) ^factanswer("I don't go to fast food restaurants very often.")

#! Do you eat healthy food
	?: DO_YOU_EAT_HEALTHY_FOOD ([healthy good nutritious] *~3 [food meals]) ^factanswer("I eat pretty healthy.")

#! Do you eat fruits and vegetables
	?: DO_YOU_EAT_FRUIT ([fruit vegetable]) ^factanswer("I do eat them.")

#! Any changes in your diet
	?: ANY_CHANGES_IN_YOUR_DIET (change * [diet eating]) ^factanswer("My diet is the same.")

#! Tell me about your appetite
	?: APPETITE (!weight appetite) ^factanswer("My appetite is fine.")

#! Do you have dietary restrictions
	?: ANY_DIETARY_RESTRICTIONS (!how [diet dietary] *~3 [restrictions rules]) ^factanswer("I have no restrictions.")

#! Do you feel full quickly
	?: DO_YOU_FEEL_FULL_QUICKLY ([full fill] * [fast quickly]) ^factanswer("I eat fine.")

#! Can you eat
	?: CAN_YOU_EAT ([can able] *~3 eat) ^factanswer("Eating is not a problem.")

#! Have you been eating
	?: HAVE_YOU_BEEN_EATING (![what how why do pain] eat)  ^factanswer("I have been eating.")

#! What snacks do you eat
	?: WHAT_SNACKS (snack) ^factanswer("I don't snack.")

#! What were you eating before
	?: WHAT_WERE_YOU_EATING_BEFORE (eat *~3 before)  ^factanswer("My diet hasn't changed.")

#! What do you eat each day
	?: WHAT_DO_YOU_EAT
		([
			(what * eat)
			([breakfast lunch dinner])
		])
	^factanswer("I try to eat a healthy diet.")

#! How much salt
	?: HOW_MUCH_SALT ([how what 'do] * [salt sodium salty])  ^factanswer("Not too much.")

#! How is your diet
	?: HOW_IS_YOUR_DIET
		([
			(!change !on diet)
 			(!change !on eat * [healthy well])
		])
	^factanswer("My diet is fine.")

# ***************************************************************************************************
# Tobacco Use

topic: ~tobaccouse keep repeat (!~drugs_illegal ~tobacco smoke cigarettes pipe chew smoker quit nicotine secondhand ~spouse ~family_members)

	u: ( $$family) ^fail(TOPIC)

#! How much do you smoke
	?: HOW_MUCH_DO_YOU_SMOKE (how [much many] *~3 [smoke tobacco cigarettes pipe]) ^factanswer("I don't smoke.")

#! How long have you smoked
	?: HOW_LONG_HAVE_YOU_SMOKED (![much did] how [long many] * [smoke smoker]) ^factanswer("I don't smoke.")

#! Have you ever smoked
	?: HAVE_YOU_EVER_SMOKED (<< [ever past previously history before] [smoke tobacco cigarettes pipe smoker] >>)  ^factanswer("I have never smoked.")

#! How long did you smoke
	?: HOW_LONG_DID_YOU_SMOKE (!much how [long many] * [smoke smoker])  ^factanswer("I never smoked in my life.")

#! When did you start smoking
	?: WHEN_DID_YOU_START_SMOKING
		([
			(when * [begin start first] * [smoke smoking cigarette])
			([what how] [old age] * [begin start first] * [smoke smoking cigarette])
		])
	^factanswer("I never started smoking.")

#! Have you quit smoking
	?: HAVE_YOU_QUIT_SMOKING
		([
			([tried attempted] *~3 [quit stop stopped] *~3 [smoking smoke chew chewing])
			([tried attempted] *~2 [quit stop]>)
		])
	^factanswer("I never smoked.")

#! Are you exposed to secondhand smoke
	?: ARE_YOU_EXPOSED_TO_SECONDHAND_SMOKE
		([
			([exposed around near] * [other secondhand] * [smoker smoke])
			([~family_members ~spouse other people] * [smoker smoke])
		])
	^factanswer("I am not around smoke.")

#! Do you smoke
	?: DO_YOU_SMOKE
		([
			([smoke smoker tobacco] >)
			([smoke chew use try] * [tobacco cigarettes pipe nicotine])
			([tobacco cigarettes pipe nicotine] * [smoke chew use try])
		])
	^factanswer("I have never smoked.")

#! Any other tobacco
	?: ANY_OTHER_TOBACCO ([any other] *~3 tobacco)  ^factanswer("I haven't touched tobacco.")

# ***************************************************************************************************
# Education

topic: ~education keep repeat (education school college study major minor)

	u: ( $$family) ^fail(TOPIC)


#! How far did you get in school
	?: HOW_FAR_IN_SCHOOL ([what how] * [education school college]) ^factanswer("I graduated from college")

#! What is your major
	?: WHAT_IS_YOUR_MAJOR (what * [study studying major minor]) ^factanswer("I haven't decided yet.")

# ***************************************************************************************************

Table: ^equivalent() # these are all essential
HAS_THE_PAIN_AFFECTED_YOUR_ACTIVITY  HAS_THE_PAIN_AFFECTED_YOUR_ACTIVITY   # this is the base question - all similar questions map to this base
CAN_YOU_DO_NORMAL_ACTIVITIES HAS_THE_PAIN_AFFECTED_YOUR_ACTIVITY

# In relation to HPI

topic: ~inrelationtohpi keep repeat (~pain work activity afraid limit interfere concern care bother worry trouble life handle handling affect normal normally dress situation feel cook clean household particular laundry clothe housework chore challenge function)

#! How has this affected you
	?: HOW_HAS_THIS_AFFECTED_YOU (!~medicines how * [~pain ~itwords symptoms injury] * [affect influence influenced handle handling interfere impact]) ^factanswer("It hasn't affected me that much.")

#! Can you do normal activities
	?: CAN_YOU_DO_NORMAL_ACTIVITIES
		([
			([day daily limit normal difficult] * [activity routine things])
			([activity routine things] * [day daily])
			([activity routine things activities routines chores] * [impacted affected 'do challenge])
			([impacted affected restrict difficult able can] * [activity routine things activities routines function])
			(daily [activity function])
		])
	^factanswer("I can do normal activities.")

#! Has the pain affected your activity
#! Has this impacted your life outside of work?
	?: HAS_THE_PAIN_AFFECTED_YOUR_ACTIVITY
		([
			(![better worse ~bodyparts how work]  ~the_pain [affect inhibit impact influence alter interfere change hard] * [activity routine function functioning life])
			(![better worse ~bodyparts how work] [activity routine function functioning] * [~pain ~itwords injury])
			(![better worse ~bodyparts how work] [~the_pain injury] * [affect inhibit impact influence alter interfere change hard] * [activity routine function functioning life])
		])
	^factanswer("Not really.")

#! Has the pain affected you
		?: HAS_THE_PAIN_AFFECTED_YOU
			([
				(![~bodyparts how ~job] [~pain ~itwords injury] * [affect inhibit impact influence alter interfere change hard] * you)
 				(![better worse ~bodyparts how ~job] [affect inhibit impact influence alter interfere change] * [~pain ~itwords injury])
			])
		^factanswer("Not really.")

#! How have you been handling this
	?: HOW_HAVE_YOU_BEEN_HANDLING_THIS
		([
			([handle handling] *~3 [~pain ~itwords situation])
			([~pain ~itwords situation] * [handle handling])
		])
	^factanswer("Just fine.")

#! What bothers you the most
	?: WHAT_BOTHERS_YOU_THE_MOST
		([
			([bother worry trouble concern] * [particular most])
			([big large main most] * [afraid worry concern worried])
		])
	factanswer("Nothing bothers me.")

#! Can you care for yourself
	?: CAN_YOU_CARE_FOR_YOURSELF
		([
			([care dress] * yourself)
			([problems issues trouble difficult] * [care dress])
			([can able] * [cook clean household laundry clothe housework])
		])
	^factanswer("I can care for myself.")

#! Does this bother you mentally
	?: DOES_THIS_BOTHER_YOU_MENTALLY ([feel make cause] * [bother worry depress "mental health"]) ^factanswer("I don't think so.")

# ***************************************************************************************************
# Recreational drug use

topic: ~druguse keep repeat (~drugs_illegal "bath salts" pills sure recreational drug illicit illegal street experiment use)

	u: ( $$family) ^fail(TOPIC)

#! Do you use illegal drugs
	?: DO_YOU_USE_ILLEGAL_DRUGS
		([
			(!other !ever [use take do indulge] * [drugs substances]>)
			(![taste reflux feel ever] [~drugs_illegal "pcp" MDMA Adam])
			(!ever ["bath salts" bathsalts])
 			(!ever ["recreational drug" "street drug" "drug use" "IV drugs" "intraveous drugs"])
			(<< !ever drug [recreationally recreational illicit illegal] >>)
 			(!ever [used experiment] * [~drugs_illegal drug])
			(!ever illegal)
			([drug ~drugs_illegal substance] *~3 use)
		])
	^factanswer("Never.")
		a: ARE_YOU_SURE (are you [sure certain])  ^factanswer("I am sure.")

#! Have you ever used illegal drugs
		?: HAVE_YOU_EVER_USED_ILLEGAL_DRUGS
			([
				([use take do indulge experiment] *~3 [drugs substances] {ever} >)
				(![taste reflux feel] [~drugs_illegal "pcp" MDMA Adam])
				(["bath salts" bathsalts])
				(["recreational drug" "street drug" "drug use" "IV drugs" "intraveous drugs"])
				(<< drug [recreationally recreational illicit illegal] >>)
			])
		^factanswer("I have never used any illegal drugs.")

# ***************************************************************************************************
# Tattoos

topic: ~tattoo keep repeat (tattoo "body art")

	u: ( $$family) ^fail(TOPIC)

#! Do you have a tattoo
	?: DO_YOU_HAVE_A_TATTOO (do you * [tattoo "body art"]) ^factanswer("I have no tattoos.")

#! Have you ever had a tattoo
	?: EVER_HAD_A_TATTOO (ever * [tattoo "body art"]) ^factanswer("I have never had a tattoo.")

# ***************************************************************************************************
# Travel

topic: ~travel keep repeat (travel visit country plane airplane trip)

#! Have you traveled anywhere
	?: HAVE_YOU_TRAVELED_ANYWHERE ([have did recent recently] * [travel travels country trip]) ^factanswer("I have not traveled.")

#! Have you been on a plane
	?: HAVE_YOU_BEEN_ON_A_PLANE
		([
			([have did recent recently] * [plane airplane])
			([plane airplane] * [recently "not long ago" ~timename])
		])
	^factanswer("I haven't been on a plane.")

# ***************************************************************************************************

# Hobby

topic: ~hobbyquestions keep repeat (hobby fun outside work job enjoy "free time" "extracurricular activity" relax outside activities "spare time" "daily basis" "like to do" interests "social life" "personal life")

	u: ( $$family) ^fail(TOPIC)

#! Do you have any hobbies
	?: DO_YOU_HAVE_ANY_HOBBIES
		([
			(hobby)
			(![daily normal] [activity hobby] * [enjoy like participate])
			(![daily normal] [enjoy like participate 'do] * [activity hobby])
		])
	 ^factanswer("I don't have any specific hobbies.")

#! What do you do for fun
#! Are you involved with groups outside of work
	?: WHAT_DO_YOU_DO_FOR_FUN
		([
			([what how anything] * [enjoy enjoyment fun relax "free time" entertainment excitement "extracurricular activity" "spare time" "free time" "daily basis" "like to do" interests])
			([what anything group] * [outside when after besides] * [work job])
			([outside when after besides] * [work job] * [what anything group])
		])
	^factanswer("I do normal stuff like reading and going to a movie.")

	#! How is your social Life
		?: HOW_IS_YOUR_SOCIAL_LIFE ("social life") ^factanswer("My social life is fine.")

	#! How is your personal Life
		?: HOW_IS_YOUR_PERSONAL_LIFE ("personal life") ^factanswer("My personal life is fine.")

# ***************************************************************************************************
# Sexual Activity

topic: ~sexualactivity keep repeat (sex sexual sexually sexuality intercourse men women partner monogamous problems)

	u: ( $$family) ^fail(TOPIC)

#! How many sexual partners ever
#! How many life-time sexual partners have you had
	?: HOW_MANY_SEXUAL_PARTNERS_EVER
		([
			([many number several one] * [partner men women] * [past ever life life-time])
			([many number several one] * [life life-time] * [partner men women])
			([before previously past] * [many number several] * [partner men women] * [past ever life life-time])
		])
	^factanswer("I am not sure. Not very many.")

#! How many sexual partners
	?: HOW_MANY_SEXUAL_PARTNERS
		([
			([many number several one] * [partner men women])
			([before previously past] * [many number several] * [partner men women])
			([many number several any] * [partner men women] * [past last] * [recent recently year month])
	 		([past last recent recently year month] * [many number several] * [partner men women])
		])
	^factanswer("None.")

#! Are you monogamous
	?: MONOGAMOUS (monogamous)	^factanswer("Yes.")

#! Any other sexual partners
	?: ANY_OTHER_SEXUAL_PARTNERS
		([
			([new other additional extra] * [sex sexual] * [partner men women])
			(other * [sex sexual] * [~spouse])
			(other * [partner men women])
			([sex sexual] * [other besides] * [~spouse])
		])
	^factanswer("I'm not sexually active.")

#! Do you prefer men or women
	?: DO_YOU_PREFER_MEN_OR_WOMEN
		([
			([sex prefer like interested sexual sexually engage] * [male man men woman women female])
	 		(sexual * [orientation preference])
	 		([men women] *~2 or both)
		])
	^factanswer("I'm not sexually active.")

#! Who are your sexual partners
	?: WHO_ARE_YOUR_SEXUAL_PARTNERS
		([
			([who whom] * [sex sexual sexually intercourse])
			(partner * [man male woman female ~dating_friend])
 			([sex sexual sexually intercourse] * [man men male woman women female ~dating_friend])
		])
	^factanswer("I'm not sexually active.")

#! Are you considering being sexually active
		?: ARE_YOU_CONSIDERING_BEING_SEXUALLY_ACTIVE (be * [consider considering contemplate contemplating plan planning think thinking] * [sex sexually]) ^factanswer("Not really.")

#! Are you sexually active
		?: ARE_YOU_SEXUALLY_ACTIVE
			([
				(![ever past previously when why how problem difficulty who ~STI unprotected symptoms force coerce pain] [are is have] * [sex sexual sexually intercourse "sexually active"])
			])
	^factanswer("I'm not sexually active.")
		a: EVER_BEEN_SEXUALLY_ACTIVE ([ever before past prior previously]) ^reuse(~pastsex.EVER_BEEN_SEXUALLY_ACTIVE)
		a: HOW_OFTEN_DO_YOU_HAVE_SEX ([often frequent regularly frequently]) ^reuse(HOW_OFTEN_DO_YOU_HAVE_SEX)

# #! Have you ever been sexually active this is in ~pastsex in PastGYNSymptoms - need to validate
#	?: EVER_BEEN_SEXUALLY_ACTIVE
#		([
#			(![when why how problem difficulty who ~STI unprotected symptoms force coerce pain] [are is have] * [sex sexual sexually intercourse "sexually active"])
#		])
# ^factanswer("I have never been sexually active.")

#! When was the last time you had intercourse
	?: WHEN_DID_YOU_LAST_HAVE_INTERCOURSE
		([
	 		([when "long ago"] * [last previous] * [sex sexual sexually intercourse ~itwords])
	 		([when "long ago"] * [sex sexual sexually intercourse ~itwords] * [last previous before])
		])
	^factanswer("I'm not sexually active.")

#! Tell me about your sexual history
#! How about your sexual activity
	u: TELL_ME_ABOUT_YOUR_SEXUAL_HISTORY
		([
			([tell describe] * sexual [history activity])
			(how about * sexual [history activity])
			([tell describe] * sexuality)
		])
		 ^factanswer("What do you want to know?")

#! Any sexual problems
	?: ANY_SEXUAL_PROBLEMS
		([
			([sex sexual] [problem issue concern])
			([problem issue concern] * [with in about concerning] *~3 [sex sexual])
		])
		^factanswer("I have no concerns about that.")

#! How often do you have sex
	?: HOW_OFTEN_DO_YOU_HAVE_SEX
		([
			(how [often frequent regularly frequently] * [sex sexual sexually intercourse])
			(do you *~3 [sex sexual sexually intercourse] * [often frequent regularly frequently])
		])
		^factanswer("I am not currenlty sexually active.")

# ***************************************************************************************************
# Religion

topic: ~religion keep repeat (religion denomination church spiritual religious)

	u: ( $$family) ^fail(TOPIC)


#! What is your religious preference
	?: WHAT_IS_YOUR_RELIGIOUS_PREFERENCE  ([religion religious spiritual denomination church])  ^factanswer("I don't have a preference.")

# ***************************************************************************************************
# Advanced Directive

topic: ~advanced_directive keep repeat ("living will" "end of life" "advanced directive" "power of attorney" DNR CPR "do not resuscitate" ventilator)

	u: ( $$family) ^fail(TOPIC)

#! What is in your living will
	?: WHAT_IS_IN_YOUR_LIVING_WILL
		([
			([how what] * [contain cover] * ["living will"])
			([consider want thought] * [DNR "do not resuscitate" CPR ventilator die dying])
		])
	^factanswer("I don't have one.")

#! Do you have a living will
	?: DO_YOU_HAVE_A_LIVING_WILL ("living will") ^factanswer("I do not have one of those.")

#! Do you have an advanced directive
	?: DO_YOU_HAVE_AN_ADVANCED_DIRECTIVE  (["advanced directive" "end of life"]) ^factanswer("I do not.")

#! Do you have a medical power of attorney
	?: DO_YOU_HAVE_A_MEDICAL_POWER_OF_ATTORNEY  (<< [proxy agent "power of attorney"] [medical health care] >>)  ^factanswer("I have none.")

# ***************************************************************************************************
# General Health

topic: ~generalhealth keep repeat (health healthy quality everyday "social history" sick ill "feeling poorly" "under the weather" feel)

	u: ( $$family) ^fail(TOPIC)

#! Were you healthy
	?: WERE_YOU_HEALTHY_CHILD (!are !diet [healthy sick ill health] * [past childhood child])   ^factanswer("I have always been healthy.")

#! Are you healthy
	?: ARE_YOU_HEALTHY
		([
			(![diet food eat mental] [do be] *~3 you * [health healthy])
		])
		^factanswer("I am pretty healthy.")

#! Do you feel well
	?: DO_YOU_FEEL_WELL
		([
			(feel * [well OK allright])
			(["are you" "you are"] * [well OK allright okay ok alright])
		])
	^factanswer("I feel fine.")

#! What is your quality of life
	?: WHAT_IS_QUALITY_OF_LIFE (what * [current everyday daily quality] * life) ^factanswer("It is good.")

#! Social history
	?: SOCIAL_HISTORY (social history) ^factanswer("OK")

topic: ~feelingquestions keep repeat (frustrate nervous )

	#! Are you frustrated
		?: ARE_YOU_FRUSTRATED ('frustrated)  ^factanswer("Not really.")

	#! Are you nervous
		?: ARE_YOU_NERVOUS ([nervous worried scared worries]) ^factanswer("Not really.")
