# HOPI This is the History of present illness topic file

topic: ~chiefcomplaint  keep repeat ()

# u: ($complaint2?) respond(~complaint2) ^fail(TOPIC)
# u: ($complaint3?) respond(~complaint3) ^fail(TOPIC)


# Onset  ***************

#!!P Did anything happen to cause the 1 $chiefcomplaint=1
u: DID_ANYTHING_HAPPEN_TO_CAUSE_THE_1
	([
		(!think [remember happen situation what did notice anything] *~3 ["bring on" caused cause trigger set specific new change] *~3 [~itwords $chiefcomplaint? symptoms life world])
		([describe discuss elaborate] *~3 [happen situation])
	])
	^factanswer("It is difficult to say.")

#!!P How did the 1 start $chiefcomplaint=1
u: HOW_DID_THE_1_START
	([
		(!["how long" when exacerbate "how much"] how *~3 [~itwords $chiefcomplaint? symptoms] *~3 [start begin occur happen brought])
		(!cause !causing ![does has] [what how] *~3 [precipitate start] *~3 [~itwords $chiefcomplaint?])
		(what * [notice felt] *~3 [~itwords $chiefcomplaint?])
		([~itwords $chiefcomplaint?] *~3 [start begin] *~3 [activity trauma physical death])
	])
	^factanswer("I am not sure.")

#!!P Were you doing anything when the 1 started $chiefcomplaint=1
#! What happened
u: DOING_ANYTHING_WHEN_THE_1_STARTED
	([
		(![when else] do *~3 [~itwords $chiefcomplaint?] *~3 [start began] >)
		(![else try] do *~3 felt *~3 [~itwords $chiefcomplaint?])
		(![avoid exacerbate] [brought bring] *~3 [~itwords $chiefcomplaint?] *~3 on)
		(![avoid exacerbate] [happening doing] *~3 [brought bring when] *~3 [~itwords $chiefcomplaint?])
		(~ ![exacerbate avoid injure away treatment] what *~3 [doing happened]) # will only match if already in topic
	])
	^factanswer("I am not sure.")

#!!P Was the onset sudden1 $chiefcomplaint=1
u: WAS_THE_ONSET_SUDDEN1
	([
		(<< onset [sudden abrupt] >>)
		([~itwords $chiefcomplaint? symptoms] *~2 [immediate sudden abrupt instant instantaneous gradual gradually])
	])
	^factanswer("I am not sure.")

#!!P Did the 1 start immediately $chiefcomplaint=1
u: DID_THE_1_START_IMMEDIATELY (!when [~itwords $chiefcomplaint?] * [start begin hurt happen "come on"] * [immediately "right away" "right after" "straight away" suddenly abruptly])   ^factanswer("It is difficult to say.")

#!!P Is the 1 new $chiefcomplaint=1
u: IS_THE_1_NEW
	([
		(!~medicines is *[~itwords $chiefcomplaint?] * [new novel])
		(!~medicines is * [new novel] * [~itwords $chiefcomplaint?])
	])
	^factanswer("It is difficult to say.")

#!!P Is this the first time you have had this 1 $chiefcomplaint=1
u: IS_THIS_THE_FIRST_TIME_FOR_1 (first time * [~itwords $chiefcomplaint?])   ^factanswer("It is difficult to say.")

#!!P When did the 1 start $chiefcomplaint=1
u: WHEN_DID_THE_1_START
	(!"before we" # want to avoid "before we start, is there ....."
		[
		(![what night 'do pregnant frequently describe] [when "how long ago"] *~3 [~itwords $chiefcomplaint?] *~3 [start happen begin occur]) # when only adverb not conjunction to avoid how bad was it when it first started
		(![what night 'do pregnant frequently describe] [when "how long ago"] *~3 [start happen begin hurt] *~3 [~itwords $chiefcomplaint?])
		(![what night 'do pregnant describe] [when "how long ago"] *~3 [feel experience notice] *~3 [~itwords $chiefcomplaint? symptom problem])
 		(![pregnant describe] [when "how long ago"] * [onset start this] >)
		(![much bad worse better] ["how long ago" ago] >)
 		(![much bad worse better] [start begin notice onset ago] * ~timename) # avoid notice it getting worse throughout the day
 		(![much bad worse better] ~timename * [start begin notice onset ago "so far"])
	])
	^factanswer("I am not sure.")

#!!P How has the 1 changed over time $chiefcomplaint=1
u: HOW_HAS_THE_1_CHANGED
	([
		(![when what rest does ~position anything] how *~3 [~itwords $chiefcomplaint?] *~3 [progress progressed worse better increase change] >)
		(![when what rest does ~position anything] how *~3 [~itwords $chiefcomplaint?] *~3 [progress progressed worse better increase change been] *~3 [time day year])
		(![when what rest does ~position anything] how *~3 [~itwords $chiefcomplaint?] *~3 since * [start begin then first])
	])
	^factanswer("I am not sure.")

#!!P How long have you had the 1 $chiefcomplaint=1
u: HOW_LONG_HAVE_YOU_HAD_1
	([
		(![last take] how [long "many days"] *~3 [had been having experience felt notice need] *~3 [~itwords $chiefcomplaint?])
		(how *~3 [weeks days] *~3 [~itwords $chiefcomplaint?])
		(![last does] how long *~3 [~itwords $chiefcomplaint?] *~3 [hurt going happen occur bother]) # want to avoid for how long does it usually hurt
		(![were does similar had take severe] ["how long"] *~3 [~itwords $chiefcomplaint?] *~3 [start "been there" "been a problem"])
	])
	^factanswer("I am not sure.")

#!!P Is the 1 constant $chiefcomplaint=1
#! Did the pain increase over time
#! Does it hurt more at a certain time of day
u: IS_THE_1_CONSTANT # u: does not work. Also have IS_THE_PAIN_CONSTANT in ~CurrentPainSymptoms
	([
		(![intensity severity] [~itwords $chiefcomplaint?] *~3 ["come and go" consistent increase decrease fluctuate change always sometimes constant constantly intermittent "throughout the day" "always there" "always present" "all the time" "wax and wane" "ever go away" "get better" "get worse" continuous "all day" ongoing persist])
		(![intensity severity] [~itwords $chiefcomplaint?] *~3 [change fluctuate] *~3 [severity intensity])
		(["come and go" consistent always sometimes constant constantly intermittent "throughout the day" "always there" "always present" "all the time" "wax and wane" "ever go away" "ever get better" continuous "all day" ongoing change] *~3 [~itwords $chiefcomplaint?])
	])
	^factanswer("It is difficult to say.")

#!!P Do you have a cause for the 1 $chiefcomplaint=1
u: DO_YOU_HAVE_A_CAUSE_FOR_1
	([
		([cause reason why] * [~itwords $chiefcomplaint? ~pain])
		([thought think idea] * [~itwords $chiefcomplaint?] * [cause reason])
		([thought think idea] * [cause reason] * [~itwords $chiefcomplaint?])
	])
	^factanswer("I am not sure.")

# Exacerbation  *********

#!!P What makes the 1 worse $chiefcomplaint=1
u: WHAT_MAKES_THE_1_WORSE
	([
		(!avoid [what anything "any thing" seems] * [~itwords $chiefcomplaint?] *~3 [worse increase worsen more hard harder])
		(!avoid [what anything "any thing"] [aggravate exacerbate worsen more affect increase] *~3 [~itwords $chiefcomplaint?])
		(!avoid any *~3 [exacerbate exacerbating])
	])
	^factanswer("I am not sure.")

#!!P Does anything change the intensity of the 1 $chiefcomplaint=1
u: DOES_ANYTHING_CHANGE_1_INTENSITY
	([
		([what anything] *~3 change *~3 [intensity severity] *~3 [~itwords $chiefcomplaint?])
		([what anything] *~3 change *~3 [~itwords $chiefcomplaint?] *~3 [intense intensity severity])
	])
	^factanswer("It is difficult to say.")

#!!P What have you tried for the 1 $chiefcomplaint=1
u: WHAT_HAVE_YOU_TRIED_FOR_THE_1
	([
		(![dose dosage else how] what [have are] *~3 [try take taken use attempt done doing 'do] *~3 [~itwords $chiefcomplaint?])
		(![dose dosage else how] what [have are] *~3 [try take taken use attempt done] >)
	])
	^factanswer("I have not tried anything.")
		a: ([dosage dose "how much"]) ^reuse (~currentmedications.HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)
		a: (did that [work help]) It helped a little, but didn't make the pain go away

#!!P Have you tried anything for the 1 $chiefcomplaint=1
#! have you tried anything at home
u: HAVE_YOU_TRIED_ANYTHING_FOR_THE_1
	([
		(![else other how need ~medical_treatments ~medicines we work ever when] [try attempt take use] * [~itwords $chiefcomplaint?])
		(![else other ~medical_treatments ~medicines we work ever when] [try attempt take use] * home)
		(![else other avoid exacerbate start hurt ~medical_treatments ~medicines we work ever when] [doing done use] * [~itwords $chiefcomplaint?])
		(![else other how need ~medical_treatments ~medicines we work ever when] [try attempt take used] * [help relieve] * [~itwords $chiefcomplaint?])
		(![else ~medical_treatments ~medicines we ever when] [did have] *~2 [do try] *~2 anything >)
		(![else ~medical_treatments ~medicines we ever when] [did have] *~2 [do try] *~2 anything * [symptoms $chiefcomplaint? ~itwords problem])
	])
	^factanswer("I have not tried anything.")
	a: ([dosage dose "how much"]) ^reuse (~currentmedications.HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)
	a: (["did that" "how much"] * [work help ]) It helped a little but didn't make the pain go away.
	a: WHEN_1 (when) ^reuse(~currentmedications.WHEN_DID_YOU_TAKE_MEDICINE)

#!!P What makes the 1 better $chiefcomplaint=1
u: WHAT_MAKES_THE_1_BETTER
	([
		(!else [what anything] *~3 [$chiefcomplaint? ~itwords] *~3 [less better "hurt less""less painful" "go away" improve])
		(!else [$chiefcomplaint? ~itwords] *~3 [less better "hurt less""less painful" "go away" improve] *~3 anything)
		(!else [what anything] *~3 [help alleviate reduce lessen decrease improve relieve relief less ease] *~3 [$chiefcomplaint? ~itwords])
		(!else [what anything] *~3 [help alleviate reduce lessen decrease improve relieve relief less ease])
	])
	^factanswer("It is difficult to say.")
		a: ([dosage dose "how much"]) ^reuse (~currentmedications.HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)

#!!P Does anything else help the 1 $chiefcomplaint=1
u: DOES_ANYTHING_ELSE_HELP_THE_1
	([
		([what anything] else *~3 [help alleviate reduce lessen decrease] *~3 [~itwords $chiefcomplaint?])
		([what anything] else *~3 [~itwords $chiefcomplaint?] *~3 [help alleviate reduce lessen decrease better])
	])
	^factanswer("I have not tried anything.")

#!!K Have you tried anything else for the 1 $chiefcomplaint=1
u: HAVE_YOU_TRIED_ANYTHING_ELSE_FOR_1 ([try take] *~3 else *~3 [~itwords $chiefcomplaint?])  ^factanswer("I have not tried anything.")

#!!K Have you tried anything else1 $chiefcomplaint=1
u: HAVE_YOU_TRIED_ANYTHING_ELSE1 (!take try *~3 [besides else])  ^factanswer("I have not tried anything.")

#!!K Have you tried any other methods1 $chiefcomplaint=1
u: HAVE_YOU_TRIED_ANY_OTHER_METHODS1 (try *~3 other *~3 [method approach treatment])   ^factanswer("I have not tried anything.")

#!!P What else have you tried for the 1 $chiefcomplaint=1
u: WHAT_ELSE_HAVE_YOU_TRIED_FOR_1 (<< [else other] [try attempt] [~itwords $chiefcomplaint?] >>) ^factanswer("I have not tried anything.")

#!!K What else have you tried1 $chiefcomplaint=1
u: WHAT_ELSE_HAVE_YOU_TRIED1 (![take ~medicines] << [else other] [try attempt] >>) ^factanswer("I have not tried anything.")

#!!P Does moving increase the 1 $chiefcomplaint=1
u: DOES_MOVING_INCREASE_THE_1
	([
		([activity move active] * [increase worse aggravate enhance] * [~itwords $chiefcomplaint?])
		([activity move active]  * [~itwords $chiefcomplaint?] * [increase worse])
		(![not rest] [~itwords $chiefcomplaint?] * [increase worse hurt] * [move movement activity active])
	])
	^factanswer("It is difficult to say.")

#!!P Does any position relieve the 1 $chiefcomplaint=1
u: DOES_ANY_POSITION_RELIEVE_THE_1
	([
		(!worse !anything position *~3 relieve *~3 [~itwords $chiefcomplaint?])
		(!worse !anything position *~3 [~itwords $chiefcomplaint?] *~3 [better "hurt less"])
		(!worse !anything position *~3 [better improve comfortable])
		(!worse !anything [~itwords $chiefcomplaint?] *~3 [better improve] *~3 position)
		(!worse !anything [better improve help] *~3 [~itwords $chiefcomplaint?] *~3 [move position])
	])
	^factanswer("It is difficult to say.")

#!!P Does any position make the 1 worse $chiefcomplaint=1
u: DOES_ANY_POSITION_MAKE_THE_1_WORSE
	([
		(!better !anything position *~4 [increase worsen aggravate enhance] [~itwords $chiefcomplaint?])
		(!better !anything position *~4 [~itwords $chiefcomplaint?] *~3 [worse increase])
	])
	^factanswer("It is difficult to say.")

#!!P Do any positions make 1 worse or better $chiefcomplaint=1
u: DO_ANY_POSITIONS_MAKE_1_WORSE_OR_BETTER (!anything [thing position] *~4 [~itwords $chiefcomplaint?] *~3 [better worse intensity severity most])   ^factanswer("It is difficult to say.")

#!!P Can you move around with the 1 $chiefcomplaint=1
u: CAN_YOU_MOVE_AROUND_WITH_THE_1
	([
		(!~bodyparts move * [~itwords $chiefcomplaint?])
		(!~bodyparts [able "can you"] * [around move] * [~itwords $chiefcomplaint?])
	])
	^factanswer("It is difficult to say.")

#!!P Is 1 worse when you lie down $chiefcomplaint=1
u: IS_1_WORSE_WHEN_YOU_LIE_DOWN (<< [~itwords $chiefcomplaint?] [hurt worse] ["lie down" "lying down" "lay down" rest] >>)     ^factanswer("It is difficult to say.")

#!!P Do you have 1 when you lie down $chiefcomplaint=1
u: DO_YOU_HAVE_1_WHEN_YOU_LIE_DOWN (![better less hurt feel] << [~itwords $chiefcomplaint?] ["lie down" "lying down" rest] >>)    ^factanswer("it is difficult to say.")

#!!P Is 1 better when you lie down $chiefcomplaint=1
u: IS_1_BETTER_WHEN_YOU_LIE_DOWN
	([
		([~itwords $chiefcomplaint?] * [improve better] * ["lay down" "lie down" "lying down" rest])
		([better help comfortable] * ["lay down" "lie down" "lying down" rest])
		(["lay down" "lie down" "lying down" rest] * [better help comfortable])
	])
	^factanswer("It is difficult to say.")

#!!P Does 1 increase when you stand $chiefcomplaint=1
u: DOES_1_INCREASE_WHEN_YOU_STAND (<< [~itwords $chiefcomplaint?] [increase worse] stand >>) ^factanswer("I am not sure.")

#!!P Does the 1 improve with exercise $chiefcomplaint=1
u: DOES_THE_1_IMPROVE_WITH_EXERCISE (<< [~itwords $chiefcomplaint?] [improve better worse] [exercise sitting standing walking moving] >>) ^factanswer("I am not sure.")

#!!P Does the 1 keep you up at night $chiefcomplaint=1
u: DOES_THE_1_KEEP_YOU_UP_AT_NIGHT
	([
		(!worse !better [~itwords $chiefcomplaint? symptom] * [wake bed night sleep asleep sleeping])
		(!worse !better [~itwords $chiefcomplaint? symptom] * [wake keep] * [night bed sleep asleep])
		(!worse !better [wake bed night sleep asleep sleeping] * [~itwords $chiefcomplaint? symptom])
	])
	^factanswer("Not that I am aware of.")

#!!P Is the 1 worse in the morning or at night $chiefcomplaint=1
u: 1_WORSE_MORNING_OR_NIGHT
	([
		([~itwords $chiefcomplaint? symptoms] *~3 [worse better hurt painful worst more easier tolerable tolerate] *~3 [morning night evening "time of day" "time in the day" "times of the day" "day goes on"])
		(!"trouble getting" [~itwords $chiefcomplaint? symptoms] *~3 [morning night evening "time of day" "time in the day"])
 		([morning night "time of day" evening "time in the day" "day goes on"] *~3 [~itwords $chiefcomplaint? symptoms] *~3 [tolerable worse better hurt painful worst more])
	])
	^factanswer("It is difficult to say.")

#!!P What have you been doing to avoid exacerbating the 1 $chiefcomplaint=1
u: AVOID_EXACERBATE_1 (avoid * [~itwords $chiefcomplaint?])   ^factanswer("I am not sure.")

#!!P Is the 1 improving $chiefcomplaint=1
u: IS_THE_1_IMPROVING
	([
		(![intensity severity] [~itwords $chiefcomplaint?] *~3 ["getting better" "getting worse" worsening "the same" improve worsened "gotten worse"])
		(![intensity severity] [~itwords $chiefcomplaint?] *~3 the same)
	])
	^factanswer("It is difficult to say.")

#!!P Does the 1 get worse with your period $chiefcomplaint=1
u: DOES_THE_1_GET_WORSE_WITH_PERIOD (<< [~itwords $chiefcomplaint?] [worse worsen increase] [~gyn_bleeding bleeding vaginal] >>)   ^factanswer("It is difficult to say.")

# Intensity  ***************

#!!P Is 1 more intense  $chiefcomplaint=1
u: IS_1_MORE_INTENSE  ([~itwords $chiefcomplaint?] *~2 [intense severe higher greater worse better] *~3 [$complaint2? $complaint3?])  ^factanswer("it is difficult to say.")

#!!P Has the 1 become worse or better $chiefcomplaint=1
u: HAS_THE_1_BECOME_WORSE
	([
		([have has] *~3 [~itwords $chiefcomplaint? symptoms] *~3 [worse better increase decrease change])
		([have has] *~3 [~itwords $chiefcomplaint? symptoms] *~3 [worse better increase decrease change] *~3 [time ~timename all])
		([~itwords $chiefcomplaint? symptoms] *~3 [has have] *~3 [worse better increase decrease change] *~3 [time ~timename all])
	])
	^factanswer("I am not sure.")

#!!P How intense is the 1 $chiefcomplaint=1
u: HOW_INTENSE_IS_THE_1
	([
		(![$currentmedication1? scale 1 one 10 ten rate ~job when] [can how what] * [severe intense 'much 'bad badly sore level painful] * [~itwords $chiefcomplaint? symptoms])
		(![$currentmedication1? scale 1 one 10 ten rate ~job when] [~itwords $chiefcomplaint? symptoms] * [severe intense 'much 'bad badly sore level painful])
	])
	^factanswer("I am not sure.")
		a: THAT_MUST_BE_STRESSFUL ([stressful awful terrible]) It is.

#!!P Has the intensity1 changed $chiefcomplaint=1
u: HAS_THE_INTENSITY1_CHANGED
	([
		(<< intensity change >>)
		([intensity severity] *~4 [change same])
		([change same] *~3 [intensity severity])
	])
	^factanswer("I am not sure.")

#!!P When is the 1 most severe $chiefcomplaint=1
u: WHEN_IS_THE_1_MOST_SEVERE
	([
		(when *~3 [~itwords $chiefcomplaint?] *~3 [worst worse severe painful most])
		([severity intensity painfulness] *~3 [~itwords $chiefcomplaint?] *~3 ~timename)
	])
	^factanswer("I am not sure.")

#!!P When is the 1 most severe $chiefcomplaint=1
u: WHEN_IS_THE_1_BETTER
	([
		(when *~3 [~itwords $chiefcomplaint?] *~3 better)
	])
	^factanswer("It is difficult to say.")

#! How bad was it when it started
#! Was it painful initially
u: HOW_BAD_WAS_IT_WHEN_IT_STARTED
	([
		([bad severe painful] * when * [began started happened])
		(<< [bad severe painful] [initially "right away"] >>)
	])
	^factanswer("It is difficult to say.")

# General ********************

#!!P Do you currently have 1 $chiefcomplaint=1
u: DO_YOU_CURRENTLY_HAVE_1 (![how what before] [current currently now moment] *~3b [~itwords $chiefcomplaint?]) ^factanswer("Not that I am aware of.")

#!!P How is your 1 now $chiefcomplaint=1
u: HOW_IS_YOUR_1_NOW (![rate scale] [what how] *~3 [~itwords $chiefcomplaint?] *~3 [now currently moment]) ^factanswer("Not that I am aware of.")

#!!P Are you having any other 1 $chiefcomplaint=1
u: ARE_YOU_HAVING_ANY_OTHER_1
	([
		(![~medicines does] [just other] * $chiefcomplaint?)
		#	(![~medicines does] $chiefcomplaint? * [other any]) # want to avoid does the pain affect any other part of your body
	])
	^factanswer("Not that I am aware of.")

#!!P How else is the 1 affecting your life $chiefcomplaint=1
u: HOW_ELSE_IS_THE_1_AFFECTING_YOUR_LIFE (how else *~3 [~itwords $chiefcomplaint?] [affect effect]) ^factanswer("It is difficult to say.")

#!!P Has the 1 stopped you from doing anything $chiefcomplaint=1
u: HAS_THE_1_STOPPED_YOU (has * [~itwords $chiefcomplaint?] * [stopped kept] you from doing) ^factanswer("Not that I am aware of.")

#!!P Have you had 1 before $chiefcomplaint=1
u: HAVE_YOU_HAD_1_BEFORE
	([
		(![when worse surgery accident how medical family they them rate use try tried] [~itwords $chiefcomplaint?] * [before prior past history ever previously])
		(![when worse surgery accident how medical family they them rate use try tred] [prior past history ever had] * [~itwords $chiefcomplaint?])
	])
	^factanswer("Not that I am aware of.")

#!!P Does the 1 affect your family
u: DOES_THE_1_AFFECT_YOUR_FAMILY ([~itwords $chiefcomplaint?] *~3 [affect effect] *~3 family) ^factanswer("Not that I am aware of.")

# Description  ***************

#!!P Has the 1 changed  $chiefcomplaint=1
u: HAS_THE_1_CHANGED (!how has *~3 [~itwords $chiefcomplaint?] *~3 [change "better or worse" "worse or better"]) ^factanswer("Not that I am aware of.")

#! Have you noticed any physical changes   $chiefcomplaint=1
u: ANY_PHYSICAL_CHANGES_IN_YOUR_1
	([
		([notice see look any] *~3 [~skin_changes ~physical_changes])
		([~itwords $chiefcomplaint?] * [~skin_changes ~physical_changes])
	])
	^factanswer("Not that I am aware of.")

#!!P When do you have the 1 $chiefcomplaint=1
u: WHEN_DO_YOU_HAVE_THE_1 (when *~3 [have notice get feel hurt] *~3 [~itwords $chiefcomplaint?]) ^factanswer("I am not sure.")

#!!P When else do you have the 1 $chiefcomplaint=1
u: WHEN_ELSE_DO_YOU_HAVE_THE_1
	([
		(when else *~3 [have notice get feel] * [~itwords $chiefcomplaint?])
		(when else *~3 [~itwords $chiefcomplaint?])
	])
^factanswer("I am not sure.")

#!!P How long does the 1 last $chiefcomplaint=1
u: HOW_LONG_DOES_THE_1_LAST (how long *~3 [~itwords $chiefcomplaint?] *~3 last) ^factanswer("It is difficult to say.")

#!!P How frequent is the 1 $chiefcomplaint=1
u: HOW_FREQUENT_IS_THE_1 (!take !use << [frequent frequently often] [~itwords $chiefcomplaint?] >>) ^factanswer("It is difficult to say.")

#!!P What concerns you about the 1 $chiefcomplaint=1
u: WHAT_CONCERNS_YOU_ABOUT_THE_1
	([
		(![else other beside besides aside] [concern worry bother] * [~itwords $chiefcomplaint?]) # want to avoid what concerns you besides the back pain
		(![else other beside besides aside] [~itwords $chiefcomplaint?] * [concern worry bother])
	])
	^factanswer("I am not sure.")

#!!P Where is the 1 $chiefcomplaint=1
u: WHERE_IS_THE_1
	([
		(!scale where *~3 [~itwords $chiefcomplaint?])
		(!scale where *~3 [~itwords $chiefcomplaint?] *~3 [located location feel hurt])
		(!scale where *~3 [located location feel] *~3 [~itwords $chiefcomplaint?] )
		(!scale what [area part location] *~3 [~itwords $chiefcomplaint?])
		(!scale where *~3 [exactly specifically])
	])
	^factanswer("I am not sure.")
		a: WHERE (where) ^factanswer("It is difficult to say.")

#!!P Describe the 1 $chiefcomplaint=1
u: DESCRIBE_THE_1
	([
		(![when where else] [tell ~describe description] * [~itwords $chiefcomplaint?])
		(what * [kind type nature] * $chiefcomplaint?)
		(!outside [how what] * [~itwords $chiefcomplaint?] * [look like smell feel])
		([kind type sort manner] * [~itwords $chiefcomplaint?])
		(![when where else] how * [~describe description] * [$chiefcomplaint?])
	])
	^factanswer("It is difficult to describe.")
		a: WHERE (where) ^factanswer("It is difficult to describe.")

#!!P Do you have any 1 $chiefcomplaint=1
u: DO_YOU_HAVE_ANY_1 (!where [do are] you have *~2 $chiefcomplaint? >) ^factanswer("Not that I am aware of.")

#!!P Any concerns besides the 1 $chiefcomplaint=1
u: ANY_CONCERNS_BESIDES_THE_1
	([
		([symptom issue complaint concern worry problem "bothering you" discuss discussion trouble "talk about"] * [aside beside besides "in addition" other] * $chiefcomplaint?)
		([aside beside besides "in addition" other] * $chiefcomplaint? * [symptom issue complaint concern worry problem "bothering you" discuss discussion trouble "talk about"])
	])
	^factanswer("I don't think so.")

# ***************************************************************************************************
# Deleted all patterns for complaint 2 and complaint 3 for Carlos Martinez iPad spoken version
