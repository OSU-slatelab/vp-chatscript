

# ***************************************************************************************************

# This is the Vaccine Section

topic: ~vaccines keep repeat (vaccine vaccinate vaccinated vaccination vaccinations shot booster up-to-date "up to date" immunized immunization check)

	u: ($family) ^fail(TOPIC)


#! Have you been vaccinated for MMR

	u: have_you_been_vaccinated_for_MMR (have you been vaccinated for MMR) ^factanswer("I have.")
	u: (<<[vaccine vaccines vaccinated vaccination vaccinations][MMR mumps measles rubella]>>) ^reuse(have_you_been_vaccinated_for_MMR)
	u: ([MMR mumps measles rubella]) ^reuse(have_you_been_vaccinated_for_MMR)

#! Have you been vaccinated for hib

	u: have_you_been_vaccinated_for_HIB  (have you been vaccinated for hib)  ^factanswer("I believe so.")
	u: (<<[vaccine vaccines vaccinated vaccination vaccinations]["Haemophilus Influenza b" hib]>>) ^reuse(have_you_been_vaccinated_for_HIB)
	u: (["Haemophilus Influenza b" hib]) ^reuse(have_you_been_vaccinated_for_HIB)

#! Have you been vaccinated for Hepatitis A

	u: have_you_been_vaccinated_for_hepatitis_a (have you been vaccinated for "Hepatitis a")  ^factanswer("I received that vaccine.")
	u: (<<[vaccine vaccines vaccinated vaccination vaccinations]["Hepatitis A" "hep A"]>>) ^reuse(have_you_been_vaccinated_for_hepatitis_a)
	u: (["Hepatitis A" "hep A"]) ^reuse(have_you_been_vaccinated_for_hepatitis_a)

#! Have you been vaccinated for Hepatitis B

	u: have_you_been_vaccinated_for_hepatitis_b (have you been vaccinated for "Hepatitis b")  ^factanswer("I received that vaccine.")
	u: (<<[vaccine vaccines vaccinated vaccination vaccinations]["Hepatitis B" "hep B"]>>) ^reuse(have_you_been_vaccinated_for_hepatitis_B)
	u: (["Hepatitis B" "hep B"]) ^reuse(have_you_been_vaccinated_for_hepatitis_B)

#! Have you been vaccinated for Hepatitis

	u: have_you_been_vaccinated_for_hepatitis (have you been vaccinated for hepatitis) ^factanswer("I think so")
	u: (<<[vaccine vaccines vaccinated vaccination vaccinations] hepatitis >>) ^reuse (have_you_been_vaccinated_for_hepatitis_b)
	u: (hepatitis) ^reuse (have_you_been_vaccinated_for_hepatitis)

#! Have you had the hpv vaccine

	u: have_you_had_the_hpv_vaccine (have you had the hpv vaccine) ^factanswer("I did  not get that.")
	u: (<<[vaccine vaccines vaccinated vaccination vaccinations][hpv "human papilloma virus"]>>) ^reuse(have_you_had_the_hpv_vaccine)
	u: ([hpv "human papilloma virus"]) ^reuse(have_you_had_the_hpv_vaccine)

#! Have you had your flu shot

	u: have_you_had_flu_shot (have you had your flu shot)  ^factanswer("I get one every year.")
	u: (<<[flu influenza][vaccine shot vaccines vaccination vaccinated]>>) ^reuse(have_you_had_flu_shot)

#! Have you had the pneumonia vaccine

	u: have_you_had_the_pneumonia_vaccine (have you had the pneumonia vaccine)  ^factanswer("I haven't had that")
	u: (<<[pneumonia pneumococcal][vaccine shot vaccines vaccination vaccinated]>>) ^reuse(have_you_had_the_pneumonia_vaccine)

#! Have you received your shingles vaccine

	u: Have_you_received_your_shingles_vaccine (have you received your shingles vaccine)   ^factanswer("Not yet.")
	u: (<< shingles [vaccine shot vaccines vaccination vaccinated]>>) ^reuse(have_you_received_your_shingles_vaccine)

#! When was your last tetanus shot

	u: When_was_your_last_tetanus_shot (when was your last tetanus shot)    ^factanswer("I had my last booster within the last 5 years.")
	u: (<<tetanus [vaccine shot vaccines vaccination vaccinated booster]>>) ^reuse(when_was_your_last_tetanus_shot)

#! Are you current on vaccinations
	u: Are_you_current_on_vaccinations (are you current on vaccinations)  ^factanswer("I am up to date.")
	u: ([current recent recently had up-to-date "up to date" all been] * [vaccine vaccination immunized immunization immunizations shots]) ^reuse(are_you_current_on_vaccinations)
	u: ([vaccine vaccination immunization immunizations shots] * [current recent recently had up-to-date "up to date" all]) ^reuse(are_you_current_on_vaccinations)
	u: ([vaccine vaccination immunization immunizations shots]>) ^reuse(are_you_current_on_vaccinations)

# ***************************************************************************************************


# This is the physician/Doctor's office section

topic: ~seeingadoctor keep repeat (doctor physician find locate practice last hard recent location office us park patient parking here wait ~medical_professionals physical exam gynecologist counseling sought help seen discuss)


#! When was the last time you saw a doctor
	u: When_did_you_see_a_doctor_last (when was the last time you saw a doctor)  ^factanswer("I don't remember.")
	u: (!pap [when do] * [recent most last regular] * [doctor physician physical exam checkup]) ^reuse (When_did_you_see_a_doctor_last)
	u: (!pap [when do] * [recent most last regular]) ^reuse (When_did_you_see_a_doctor_last)

#! When was the last time you saw a gynecologist
	u: When_did_you_see_a_gynecologist_last (when was the last time you saw a gynecologist)  ^factanswer("I haven't seen a gynecologist.")
	u: (["have you" when] * [recently recent most last] * [gynecologist]) ^reuse (When_did_you_see_a_gynecologist_last)

#! Do you have annual exams
	u: Do_you_have_annual_exams (do you have annual exams)  ^factanswer("I do.")
	u: ([annual yearly regular] [exam appointment physical]) ^reuse (Do_you_have_annual_exams)
	u: ([doctor physical exam appointment] * [year yearly regularly annually regular])  ^reuse (Do_you_have_annual_exams)

#! Have you seen a doctor
	u: Have_you_seen_a_doctor (have you seen a doctor)  ^factanswer("I have not.")
	u: (![any other else] [been see sought discuss] * [~medical_professionals anyone counseling help]) ^reuse (Have_you_seen_a_doctor)

#! Did you have any trouble finding us
	u: Did_you_have_any_trouble_finding_us (did you have any trouble finding us)  ^factanswer("I had no trouble.")
	u: ([trouble problem hard] * [get locate find] * [us office here in]) ^reuse (Did_you_have_any_trouble_finding_us)

#! Did you find a parking spot
	u: Did_you_find_a_parking_spot (did you find a parking spot)   ^factanswer("I had no problems parking")
	u: ([able trouble problem] * [locate find park parking]) ^reuse(Did_you_find_a_parking_spot)

#! How did you get here today
	u: How_did_you_get_here_today (how did you get here today)  ^factanswer("I drove")
	u: (how * ["get here" arrive travel] * here) ^reuse (How_did_you_get_here_today)

#! How did you come to find us
	u: How_did_you_come_to_find_us (how did you come to find us)  ^factanswer("I have been here before.")
	u: (!trouble find us) ^reuse (How_did_you_come_to_find_us)

#! How did you learn about this office
	u: How_did_you_learn_about_this_office (how did you learn about this office) ^factanswer("I have been here before.")
	u: ([learn discover] * [this our] * [office practice location]) ^reuse (How_did_you_learn_about_this_office)

#! Have you been seen at this office before
	u: Have_you_been_seen_at_this_office_before (have you been seen at this office before)  ^factanswer("I have been here before.")
	u: (office [before prior]) ^reuse (Have_you_been_seen_at_this_office_before)
	u: (first * visit) ^reuse (Have_you_been_seen_at_this_office_before)
	u: (first * time * office) ^reuse (Have_you_been_seen_at_this_office_before)

#! Are you a new patient
	u: Are_you_a_new_patient (are you a new patient)  ^factanswer("I am not a new patient.")
	u: (new patient) ^reuse (Are_you_a_new_patient)

#! Where are you usually seen
	u: Where_are_you_usually_seen (where are you usually seen)  ^factanswer("Only here")
	u: (where * [usually normally regularly typically] * seen) ^reuse (Where_are_you_usually_seen)

#! Have you been waiting long
	u: Have_you_been_waiting_long (have you been waiting long)  ^factanswer("I have not been waiting very long")
	u: (wait * long) ^reuse (Have_you_been_waiting_long)

#! Do you have a doctor
	u: Do_you_have_a_doctor (do you have a doctor) ^factanswer("I do not have a doctor.")
	u: (![any other else] [visit have see] * [doctor physician]) ^reuse (Do_you_have_a_doctor)

# ***************************************************************************************************


# This is the mental health section note - we have included past and family mental health questions here instead of having separate topics for those.

topic: ~mentalhealth keep repeat (depressed depression blue mood moody stress sad lonely suicide suicidal harm intend mental cry anxiety image self "self image" down hopeless worry hurt distracted concentrate think "panic attack" tearful teary "tear up" "not worth living" "better off dead" empty helpless worthless guilt guilty blame happy happier satisfied pleasure ~weapons bed morning death dying die "mind racing" "stopping your mind" thoughts relax anxious swing grumpy overwhelmed)


#! Ever have postpartum depression
	u: Ever_have_postpartum_depression  (ever have postpartum depression)  ^factanswer("I was never depressed")
	u: ([history diagnosed diagnosis suffer treat have] * postpartum depression) ^reuse (Ever_have_postpartum_depression)
	u: ([history diagnosed diagnosis suffer treat have feel] * ~depression * [postpartum birth]) ^reuse (Ever_have_postpartum_depression)

#! Family history of depression
	u: Family_history_of_depression  (family history of depression)  ^factanswer("No one in my family has depression.")
	u: ([~family_members family] * [history diagnosed diagnosis suffer treat] * ~depression) ^reuse (Family_history_of_depression)
	u: ([history diagnosed diagnosis suffer treat] * ~depression * [~family_members family]) ^reuse (Family_history_of_depression)

#! Do you have a history of depression
	u: Do_you_have_history_of_depression  (do you have a history of depression)  ^factanswer("I have never been depressed")
	u: ([history diagnosed diagnosis suffer treat ever] * ~depression) ^reuse (Do_you_have_history_of_depression)

#! Do you have a history of anxiety
	u: Do_you_have_history_of_anxiety  (do you have a history of anxiety)  ^factanswer("I have never been anxious.")
	u: ([history diagnosed diagnosis suffer treat] * [anxiety anxiousness anxious]) ^reuse (Do_you_have_history_of_anxiety)

#! Family history of anxiety
	u: Family_history_of_anxiety  (family history of anxiety)  ^factanswer("No one in my family has anxiety.")
	u: ([~family_members family] * [history diagnosed diagnosis suffer treat] * [anxiety anxiousness anxious]) ^reuse (Family_history_of_anxiety)
	u: ([history diagnosed diagnosis suffer treat] * [anxiety anxiousness anxious] * [~family_members family]) ^reuse (Family_history_of_anxiety)

#! Do you feel anxious
	u: Do_you_feel_anxious (do you feel anxious)  ^factanswer("Not really.")
	u: (![~family_members ~spouse anyone anybody pain] [anxious anxiety anxiousness worry "panic attack"])  ^reuse (Do_you_feel_anxious)

#! Are you depressed
	u: Are_you_depressed (are you depressed)  ^factanswer("I am fine.")
	u: ([depressed depression sad blue lonely "mental health" down hopeless]) ^reuse (Are_you_depressed)

#! Having mood changes
	u: Having_mood_changes (having mood changes)  ^factanswer("My mood is fine.")
	u: ([have experience notice] * [moody mood grumpy emotion emotional] * [swing change fluctuation behavior times swing])  ^reuse (Having_mood_changes)
	u: ([have experience notice] * [change fluctuation behavior times] * [grumpy moody mood])  ^reuse (Having_mood_changes)
	u: ([moody mood grumpy] * [~timename])  ^reuse (Having_mood_changes)

#! When did you cry last
	u: When_did_you_cry_last (when did you cry last) ^factanswer("I don't cry.")
	u: (when * [recently last] * [cry sob]) ^reuse (When_did_you_cry_last)
	u: (when * [cry sob] * [recently last]) ^reuse (When_did_you_cry_last)

#! How often do you cry
	u: How_often_do_you_cry  (how often do you cry)  ^factanswer("I do not cry very much.")
	u: ([when what how] * [often frequently much longest] * [cry sob]) ^reuse (How_often_do_you_cry)
	u: ([cry sob] more [often frequently longer] [now before]) ^reuse (How_often_do_you_cry)

#! Do you cry often
	u: Do_you_cry_often  (do you cry often)  ^factanswer("I do not cry.")
	u: ([cry sob] * [often frequently "all the time"]) ^reuse (Do_you_cry_often)
	u: ([often frequently "all the time"] * [cry sob]) ^reuse (Do_you_cry_often)
	u: ([tearful teary "tear up"]) ^reuse (Do_you_cry_often)

#! Do you feel like your mind is racing
	u: Do_you_feel_like_your_mind_is_racing (do you feel like your mind is racing)  ^factanswer("I do not.")
	u: ([feel trouble problems] * ["mind racing" "stopping your mind" thoughts relax]) ^reuse (Do_you_feel_like_your_mind_is_racing)

#! Do you feel hopeless
	u: Do_you_feel_hopeless (do you feel hopeless)  ^factanswer("I am not.")
	u: ([hopeless "not worth living" "better off dead" empty helpless worthless overwhelmed]) ^reuse (Do_you_feel_hopeless)

#! Do you feel loss of control
	u: Do_you_feel_loss_of_control (do you feel loss of control)  ^factanswer("I do not.")
	u: ([loss lack out] * [control]) ^reuse (Do_you_feel_loss_of_control)

#! Have you lost interest in activities
	u: Have_you_lost_interest_in_activities (have you lost interest in activities)  ^factanswer("I have not lost interest.")
	u: ([lost lack] * [interest enjoyment enthusiasm pleasure] * [activity things]) ^reuse (Have_you_lost_interest_in_activities)
	u: ([interest enthusiastic pleasure] * [activities things work sex]) ^reuse (Have_you_lost_interest_in_activities)

#! Do you feel guilt
	u: Do_you_feel_guilt (do you feel guilt)  ^factanswer("I do not.")
	u: ([guilt guilty blame]) ^reuse (Do_you_feel_guilt)

#! Are you stressed
	u: Are_you_stressed (are you stressed)  ^factanswer("I am not.")
	u: (![who work job] [stress overworked stressful]) ^reuse (Are_you_stressed)

#! Family history of suicide
	u: Family_history_of_suicide  (family history of suicide)  ^factanswer("No one in my family has committed suicide.")
	u: ([~family_members family] * [commit attempt intend consider thought intention] * [suicide kill harm suicidal]) ^reuse (Family_history_of_suicide)
	u: ([~family_members family] * suicidal) ^reuse (Family_history_of_suicide)

#! Are you suicidal
	u: Are_you_suicidal (are you suicidal) ^factanswer("I would never.")
	u: ([consider thought intend intention feel felt] * [suicide harm suicidal hurt]) ^reuse (Are_you_suicidal)
	u: ([harm hurt] yourself) ^reuse (Are_you_suicidal)

#! Do you think about death
	u: Do_you_think_about_death  (do you think about death) ^factanswer("I try not to.")
	u: ([think thought] * [death dying]) ^reuse (Do_you_think_about_death)

#! Do you have a weapon
	u: Do_you_have_a_weapon (do you a weapon) ^factanswer("No.")
	u: ([~weapons]) ^reuse (Do_you_have_a_weapon)


#! What is your self image
	u: What_is_self_image (what is self image) ^factanswer("I have no issues there.")
	u: ([what how] * [self image "self image"])  ^reuse (What_is_self_image)

#! Are you happy
	u: Are_you_happy (are you happy)  ^factanswer("I am happy.")
	u: ([happy happier satisfied pleasure "mental health"]) ^reuse (Are_you_happy)

#! What is your emotional state
  u: What_is_your_emotional_state (what is your emotional state) ^factanswer("My emotional state is fine.")
  u: ("emotional state") ^reuse (What_is_your_emotional_state)

# #! Do you feel distracted
#	u: Do_you_feel_distracted (do you feel distracted) ^factanswer("Not really.")
#	u: ([feel able difficulty] * [distracted concentrate focus think]) ^reuse (Do_you_feel_distracted)




# ***************************************************************************************************


# This is the relationship health section note - we have included past and family relationship health questions here instead of having separate topics for those.

topic: ~relationshiphealth  keep repeat (relationship relation "get along" "getting along" ~spouse ~family_children ~family_members sexual sex anyone anybody)

#! How is your relationship with children
	u: How_is_relationship_with_children (how is your relationship with your children)  ^factanswer("We have a good relationship.")
	u: (![spouse partner husband wife] [relationship relation "getting along"] * [~family_children children kid]) ^reuse (How_is_relationship_with_children)

#! How is your relationship with spouse
	u: How_is_relationship_with_spouse (how is your relationship with your spouse)  ^factanswer("We have a good relationship.")
	u: (![~family_children children kid sex sexual] [relationship relation "getting along" "get along"] * [~spouse]) ^reuse (How_is_relationship_with_spouse)
	u: (![~family_children children kid sex sexual] ~spouse * [relationship relation "getting along" "get along"])  ^reuse (How_is_relationship_with_spouse)

#! Is your family worried about you
	u: Is_your_family_worried_about_you (is your family worried about you)  ^factanswer("I don't think anyone worries about me.")
		a: Why_worried ([what why] * [concern worry]) ^factanswer("I don't know.")
	u: (!health [~family_members ~spouse anyone anybody] * [concern worry]) ^reuse (Is_your_family_worried_about_you)

#! Do you wish you were not married
	u: Do_you_wish_you_were_not_married  (do you wish you were not married)  ^factanswer("Well, no.")
	u: ([wish think] * [not "no longer"] * [married together relationship]) ^reuse (Do_you_wish_you_were_not_married)
	u: ([wish think] * [divorce divorced]) ^reuse (Do_you_wish_you_were_not_married)

#! How is your sexual relationship
	u: How_is_your_sexual_relationship (how is your sexual relationship) ^factanswer("fine.")
	u: (how * [sex sexual] * [relationship life relations]) ^reuse (How_is_your_sexual_relationship)
	u: ([change decrease increase] * [sex sexual] * [relationship life relations]) ^reuse (How_is_your_sexual_relationship)
