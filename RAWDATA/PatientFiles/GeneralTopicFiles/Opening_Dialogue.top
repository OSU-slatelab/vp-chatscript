Table: ^equivalent() # these are all essential

WHAT_BRINGS_YOU_IN_TODAY WHAT_BRINGS_YOU_IN_TODAY  # this is the base question - all similar questions map to this base
HOW_DO_YOU_FEEL WHAT_BRINGS_YOU_IN_TODAY

# ***************************************************************************************************

topic: ~openingdialogue keep repeat ()

# 1.Primary

#! What do you think is the problem
	u: WHAT_DO_YOU_THINK_IS_THE_PROBLEM
		([
 			(!when [what "can you"] * think * [problem wrong "led to" "going on" reason matter])
	 		(!when what * [wrong "going on"] with * [back~n ~itwords])
	 		(!when [ideas idea thought thoughts] * [might may could] * [be problem wrong "led to" "going on" reason matter])
		])
	^factanswer("Well, I'm hoping you can tell me that.")

#! What brings you in today
	u: WHAT_BRINGS_YOU_IN_TODAY
		([
			(["what brings you" "why are you"] in today)
			(![when other else] [why what] * ["do for" bring help come] * [today tonight evening afternoon morning day])
			(![when other else] "what brings you in")
			(![when other else] "brings you in today")
	 		(![when other else] "what brings you here")
	 		(![when other else] "what is bringing you in")
 			(![when other else] ["to the office" "to my office" "the clinic"])
	 		(![when other else] why * [in into come] * ["to the doctor" today]) # What brings you in today tokenizes to Why are you in today
	 		(![when other else worse better chronic] [problem trouble] * [having experiencing])
			(![when other else] [what how] * [help assist] you)
			(![when other else pain] [is what about] * [problem trouble bother concern complaint issue])
 			(![when other else] come * see me)
			(![when other else] seems * [trouble problem issue]) # this tokenizes to What is the trouble so not sure I need it
			(![when other else] why * [here in to at] * today)
			(![when other else] why * [here in])
			(![when other else back think] what * is * wrong)
			(![when other else] feel [OK "allright" "all right"])
			(![when other else] what * you * ["talk about" discuss])
		])
	^factanswer("I'm hoping you can help me out with a problem I've been having.")

#! What should we do
	u: WHAT_SHOULD_WE_DO (!when what * [should we help helpful]) ^factanswer("I was hoping you could tell me.")

#! What would you like me to do
	u: WHAT_WOULD_YOU_LIKE_ME_TO_DO
		([
	 		(!when what * [I me]* [do try])
			(!when what * [want like can] * [me I] * do)
 			(!when !other !else [what how] *~2 ["do you" "can I"] *~2 [help "do for"])
		])
	^factanswer("I'd like you to help me out.")

#! What is your goal for this visit
	u: WHAT_IS_YOUR_GOAL
		([
			([you your] *~2 [hope goal achieve want] *~2 [consultation visit today])
			([hope want] * accomplish)
		])
	^factanswer("I'm hoping you could help me out with a problem I've been having.")

#! Does anything make you feel better
	u: DOES_ANYTHING_MAKE_YOU_FEEL_BETTER
		([
			(anything * feel * better)
 			(!else !other anything * help)
		])
	^factanswer("Nothing makes me feel better.")

#! Are you around sick people
	u: ARE_YOU_AROUND_SICK_PEOPLE
		([
			(!other !else [around near close know] * [~family_members ~sibling ~people] * [sick ill "feeling poorly" "under the weather"])
			(!other !else [~family_members ~sibling ~people] * [around near close know] * [sick ill "feeling poorly" "under the weather"])
		])
	^factanswer("Not that I know.")

#! Are you sick
	u: ARE_YOU_SICK (![other else ~family_members sibling people] you [sick ill "feeling poorly" "under the weather"]) ^factanswer("I am not sick.")

#! Tell me more
	u: TELL_ME_MORE
		([
			("tell me more" >)
			([continue elaborate])
			(![we 'I] tell * [more happened doing "going on" about])
		])
		What do you want to know?

#! Let me wash my hands
	u: LET_ME_WASH_MY_HANDs ([wash clean] * hands) ^factanswer("Ok take your time.")

#! Hopefully we can help you
	u: HOPEFULLY_WE_CAN_HELP_YOU
		([
			(hopefully we can help you)
			([hopefully hope] * [help "feeling better" "feel better" plan solution communicate "figure out" "take care"])
 			([I we] * [help "feeling better" "feel better" plan "go from here" "go from there" solution communicate "take care" "figure out"])
		])
	 ^factanswer("Thank you.")

#! I am sorry
	u: I_AM_SORRY
		([
			(I am sorry)
			(~emoapology >)
			(*~3 [~emoapology])
			])
	^factanswer("Thank you for your concern.")

#! That must be awful
#! It sounds awful
	u: THAT_MUST_BE_AWFUL ([it that] ["must be" sounds] * [awful difficult sometimes bad]) ^factanswer("Thank you for your concern.")

#! Why
	u: WHY (< why >) ^factanswer("Why what?")

#! Good
	u: THAT_IS_GOOD
		([
			(!better [good great awesome cool super excellent congratulations] >)
			( *~2 [good great super excellent glad] * [hear know you keep])
		])
	^factanswer("It is.")

#!!P Got it => ~yes
	u: GOT_IT (*~2 got it >) \[ Nod \] .

#! Ok then
#! How does that sound
#! Sound like a plan
#! Does that sound OK to you
	u: OK_THEN
		([
			([~yes OK okay] >)
			([~yes OK okay] to you)
			(sound like a *~3 plan)
			(does that sound)
		])
	^factanswer("OK.")

#! Please repeat that
	u: REPEAT_THAT
		([
			(repeat that)
			(say that again)
			(come again)
		])
		Of course: $lastoutput
# #####################################################################
# Greetings

#! I am Doctor Winslow
	s: I_AM_DOCTOR ([name be "I am"] * Doctor _*~2)  # also matches "My name is doctor ..."
	$drfirstname = '_0
	$drlastname = '_1
	Nice to meet you
	# Nice to meet you doctor $drfirstname $drlastname

#! I am medical student
	u: I_AM_MEDICAL_STUDENT ([I my] [name be] * student) ^factanswer("Nice to meet you.")

#! I will be part of your health care team
	u: I_WILL_BE_PART_OF_YOUR_HEALTH_CARE_TEAM
		([
			([I student] * [part on with] * [care team])
			(I * ["taking care" interview question] * you)
		])
	^factanswer("Sounds good.")

#! Today we have 15 minutes
	u: TODAY_WE_HAVE_15_MINUTES ([we I] * [minutes min]) ^factanswer("OK, thanks.")

#! Does that plan sound good
	?: DOES_THAT_PLAN_SOUND_GOOD ([does is] *~2 [plan approach] *~2 [good OK acceptable]) ^factanswer("Sure.")

#! I will get your history
	s: I_WILL_GET_YOUR_HISTORY ([get obtain] * your * history) ^factanswer("OK, thanks.")

#!!P Nice to meet you => ~emohello
	s: NICE_TO_MEET_YOU ([nice pleased pleasure good] * meet)  ^factanswer("Nice to meet you")

#!!P Nice to see you => ~emohello
	s: NICE_TO_SEE_YOU ([nice pleased pleasure good glad] * see *~2 you) ^factanswer("Nice to see you")

#!!P Nice to see you too => ~emohello
	s: NICE_TO_SEE_YOU_TOO ([nice pleased pleasure good glad] * see *~2 you [too "as well"]) ^factanswer("Thanks.")

#! How are you
	u: HOW_ARE_YOU
		([
			(how are you [> ,])
			(!~family_members ~emohowzit >)
			("how are you doing")
			("how are you today")
			("how are you" >)
			("how are you doing today")
			("how have you been")
			(how are you *~3 $patientlastname?)
			(!~family_members you [ill sick "under the weather" ailments])
			(![better worse did it long] how *~4 [feel feeling going])
			])
	^factanswer("Ok, how are you?")

#! Tell me about yourself
	u: TELL_ME_ABOUT_YOURSELF
		([
			(tell * yourself)
			([talk speak] * ["about you" more])
		])
	^factanswer("Can you be more specific?")

#!!P Hello => ~emohello
	u: HELLO
		([
			(hello)
			([hello hi hello ~emohello] *~3 >)
			(good [morning afternoon day] *~3 >)
			(good [morning afternoon evening day])
		])
	^factanswer("Hello, doctor.")

#!!S I am Mr Wilcox
#	s: I_am_mister ([name be] * [miss "ms" "mrs" "Ms" "Ms." "Mrs" "Mrs." mister "mr" "Mr" "Mr." "mr."] _*2)
	s: I_AM_MISTER (I am _*~2)
	Nice to meet you

#! My name is Bob
	s: MY_NAME_IS (my name is _*~3)
	Nice to meet you '_0

topic: ~followupdialogue erase norepeat (else other additional only any "before now" discuss)

#! Any other problems
	u: ANY_OTHER_PROBLEMS1 (!health !medical [other else only associated] * [symptom issue concern problem "bothering you" discuss discussion trouble "talk about"])  ^factanswer("I don't believe so.")

#! Any other problems
	u: ANY_OTHER_PROBLEMS2 (!health !medical [other else associated] * [symptom issue concern problem "bothering you" discuss trouble])  ^factanswer("I don't think so.")

#! Any other problems
	u: ANY_OTHER_PROBLEMS3 (!health !medical [other else associated] * [symptom issue concern problem "bothering you" discuss trouble])  ^factanswer("I can't think of anything.")

#! Have you seen anyone else
	u: HAVE_YOU_SEEN_ANYONE_ELSE
		([
			([see discuss] * [~medical_professionals anybody anyone] * ["before now" else])
 			([see discuss] * [any other] * [~medical_professionals])
		])
	^factanswer("I have not.")
			a: WHY_DID_YOU_WAIT (why * [wait take not] * [long soon sooner "before now"]) ^factanswer("I don't know.")
