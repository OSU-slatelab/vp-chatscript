# Age, Date of BIrth

topic: ~Patient_age keep repeat (age born "date of birth" old birthday)

#! How old are you
u: HOW_OLD_ARE_YOU
	([
		(![~family_children ~sibling ~family_adult]  how old are you)
		(![~family_children ~sibling ~family_adult]  your age)
	])
	^factanswer("I am not sure.")

#! When were you born
u: WHEN_WERE_YOU_BORN
	([
		(!where when *~3 [born birthday])
		(!name ["date of birth" birthday])
	])
	^factanswer("I am not sure.")

# #############################################################################################################

topic: ~Patient_weight keep repeat (weigh weight)

#! How much do you weigh
u: HOW_MUCH_DO_YOU_WEIGH ([weigh weight]) ^factanswer("I don't know. I haven't weighed myself in a while.")

topic: ~Patient_name keep repeat (name Mr. Mrs. Ms Ms. address pronounce mind call speak "speaking with" who)

#! Name and date of birth
u: NAME_AND_DATE_OF_BIRTH
	([
		(name and date of birth)
		(name and ["date of birth" birthday])
	])
	^factanswer("I thought I told you.")

#! What is your name
u: WHAT_IS_YOUR_NAME
	([
	 	(what *~4 your name)
	 	(!lives [whom who] *~3 [speak "speaking with"])
	 	([tell remind verify confirm] *~4 name)
	])
		^factanswer("You can call me anything you want.")
#	$patientfirstname $patientlastname # putting variables in answer tables and using factanswer does not work so just putting variables in here also works.

#! Did I pronounce that correctly
u: DID_I_PRONOUNCE_THAT_CORRECTLY (pronounce *~3 [correct correctly]) ^factanswer("That is correct.")

#!!P Do you mind if I call you Jack $patientfirstname=Jack
u: DO_YOU_MIND_IF_I_CALL_YOU
 ([
	 ([mind care OK okay allright may can] *~3 call you *~3 $patientfirstname?)
	 ([mind care OK okay allright may can] *~3 call you *~3 $patientlastname?)
	 ([prefer rather] *~4 [$patientlastname? $patientfirstname?])
	])
	^factanswer("That is fine.")

#! How do you pronounce that
u: HOW_DO_YOU_PRONOUNCE_THAT (pronounce that)  ^factanswer("Just like it looks.")

#! What should I call you
u: WHAT_SHOULD_I_CALL_YOU ([what how] * ["call you" name address]) ^factanswer("Please call me by my first name.")

#!!P Are you Mr. Wilcox $patientlastname=Wilcox
u: ARE_YOU_MR_PATIENTLASTNAME (< !brings !how !what are you *~3 $patientlastname?) Yes, I am $patientfirstname $patientlastname. # Note no factanswer

#!!P Are you Mr. $patientlastname=null
u: ARE_YOU_MR_SOMEONEELSE (!how !$patientlastname? 'are you [Mr. Mrs. Ms Ms.]) No, I am $patientfirstname $patientlastname.  # Note no factanswer

#! Do you have a different Name
u: DIFFERENT_NAME ([different another other] name) ^factanswer("No")

#!!P Hello Mr =>  ~emohello and then Mr.  $patientlastname=Wilcox
u: HELLO_MR_PATIENTLASTNAME ([hi hello ~emohello] [Mr. Mrs. Ms Ms. mr mr.] $patientlastname? >)  ^factanswer("Hello, doctor.")

#!!P Hello Mr.  =>  ~emohello  and then Mr. $patientlastname=null
u: HELLO_MR_SOMEONEELSE (!$patientlastname?  [hi hello ~emohello] [Mr. Mrs. Ms Ms.] _*1) Hello, doctor, but my name is $patientfirstname $patientlastname. # Note no factanswer

# Have to do this because "Hello Mr. Coddington" gets tokenized into ~emohello Mr. Coddington
# so if someone types "Mr. Coddington?" we have to respond "Hello Doctor" instead of "Yes, I am Roger Coddington"

#!!P Mr. Wilcox $patientlastname=wilcox
u: MR_PATIENTLASTNAME (< !~emohello [Mr. Mrs. Ms Ms.] $patientlastname?)   ^factanswer("Hello.")

#!!P Mr. $patientlastname=null
u: MR_SOMEONEELSE (!$patientlastname?  !~emohello [Mr. Mrs. Ms Ms.]) No, I am $patientfirstname $patientlastname. # Note no factanswer
