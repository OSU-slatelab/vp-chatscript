# ***************************************************************************************************

# Family Life

topic: ~familylife keep repeat (family "live with" pet dog cat live area home ~spouse life near supportive share similar)

#! Do you have a family
#! Any living family members
	u: DO_YOU_HAVE_A_FAMILY
	 ([
		  (!~all_systems [have any many] *~3 family {members} >)
			(!~all_systems [family parents] *~3 [near nearby close area])
			(!~all_systems [near nearby close area] *~3 [family parents])
			(living family)
		])
	^factanswer("I do have a family.")

#! Do you have any pets
	u: DO_YOU_HAVE_ANY_PETS ([have any] *~3 [pet dog cat]) ^factanswer("I have no pets.")

#! How is your family
	u: HOW_IS_YOUR_FAMILY (![old many 'do relationship] [how tell] *~3 [~family_members family])  ^factanswer("My family is fine.")

#! Who do you live with
	u: WHO_DO_YOU_LIVE_WITH
	 ([
		 ([live lives] with)
		 ([live lives] *~3 home)
		])
	^factanswer("I live alone.")

#! Do you live alone
	u: DO_YOU_LIVE_ALONE (live *~3 [alone yourself]) ^factanswer("I live alone.")

#! Where do you live
	u: WHERE_DO_YOU_LIVE
	 ([
		 (what *~3 address)
		 (where *~3 [live from home])
		])
	^factanswer("I live by myself at home in Columbus.")

#! How is home
	u: HOW_IS_HOME
	 ([
		 (!where [tell how] *~3 [things everything life] *~3 home)
		 (!where [tell how] *~3 home *~3 life)
		 (!where [supportive comfortable tell] *~3 home)
		])
	^factanswer("It's fine.")

	topic: ~familymedicalproblems keep repeat deprioritize (~role_family condition inherit problem issue hereditary genetic cancer sick ill illness)

	# u: ($pasthistory) respond(~pastmedicalproblems)  ^fail(TOPIC)
	#	u: ($present) respond(~currentmedicalproblems) ^fail(TOPIC)

#! Is anyone sick at home
	u: ANYONE_SICK_AT_AT_HOME
	 ([
		 ([anyone anybody ~spouse ~family_children] *~3 [sick ill] *~3 [house home])
		 ([anyone anybody ~spouse ~family_children] *~3 [house home] *~3 [sick ill])
		 ([anyone anybody ~spouse ~family_children] *~3 [similar like share know] *~3 [symptoms problems yours])
		 ([anyone anybody ~spouse ~family_children] *~3 [symptoms problems 'pain] *~3 [similar like share])
		])
	^factanswer("Not that I am aware of.")

#! Any family history of illness
	 	u: ANY_FAMILY_HISTORY_OF_CANCER (!~depression  << family cancer >>) ^factanswer("I don't know of any.")

	#! Any family history of illness
	#! Are there any medical conditions that run in your family
	#! Does anyone in your family have anything
		u: ANY_FAMILY_HISTORY_OF_ILLNESS
		 ([
			 (!~depression [medical health inherit hereditary genetic history] * [issue condition disease illness problems] * family)
			 (!~depression family * [medical health] * [issue condition disease illness problems illnesses])
			 (!~depression anyone in *~2 family have [issue condition disease illness problems illnesses anything])
			 (!~depression family *~4b [illness disease conditions illnesses problems cancer] )
			])
		^factanswer("Not that I am aware of.")

	#! Tell me about your family history
	#! Can I ask about your family
	#! I need to ask about your past and family history
		u: TELL_ME_ABOUT_YOUR_FAMILY_HISTORY
			([
				(family *~3 history >)
				(I *~3 ask * your *~3 family)
			])
			 ^factanswer("What would you like to know?")

#! Any family history of drug use
	u: ANY_FAMILY_HISTORY_OF_DRUG_USE (<< ~role_family drug >>) ^factanswer("Not that I know of.")

# ***************************************************************************************************

topic: ~parents_information keep repeat (~parents)

#! Are your parents living
	u: ARE_YOUR_PARENTS_LIVING
	([
		(~parents *~3 [alive living "with us"])
	])
	^factanswer("Both of my parents are alive and well.")
			a: (~grandparents)  ^reuse(~grandparents_information.ARE_YOUR_GRANDPARENTS_LIVING)
			a: ([age old]) ^reuse(~parents_information.HOW_OLD_ARE_YOUR_PARENTS)

#! How did your parents die
#! Did your parents pass away due to disease
	u: HOW_DID_PARENTS_DIE
		([
			(!old [how what] *~3 ~parents *~3 [die pass])
			(did *~3 ~parents [die pass] *~3 [due because to result])
		])
		^factanswer("They have not died.")
		a: (~grandparents)  ^reuse(~grandparents_information.HOW_DID_GRANDPARENTS_DIE)

#! How old are your parents
	u: HOW_OLD_ARE_YOUR_PARENTS
	([
 		(~age_words *~3 [they ~parents])
		(~ [his her their] age) # ~ means only if already in this topic
	])
	^factanswer("I am not exactly sure.")
		a: (~grandparents) ^reuse(~grandparents_information.HOW_OLD_ARE_YOUR_GRANDPARENTS)

#! Are your parents healthy
#! Do you parents have any illnesses
	u: ARE_YOUR_PARENTS_HEALTHY ([he she ~parents them they] *~3 [healthy health conditions issues concerns disease illness illnesses]) ^factanswer("Both of my parents are in good health.")
		a: (~grandparents)  ^reuse(~grandparents_information.ARE_YOUR_GRANDPARENTS_HEALTHY)

#! Tell me about your parents
	u: TELL_ME_ABOUT_YOUR_PARENTS ([tell health what how] *~3 [~parents folks ~itwords]) ^factanswer("My parents are both doing well. They've always been really healthy.")
		a: (~grandparents) ^reuse(~grandparents_information.TELL_ME_ABOUT_YOUR_GRANDPARENTS)

	#! Do your parents have any medical problems
		u: PARENTS_HAVE_ANY_MEDICAL_PROBLEMS
		([
			([~parents them] *~3 [chronic medical health] *~3 [problem issue illness condition illnesses])
			([chronic medical health] *~3 [problem issue illness condition illnesses] *~3 ~parents)
		])
		^factanswer("My parents are healthy.")
			a: (~grandparents) ^reuse(~grandparents_information.GRANDPARENTS_HAVE_ANY_MEDICAL_PROBLEMS)

#! Do you see your parents often
#! How often do you see your parents
		?: DO_YOU_SEE_YOUR_PARENTS_OFTEN (<< [see visit "spend time with"] ~parents >>) ^factanswer("I see my parents about once or twice a month.")

# ***************************************************************************************************

topic: ~grandparents_information keep repeat (~grandparents)

#! WHen did your grandparents die
	u: WHEN_DID_GRANDPARENTS_DIE (<< [when age "how old"] [~grandparents they] [die pass] >>) ^factanswer("I don't remember.")

#! How did your grandparents die
#! Did your grandparents pass away due to disease
	u: HOW_DID_GRANDPARENTS_DIE
		([
			(!old [how what] * ~grandparents * [die pass "pass away"])
			(did *~3 ~grandparents [die pass] *~3 [due to because result])
		])
		^factanswer("I don't know.")
			a: (~parents) ^reuse(~parents_information.HOW_DID_PARENTS_DIE)

#! How old are your grandparents
	u: HOW_OLD_ARE_YOUR_GRANDPARENTS
	([
	 	(~age_words *~3 [they ~grandparents])
		(~ their ages)
	])
	^factanswer("I am not sure.")
		a: (~parents) ^reuse(~parents_information.HOW_OLD_ARE_YOUR_PARENTS)

#! Are your grandparents living
	u: ARE_YOUR_GRANDPARENTS_LIVING (~grandparents * [dead passed died alive living "with us" "still around"]) ^factanswer("My grandparents are deceased.")
		a: (~parents) ^reuse(~parents_information.ARE_YOUR_PARENTS_LIVING)

#! Are your grandparents healthy
	u: ARE_YOUR_GRANDPARENTS_HEALTHY (<< ~grandparents [healthy health chronic illness] >>) ^factanswer("They are healthy.")
		a: (~parents) ^reuse(~parents_information.ARE_YOUR_PARENTS_HEALTHY)

#! Tell me about your grandparents
#! What about your grandparents
	u: TELL_ME_ABOUT_YOUR_GRANDPARENTS
		([
			([tell health how 'do about] *~3 ~grandparents)
		])
		 ^factanswer("My grandparents are wonderful.")
		 	a: (~parents) ^reuse(~parents_information.TELL_ME_ABOUT_YOUR_PARENTS)

#! Do your grandparents have any medical problems
	u: GRANDPARENTS_HAVE_ANY_MEDICAL_PROBLEMS
		([
			([~grandparents them] *~3 [chronic medical health] *~3 [problem issue illness condition illnesses])
			([chronic medical health] *~3 [problem issue illness condition illnesses] *~3 ~grandparents)
		])
			^factanswer("My grandparents are healthy.")
				a: (~parents) ^reuse(~parents_information.PARENTS_HAVE_ANY_MEDICAL_PROBLEMS)

# ***************************************************************************************************

topic: ~siblings_information keep repeat (~sibling siblings)

#! How old are your siblings
	u: HOW_OLD_ARE_YOUR_SIBLINGS
	([
		(~age_words *~3 [he she they ~sibling siblings])
		([his her their] age)
	])
	^factanswer("I have no siblings.")

#! Are your siblings healthy
	u: ARE_YOUR_SIBLINGS_HEALTHY ([they he she ~sibling siblings] *~3 [healthy health well problems]) ^factanswer("I have no siblings.")

#! Tell me about your siblings
	u: TELL_ME_ABOUT_YOUR_SIBLINGS ([tell health what] *~3 [them him her ~sibling siblings]) ^factanswer("I have no siblings")

#! Do you have siblings
	u: DO_YOU_HAVE_SIBLINGS ([~sibling siblings]) ^factanswer("I have no siblings.")

# ***************************************************************************************************

topic: ~relatives_information keep repeat (relative aunt uncle cousin family)

#! Do you have relatives
	u: DO_YOU_HAVE_RELATIVES ([have any] *~3 [relative aunt uncle cousin]) ^factanswer("I have no relatives.")

#! How old are your relatives
	u: HOW_OLD_ARE_YOUR_RELATIVES
	([
		(~age_words *~3 [they relative aunt uncle cousin])
		(their ages)
	])
	^factanswer("I have no living relatives.")

#! Are your relatives healthy
	u: ARE_YOUR_RELATIVES_HEALTHY ([they relative family aunt uncle cousin] *~3 [healthy health]) ^factanswer("My relatives are healthy.")

#! Tell me about your relatives
	u: TELL_ME_ABOUT_YOUR_RELATIVES ([tell health what] *~3 [them him her relative aunt uncle cousin]) ^factanswer("I have no relatives.")

# ***************************************************************************************************

topic: ~children_information keep repeat (!~sibling !sibling !neonatalogist ~family_children  children kids family)

#! How many children do you have
	u: HOW_MANY_CHILDREN (![~sibling sibling] how [much many] [~family_children children kids]) ^factanswer("I don't have any children.")

#! How old are your children
	u: HOW_OLD_ARE_YOUR_CHILDREN
	([
		(~age_words *~3 [they he she ~family_children children kids baby])
		(their ages)
		([when what] *~3 [they he she ~family_children children kids baby] * [born delivered adopted])
	])
	^factanswer("I don't have any children.")

#! Are your children healthy
	u: ARE_YOUR_CHILDREN_HEALTHY
		([
			(![~sibling sibling] your * [~family_children children kids] * [healthy health well])
			(![~sibling sibling] [healthy health well] * your * [~family_children children kids])
		])
	^factanswer("I have no children.")

#! How are your children
	u: HOW_ARE_YOUR_CHILDREN (![~sibling sibling old many 'do] how * [~family_children children kids]) ^factanswer("I don't have any children.")

#! Where are your children
	u: WHERE_ARE_YOUR_CHILDREN
		([
			(![~sibling sibling old many 'do] [where] *~3 [~family_children children kids])
			(![~sibling sibling] [~family_children children kids] *~3 [live reside] *~3 [near close closeby])
		])
	^factanswer("I don't have any children.")

#! Do you want children
	u: DO_YOU_WANT_CHILDREN (![~sibling sibling] [want like desire hope] *~3 [~family_children children kids family] >)  ^factanswer("I do not want children.")

#! Do you have any children
	u: DO_YOU_HAVE_ANY_CHILDREN
		([
			(![~sibling sibling health] [have any] *~3 [~family_children children kids])
			(no children)
		])
			 ^factanswer("I have no children.")

#! What are your children named
	u: WHAT_ARE_YOUR_CHILDREN_NAMED (<< name [~family_children children kids] >>) ^factanswer("I have no children.")

#! Tell me about your children
	u: TELL_ME_ABOUT_YOUR_CHILDREN (![~sibling sibling neonatal neonatalogist how] [tell what] * [~family_children children kids]) ^factanswer("I have no children")

	#! Do your children have any medical problems
		u: CHILD_HAVE_ANY_MEDICAL_PROBLEMS
			([
				([~family_children them] *~3 [chronic medical health] *~3 [problem issue illness condition])
				([chronic medical health] *~3 [problem issue illness condition] *~3 ~family_children)
			])
		^factanswer("My children are healthy.")

# ***************************************************************************************************

topic: ~spouse_information keep repeat
	(spouse husband wife involved romantic divorced married marital single widowed boyfriend girlfriend
	 "live with" "significant other" single partner dating relationship "someone special")

#! Are you divorced
	u: ARE_YOU_DIVORCED (!ever divorce) ^factanswer("I've never been married.")

#! How long have you been married
	u: HOW_LONG_HAVE_YOU_BEEN_MARRIED (how long *~3 [married partner relationship significant dating]) ^factanswer("I am not married.")

#! Are you single
	u: ARE_YOU_SINGLE (you *~2 single) ^factanswer("I am not seeing anyone.")

#! Are you in a relationship
	u: ARE_YOU_IN_A_RELATIONSHIP (![ever past before] [have in] *~3 [involved relationship romantic]) ^factanswer("I'm not seeing anyone.")

#! Are you dating anyone
	u: ARE_YOU_DATING_ANYONE ([dating "seeing anyone" "seeing anybody" romantically "going out with" "someone special"]) ^factanswer("I'm not seeing anyone.")

#! Are you widowed
	u: ARE_YOU_WIDOWED (widow)  ^factanswer("I've never been married.")

#! Is your partner healthy
	u: IS_YOUR_PARTNER_HEALTHY ([wife husband partner spouse "significant other" boyfriend girlfriend] *~3 [healthy health symptoms ~sicknesses]) ^factanswer("I have no partner.")

#! Do you have a boyfriend
	u: DO_YOU_HAVE_A_BOYFRIEND (![healthy symptoms] boyfriend)  ^factanswer("I do not.")

#! Do you have a girlfriend
	u: DO_YOU_HAVE_A_GIRLFRIEND (![healthy symptoms] girlfriend)  ^factanswer("I do not.")

#! Do you have a significant other
	u: DO_YOU_HAVE_A_SIGNIFICANT_OTHER (![healthy symptoms] significant other)  ^factanswer("I do not.")

#! Tell me about your partner
	u: TELL_ME_ABOUT_YOUR_PARTNER
		([
			([tell describe] * [wife husband partner spouse "significant other" boyfriend girlfriend])
			(who * [partner spouse "married to"])
		])
	^factanswer("I have no partner.")

#! How old is your partner
	u: HOW_OLD_IS_YOUR_PARTNER
		([
			(~age_words * [he she wife husband partner spouse "significant other" boyfriend girlfriend])
			([his her their] [age ages])
		])
	^factanswer("I have no partner.")

#! Do you have a partner
	u: DO_YOU_HAVE_A_PARTNER (![sex sexual other] do you have * partner)  ^factanswer("I do not.")

#! What is your partner's name
	u: WHAT_IS_YOUR_PARTNERS_NAME (<< name [partner spouse significant husband wife] >>) ^factanswer("I have no partner.")

#! Is your partner male
	u: IS_YOUR_PARTNER_MALE (<< [man male] [partner spouse significant married] >>) ^factanswer("I have no partner.")

#! Is your partner female
	u: IS_YOUR_PARTNER_FEMALE (<< [female woman] [partner spouse significant married] >>) ^factanswer("I have no partner.")

#! Are you married
	u: ARE_YOU_MARRIED (![sex sexual relationship "get along" not] [married "marital status" spouse wife husband])  ^factanswer("I've never been married.")
		a: WHAT_IS_SPOUSE_NAME ([who what] * [name call spouse married]) ^factanswer ("I don't have a spouse.")

# ***************************************************************************************************

topic: ~support_information keep repeat (abuse safe ~shelter live home relationship support system help friends people turn stress talk home)

#! Are you abused
	u: ARE_YOU_ABUSED
		([
			(!~druglist abuse) # !~druglist should exlude questions about abusing drugs
	 		([spouse husband wife boyfriend girlfriend] *~3 [hit abuse hurt])
		])
	^factanswer("I am not.")

#! Do you feel safe at home
	u: DO_YOU_FEEL_SAFE (![work job] [secure safe]) ^factanswer("I feel safe.")

#! Do you have support
	u: DO_YOU_HAVE_SUPPORT
		([
			(!who [support help] *~3 [~shelter system])
			(!who people *~3 [support help] *~3 [~shelter system])
			(!who people *~3 [support help])
			(!who [support friends])
		])
	^factanswer("I have support.")

#! Who supports you
	u: WHO_SUPPORTS_YOU (who * [support help turn talk])  ^factanswer("My family.")

#! Any problems at home
	u: ANY_PROBLEMS_AT_HOME (any [problem difficulty issue concern] *~3 home) ^factanswer("I have no concerns.")
