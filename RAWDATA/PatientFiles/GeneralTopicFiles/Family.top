# ***************************************************************************************************

# Family Life

topic: ~familylife keep repeat (family "live with" pet dog cat live area home ~spouse life near supportive share similar yours)

#! Do you have a family
	u: DO_YOU_HAVE_A_FAMILY (do you have a family >) ^factanswer("I do have a family.")
	u: (!~all_systems [have any many] *~3 family >) ^reuse (DO_YOU_HAVE_A_FAMILY)
	u: (!~all_systems [family parents] * [near nearby close area]) ^reuse (DO_YOU_HAVE_A_FAMILY)
	u: (!~all_systems [near nearby close area] * [family parents]) ^reuse (DO_YOU_HAVE_A_FAMILY)

#! Do you have any pets
	u: Do_you_have_any_pets (do you have any pets) ^factanswer("I have no pets.")
	u: ([have any] * [pet dog cat]) ^reuse (Do_you_have_any_pets)

#! How is your family
	u: How_is_your_family (how is your family)  ^factanswer("My family is fine.")
	u: (!old !many !'do !relationship [how tell] * [~family_members family]) ^reuse (How_is_your_family)

#! Who do you live with
	u: Who_do_you_live_with (who do you live with)  ^factanswer("I live alone.")
	u: ([live lives] with) ^reuse (Who_do_you_live_with)
	u: ([live lives] * home) ^reuse (Who_do_you_live_with)

#! Do you live alone
	u: Do_you_live_alone (do you live alone) ^factanswer("I live alone.")
	u: (live * [alone yourself]) ^reuse (Do_you_live_alone)

#! Where do you live
	u: Where_do_you_live (where do you live) ^factanswer("I live by myself at home in Columbus.")
	u: (what * address) ^reuse (Where_do_you_live)
	u: (where * [live from home]) ^reuse (Where_do_you_live)

#! How is home
	u: How_is_home (how is home) ^factanswer("It's fine.")
	u: (!where [tell how] * [things everything life] * home) ^reuse (How_is_home)
	u: (!where [tell how] * home * life) ^reuse (How_is_home)
	u: (!where [supportive comfortable tell] * home) ^reuse (How_is_home)

#! Is anyone sick at home
	u: Anyone_sick_at_at_home (anyone sick at home) ^factanswer("Not that I am aware of.")
	u: ([anyone anybody ~spouse ~family_children] * [sick ill] * [house home]) ^reuse (Anyone_sick_at_at_home)
	u: ([anyone anybody ~spouse ~family_children] * [house home] * [sick ill]) ^reuse (Anyone_sick_at_at_home)
	u: ([anyone anybody ~spouse ~family_children] * [similar like share know] * [symptoms problems yours])  ^reuse (Anyone_sick_at_at_home)
	u: ([anyone anybody ~spouse ~family_children] * [symptoms problems 'pain] * [similar like share])  ^reuse (Anyone_sick_at_at_home)

# ***************************************************************************************************

topic: ~parents_information keep repeat (~parents ~father ~mother)

#! Are your parents living
	u: Are_your_parents_living (are your parents living)     ^factanswer("Both of my parents are alive and well.")
		a: Grandparents_living (grandparents)  ^factanswer("My grandparents are deceased")
	u: ([~parents ~mother ~father] * [alive living "with us"]) ^reuse (Are_your_parents_living)

#! How did your parents die
	u: How_did_parents_die (how did parents die)  ^factanswer("They have not died.")
	u: (!old [how what] * ~parents * [die pass "pass away"])  ^reuse (How_did_parents_die)

#! How old are your parents
	u: How_old_are_your_parents (how old are your parents) ^factanswer("I am not exactly sure.")
	u: (~age_words * [they ~parents ~mother ~father]) ^reuse (How_old_are_your_parents)
	u: ([his her their] age) ^reuse (How_old_are_your_parents)

#! Are your parents healthy
	u: Are_your_parents_healthy (are your parents healthy)     ^factanswer("Both of my parents are in good health.")
		a: Grandparents_healthy (grandparents)  ^factanswer("My grandparents are deceased")
	u: ([he she ~parents ~mother ~father them they] * [healthy health conditions issues concerns disease]) ^reuse (Are_your_parents_healthy)

#! Tell me about your parents
	u: Tell_me_about_your_parents (tell me about your parents)     ^factanswer("My parents are both doing well. They've always been really healthy.")
	u: ([tell health what how] * [~parents ~father ~mother folks ~itwords]) ^reuse (Tell_me_about_your_parents)


# ***************************************************************************************************

topic: ~grandparents_information keep repeat (~grandparents)

#! Are your grandparents living
	u: Are_your_grandparents_living (are your grandparents living)     ^factanswer("My grandparents are deceased.")
	u: (~grandparents * [dead passed died alive living "with us"]) ^reuse (Are_your_grandparents_living)

#! How did your grandparents die
	u: How_did_grandparents_die (how did grandparents die)  ^factanswer("I don't know.")
	u: (!old [how what] * ~grandparents * [die pass "pass away"])  ^reuse (How_did_grandparents_die)

#! How old are your grandparents
	u: How_old_are_your_grandparents (how old are your grandparents) ^factanswer("I am not sure.")
	u: (~age_words * [they ~grandparents]) ^reuse (How_old_are_your_grandparents)
	u: (their ages) ^reuse (How_old_are_your_grandparents)

#! Are your grandparents healthy
	u: Are_your_grandparents_healthy (are your grandparents healthy)     ^factanswer("They are healthy.")
	u: (!~parents ~grandparents * [healthy health]) ^reuse (Are_your_grandparents_healthy)

#! Tell me about your grandparents
	u: Tell_me_about_your_grandparents (tell me about your grandparents)     ^factanswer("My grandparents are wonderful.")
	u: ([tell health how 'do] * ~grandparents) ^reuse (Tell_me_about_your_grandparents)

# ***************************************************************************************************

topic: ~siblings_information keep repeat (~sibling siblings)


#! How old are your siblings
	u: How_old_are_your_siblings (how old are your siblings) ^factanswer("I have no siblings.")
	u: (~age_words * [he she they ~sibling siblings]) ^reuse (How_old_are_your_siblings)
	u: ([his her their] age) ^reuse (How_old_are_your_siblings)

#! Are your siblings healthy
	u: Are_your_siblings_healthy (are your siblings healthy)     ^factanswer("I have no siblings.")
	u: ([they he she ~sibling siblings] * [healthy health well problems]) ^reuse (Are_your_siblings_healthy)

#! Tell me about your siblings
	u: Tell_me_about_your_siblings (tell me about your siblings)     ^factanswer("I have no siblings")
	u: ([tell health what] * [them him her ~sibling siblings]) ^reuse (Tell_me_about_your_siblings)

#! Do you have siblings
	u: Do_you_have_siblings (do you have siblings) ^factanswer("I have no siblings.")
	u: ([~sibling siblings]) ^reuse (Do_you_have_siblings)


# ***************************************************************************************************

topic: ~relatives_information keep repeat (relative aunt uncle cousin family)

#! Do you have relatives
	u: Do_you_have_relatives (do you have relatives) ^factanswer("I have no relatives.")
	u: ([have any] * [relative aunt uncle cousin]) ^reuse (Do_you_have_relatives)

#! How old are your relatives
	u: How_old_are_your_relatives (how old are your relatives) ^factanswer("I have no living relatives.")
	u: (~age_words * [they relative aunt uncle cousin]) ^reuse (How_old_are_your_relatives)
	u: (their ages) ^reuse (How_old_are_your_relatives)

#! Are your relatives healthy
	u: Are_your_relatives_healthy (are your relatives healthy)     ^factanswer("My relatives are healthy.")
	u: ([they relative family aunt uncle cousin] * [healthy health]) ^reuse (Are_your_relatives_healthy)

#! Tell me about your relatives
	u: Tell_me_about_your_relatives (tell me about your relatives)     ^factanswer("I have no relatives.")
	u: ([tell health what] * [them him her relative aunt uncle cousin]) ^reuse (Tell_me_about_your_relatives)

# ***************************************************************************************************

topic: ~children_information keep repeat (!~sibling !sibling !neonatalogist ~family_children  children kids family)


#! How many children do you have
	u: How_many_children (how many children do you have) ^factanswer("I don't have any children.")
	u: (![~sibling sibling] how [much many] [~family_children children kids]) ^reuse (How_many_children)

#! How old are your children
	u: How_old_are_your_children (how old are your children) ^factanswer("I don't have any children.")
	u: (~age_words * [they he she ~family_children children kids baby]) ^reuse (How_old_are_your_children)
	u: (their ages) ^reuse (How_old_are_your_children)
	u: ([when what] * [they he she ~family_children children kids baby] * [born delivered adopted]) ^reuse (How_old_are_your_children)

#! Are your children healthy
	u: Are_your_children_healthy (are your children healthy)     ^factanswer("I have no children.")
	u: (![~sibling sibling] your * [~family_children children kids] * [healthy health well]) ^reuse (Are_your_children_healthy)
	u: (![~sibling sibling] [healthy health well] * your * [~family_children children kids]) ^reuse (Are_your_children_healthy)

#! How are your children
	u: How_are_your_children (how are your children)  ^factanswer("I don't have any children.")
	u: (![~sibling sibling old many 'do] how * [~family_children children kids]) ^reuse (How_are_your_children)

#! Where are your children
	u: Where_are_your_children (where are your children)  ^factanswer("I don't have any children.")
	u: (![~sibling sibling old many 'do] [where] * [~family_children children kids]) ^reuse (Where_are_your_children)
	u: (![~sibling sibling] [~family_children children kids] * [live reside] * [near close closeby]) ^reuse (Where_are_your_children)


#! Do you want children
	u: Do_you_want_children (do you want children)  ^factanswer("I do not want children.")
	u: (![~sibling sibling] [want like desire hope] * [~family_children children kids family] >) ^reuse (Do_you_want_children)

#! Do you have any children
	u: Do_you_have_any_children (do you have any children)  ^factanswer("I have no children.")
	u: (![~sibling sibling health] [have any] * [~family_children children kids]) ^reuse (Do_you_have_any_children)
#	u: (![~sibling sibling health] [~family_children children kids] >) ^reuse (Do_you_have_any_children)

#! What are your children named
	u: What_are_your_children_named (what are your children named) ^factanswer("I have no children.")
	u: (<< name [~family_children children kids] >>) ^reuse (What_are_your_children_named)

#! Tell me about your children
	u: Tell_me_about_your_children (tell me about your children)     ^factanswer("I have no children")
	u: (![~sibling sibling neonatal neonatalogist how] [tell what] * [~family_children children kids]) ^reuse (Tell_me_about_your_children)


# ***************************************************************************************************

topic: ~spouse_information keep repeat (spouse husband wife involved romantic divorced married marital single widowed boyfriend girlfriend "live with" "significant other" single partner dating relationship)

#! Are you divorced
	u: Are_you_divorced (are you divorced) ^factanswer("I've never been married.")
	u: (divorce) ^reuse (Are_you_divorced)

#! How long have you been married
	u: How_long_have_you_been_married (how long have you been married) ^factanswer("I am not married.")
	u: (how long * [married partner relationship significant dating]) ^reuse (How_long_have_you_been_married)

#! Are you single
	u: Are_you_single (are you single) ^factanswer("I am not seeing anyone.")
	u: ([single "seeing anyone" "seeing anybody"]) ^reuse (Are_you_single)

#! Are you in a relationship
	u: Are_you_in_a_relationship (are you in a relationship) ^factanswer("I'm not seeing anyone.")
	u: ([have in] * [involved relationship romantic]) ^reuse (Are_you_in_a_relationship)

#! Are you dating anyone
	u: Are_you_dating_anyone (are you dating anyone) ^factanswer("I'm not seeing anyone.")
	u: (dating) ^reuse (Are_you_dating_anyone)

#! Are you widowed
	u: Are_you_widowed (are you widowed)  ^factanswer("I've never been married.")
	u: (widow) ^reuse (Are_you_widowed)

#! Is your partner healthy
	u: Is_your_partner_healthy (is your partner healthy)     ^factanswer("I have no partner.")
	u: ([wife husband partner spouse "significant other" boyfriend girlfriend] * [healthy health symptoms ~sicknesses]) ^reuse (Is_your_partner_healthy)

#! Do you have a boyfriend
	u: Do_you_have_a_boyfriend (do you have a boyfriend)  ^factanswer("I do not.")
	u: (![healthy symptoms] boyfriend) ^reuse (Do_you_have_a_boyfriend)

#! Do you have a girlfriend
	u: Do_you_have_a_girlfriend (do you have a girlfriend)  ^factanswer("I do not.")
	u: (![healthy symptoms] girlfriend) ^reuse (Do_you_have_a_girlfriend)

#! Do you have a significant other
	u: Do_you_have_a_significant_other (do you have a significant other)  ^factanswer("I do not.")
	u: (![healthy symptoms] significant other) ^reuse (Do_you_have_a_significant_other)

#! Tell me about your partner
	u: Tell_me_about_your_partner (tell me about your partner)     ^factanswer("I have no partner.")
	u: ([tell describe] * [wife husband partner spouse "significant other" boyfriend girlfriend]) ^reuse (Tell_me_about_your_partner)
	u: (who * [partner spouse "married to"]) ^reuse (Tell_me_about_your_partner)

#! How old is your partner
	u: How_old_is_your_partner (how old is your partner) ^factanswer("I have no partner.")
	u: (~age_words * [he she wife husband partner spouse "significant other" boyfriend girlfriend]) ^reuse (How_old_is_your_partner)
	u: ([his her their] [age ages]) ^reuse (How_old_is_your_partner)

#! Do you have a partner
	u: Do_you_have_a_partner (do you have a partner)  ^factanswer("I do not.")
	u: (![sex sexual other] do you have * partner) ^reuse (Do_you_have_a_partner)

#! What is your partner's name
	u: What_is_your_partners_name (what is your partners name) ^factanswer("I have no partner.")
	u: (<< name [partner spouse significant husband wife] >>) ^reuse (What_is_your_partners_name)

#! Is your partner male
	u: Is_your_partner_male (is your partner male) ^factanswer("I have no partner.")
	u: (<< [man male] [partner spouse significant married] >>) ^reuse (Is_your_partner_male)

#! Is your partner female
	u: Is_your_partner_female (is your partner female) ^factanswer("I have no partner.")
	u: (<< [female woman] [partner spouse significant married] >>) ^reuse (Is_your_partner_female)

#! Are you married
	u: Are_you_married (are you married)  ^factanswer("I've never been married.")
		a: What_is_spouse_name ([who what] * [name call spouse married]) ^factanswer ("I don't have a spouse.")
	u: (![sex sexual relationship "get along" not] [married "marital status" spouse wife husband]) ^reuse (Are_you_married)

# ***************************************************************************************************

topic: ~support_information keep repeat (abuse safe ~shelter live home relationship support system help friends people turn stress talk)


#! Are you abused
	u: Are_you_abused (are you abused) ^factanswer("I am not.")
	u: (!~druglist abuse) ^reuse (Are_you_abused) # !~druglist should exlude questions about abusing drugs
	u: ([spouse husband wife boyfriend girlfriend] * [hit abuse]) ^reuse (Are_you_abused)

#! Do you feel safe at home
	u: Do_you_feel_safe (do you feel safe at home) ^factanswer("I feel safe.")
	u: ([secure safe]) ^reuse (Do_you_feel_safe)

#! Do you have support
	u: Do_you_have_support (do you have support) ^factanswer("I have support.")
	u: (!who [support help] * [~shelter system]) ^reuse (Do_you_have_support)
	u: (!who people * [support help] * [~shelter system]) ^reuse (Do_you_have_support)
	u: (!who people * [support help]) ^reuse (Do_you_have_support)
	u: (!who [support friends]) ^reuse (Do_you_have_support)

#! Who supports you
	u: Who_supports_you (who supports you)  ^factanswer("My family.")
	u: (who * [support help turn talk]) ^reuse (Who_supports_you)
