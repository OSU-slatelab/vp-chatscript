# ***************************************************************************************************

# pain - I have defined several pain concepts in MedicalConcepts.top
# The ones below have been moved to MedicalConcepts.top
# ~the_pain  [~pain ~itwords]
# ~pain [pain hurt sore throb crush ache burn squeeze gnaw]
# ~itwords [it this that them those these they such]

topic: ~currentpain keep repeat
	(10 ten painful ~pain better worse "lie down" "lay down" food exercise position progress dull intensity injury incident injure onset exactly specifically
	ibuprofen cause rate uncomfortable~1 tender situation "go away" positions use symptom travel radiate shoot place spread swallow walk point)

u: ($pasthistory) respond(~pastpain) ^fail(TOPIC)
u: ($family) respond(~familypain) ^fail(TOPIC)

# Onset  ***************

#! What were you doing when you injured yourself
u: WHAT_WERE_YOU_DOING_WHEN_INJURED
	([
		(what *~3 do *~3 [hurt injure] *~3 [yourself "your back"])
		(what *~3 do *~3 ~the_pain *~3 [start begin began appear])
	])
	^factanswer("I am not sure.")

#! What were the circumstances of the injury
u: CIRCUMSTANCES_OF_THE_INJURY (circumstance *~3 injury)  ^factanswer("I am not sure.")

#! How did you injure yourself
u: HOW_DID_YOU_INJURE_YOURSELF (how did *~3 [injure injury])  ^factanswer("I am not sure.")

#! Did you injure yourself
u: DID_YOU_INJURE_YOURSELF
	([
		(![other ~bodyparts ~job] have you had *~3 injury) # excluding ~job avoids were you injured at work
		(![other ~bodyparts ~job] [were did] you injure)
		(![other ~bodyparts ~job] was there * injury)
		(![other ~bodyparts ~job] [remember memory] * injury)
	])
	^factanswer("Not that I am aware of.")

#! How long ago was the incident
u: HOW_LONG_AGO_WAS_THE_INCIDENT # this must be repeated in pastpain due to "ago" triggering past
	([
		([when "long ago"] *~3 [incident accident])
		(how long * [since ago] *~3 [issue incident accident injury])
		(~ [when "long ago"] *~3 [incident accident ~itwords])
	])
	^factanswer("I am not sure.")

#! Did the pain begin immediately
u: DID_THE_PAIN_BEGIN_IMMEDIATELY ([pain hurt] *~3 immediately) ^factanswer("I am not sure.")

#! Is the pain constant
#! Does the pain increase over time
#! Has the pain changed
#! Is the pain worse at any time of the day
#! Is there a time of the day when it is worse
# want to avoid what makes the pain get better
u: IS_THE_PAIN_CONSTANT
	([
		(![intensity severity what] ~the_pain *~3 ["come and go" consistent increase decrease  sometimes constant constantly intermittent "throughout the day" "always there" "always present" always "all the time" "wax and wane" "ever go away" "ever get better" continuous "all day" ongoing change persist])
		(![intensity severity what] ~the_pain *~3 [change fluctuate] *~3 [severity intensity])
		(["come and go" consistent  sometimes constant constantly intermittent "throughout the day" "always there" "always present" always "all the time" "wax and wane" "ever go away" "ever get better" continuous "all day" ongoing change] *~3 ~the_pain)
		(![intensity severity what] ~the_pain *~3 [change "get worse" "get better" fluctuate])
		(![intensity severity what] ~the_pain *~3 [worse hurt better] * [time day])
		(![intensity severity what] [day time] *~3 ~the_pain *~3 [worse hurt better])
	])
	^factanswer("It's difficult to say.")

#! How has the pain changed
u: HOW_HAS_THE_PAIN_CHANGED (how [has does did] *~2 ~the_pain [change develop]) ^factanswer("It is difficult to say.")

# Exacerbation  ******************************************************************************************

#! Does rest make the pain go away
u: DOES_REST_MAKE_THE_PAIN_GO_AWAY
	([
		(<< rest ~the_pain ["go away" better relieve help] >>)
		(<< rest ["go away" better relieve help] >>)
	])
	^factanswer("I am not sure.")

#! Do you have pain when you are at rest
u: DO_YOU_HAVE_PAIN_AT_REST (<< rest ~the_pain >>)  ^factanswer("I am not sure.")

#! Does it hurt when you are not moving
u: DOES_IT_HURT_WHEN_NOT_MOVING
	([
		([hurt painful bother ~pain] * not * [move moving])
		([hurt painful bother ~pain] * still)
		(still * [hurt painful bother ~pain])
		(!down [lay lie] * [motionless still])
	])
	^factanswer("It is difficult to say.")

#! Does it hurt to sneeze
u: DOES_IT_HURT_TO_SNEEZE
	([
		([hurt painful bother ~pain] *~2 sneeze)
		([sneeze sneezing] * [increase exacerbate] * ~the_pain)
	])
	^factanswer("I have not had any problems like that.")

#! Does it hurt to cough
u: DOES_IT_HURT_TO_COUGH
	([
		([hurt painful bother ~pain] *~2 cough)
		([cough coughing] * [increase exacerbate] * ~the_pain)
	])
	^factanswer("I have not had any problems like that.")

#! Does it hurt to swallow
u: DOES_IT_HURT_TO_SWALLOW
	([
		([hurt painful bother ~pain] * swallow)
		([swallow swallowing] * [increase exacerbate] * ~the_pain)
	])
	^factanswer("I have not had any problems like that.")

#! Does it hurt to move
u: DOES_IT_HURT_TO_MOVE
	([
		(!radiate [hurt painful bother] * [walk run move turn])
		(!radiate [move movement moving walk] * [increase exacerbate] * ~the_pain)
	])
	^factanswer("I have not had any problems like that.")

#! Is the pain associated with food
u: IS_THE_PAIN_ASSOCIATED_WITH_FOOD (~the_pain * [food eating])   ^factanswer("I am not sure.")

#! Does the pain get worse at work
u: DOES_THE_PAIN_GET_WORSE_AT_WORK ([does be] *~3 ~the_pain * [~job working]) ^factanswer("The pain is not related to work.")

# Intensity  ***************

#! How would you rate the pain on the medicine $currentmedication1=medicine
u: RATE_PAIN_ON_MEDICINE (rate * ~the_pain * on * [$currentmedication1? ~medicines])    ^factanswer("I'm not taking any medicine.")

# For rating scale is 1 - 10. probably should improve this rule

#! Can you rate the pain
#! On a scale of 1-10
u: CAN_YOU_RATE_THE_PAIN
	([
		(![describe 5 five] [rate scale rank quality quantify] * ~the_pain)
		(![5 five] [rate scale rank quantify] * ~the_pain)
		(![5 five] ~the_pain * [between rate scale rank])
		(![5 five] [10 ten] * [painful severe worst highest])
		(number * ~the_pain)
		(scale * [10 ten])
		([1 one] to [10 ten])
	])
	^factanswer("I'm not really having any pain to rate.")

# For rating scale 1-5

#! Can you rate it
u: CAN_YOU_RATE_IT
	([
		(![10 ten] [rate scale rank] * ~the_pain)
		(![10 ten] ~the_pain * [rate scale rank])
	])
	^factanswer("I'm not really having any pain to rate.")

#! How painful is it
u: HOW_PAINFUL_IS_IT (how * [painful much] * [~itwords injury hurt])    ^factanswer("It is difficult to say.")

#!!P Is the pain always a number $currentrateofpain=number
u: IS_THE_PAIN_ALWAYS_A_NUMBER (~the_pain * [usually always mostly] * $currentpainnum?)    ^factanswer("It is difficult to say.")

#! Does the pain hurt a lot
u: DOES_THE_PAIN_HURT_A_LOT
	([
		(![when how if where] does * ~the_pain *~2 hurt *~2 [much lot "great deal"])
		(is it *~2 painful >)
	])
	^factanswer("It is difficult to say.")

# General ****************************************************************************************************

#! Are you in pain
u: ARE_YOU_IN_PAIN (are you [in feeling] *~3 pain) ^factanswer("I am not having any pain.")

#! Are you sore
u: ARE_YOU_SORE (![throat mouth] 'you * [sore tender] *~2) ^factanswer("Not that I am aware of.")

#! Does the pain start somewhere else
u: DOES_THE_PAIN_START_SOMEWHERE_ELSE (~the_pain * [start begin] * [anywhere somewhere someplace anyplace "any place"] * else) ^factanswer("It is difficult to say.")

#! Do you have pain anywhere else
#! Does the pain affect any other part of your body
u: DO_YOU_HAVE_PAIN_ANYWHERE_ELSE
	([
		(![go radiate] ~the_pain * [anywhere anyplace "any place" any] *~3 [location else area part])
		(![go radiate] [anywhere anyplace "any place" anything] * else * [hurt painful sore])
		(![go radiate] [anywhere anyplace "any place" any] *~3 [location else area part] * [~the_pain hurt painful])
	])
	^factanswer("it is difficult to say.")

#! You seem uncomfortable
s: YOU_SEEM_UNCOMFORTABLE
	([
		(![questions "let me know" patient] [seem appear feel] * [uncomfortable~1 "in pain" discomfort]) # uncomfortable1 is physical discomfort
		(![questions "let me know" patient] you * [uncomfortable~1 "in pain"]) # avoids let me know if these questions make you uncomfortable
	])
	^factanswer("I'm not really uncomfortable.")

#! Can you point to the pain
u: CAN_YOU_POINT_TO_THE_PAIN
	([
		([show point])
		(![toe knee] can you touch)
	])
	^factanswer("I'm not able to do that.")

#! Does the pain limit your movement
u: DOES_PAIN_LIMIT_MOVEMENT (~the_pain *~3 [limit affect hinder] *~3 [movement motion]) ^factanswer("It is difficult to say.")

# Description  ***************

#! Does the pain radiate
#! DOes the pain move anywhere
u: DOES_THE_PAIN_RADIATE
	([
		(!["how long" when away "going on" where ~pain_descriptors describe constant "come and go"] ~the_pain * [radiate travel go spread shoot place extend "move around" widespread])
		(!["how long" when away "going on" where ~pain_descriptors describe constant "come and go"] does *~2 ~the_pain * stay)
		(!["how long" when away "going on" where ~pain_descriptors describe constant "come and go"] does *~2 ~the_pain *~1 move)
		(!["how long" when away "going on" where ~pain_descriptors describe constant "come and go"] ["same spot" "same area"])
		(!["how long" when away "going on" where ~pain_descriptors describe constant "come and go"] << [radiate radiation travel go spread shoot extend "move around"][~the_pain feeling] >>)
	])
	^factanswer("It is difficult to say.")
	a: RADIATE_HOW_FAR_DOWN ([far down]) ^factanswer("It is difficult to say.") # How far down.
	a: RADIATE_DOWN_FRONT_OR_BACK ([front back]) ^factanswer("It is difficult to say.") # Does it radiate down the front or back of your leg?
	a: RADIATE_TO_THIGH_CALF_FOOT ([thigh calf foot]) ^factanswer("It is difficult to say.") # Does it go to your thigh.
u: ([far down])	^incontext ( DOES_THE_PAIN_RADIATE) ^reuse (RADIATE_HOW_FAR_DOWN) # incontext allows all three rejoinders to match
u: ([front back])	^incontext ( DOES_THE_PAIN_RADIATE) ^reuse (RADIATE_DOWN_FRONT_OR_BACK)
u: ([thigh calf foot])	^incontext ( DOES_THE_PAIN_RADIATE) ^reuse (RADIATE_TO_THIGH_CALF_FOOT)

#! Do you have any radiating pain
u: DO_YOU_HAVE_ANY_RADIATING_PAIN (!where radiating pain) ^factanswer("It is difficult to say.")

#! Any pain on right side
u: ANY_PAIN_ON_RIGHT_SIDE (![where when did outside] ~the_pain *~3 right side)    ^factanswer("I am not sure.")

#! Any pain on left side
u: ANY_PAIN_ON_LEFT_SIDE (![where when did outside] ~the_pain *~3 left side)    ^factanswer("I am not sure.")

	#! Is the pain deep or superficial
u: IS_THE-PAIN_DEEP (~the_pain * [deep superficial]) ^factanswer("It is difficult to say.")

#! Is the pain on one side
u: IS_THE_PAIN_ON_ONE_SIDE
	([
		(![where when did outside always night how] [any ~the_pain] * [single one particular] * [~pain_locations other])
		(![where when did outside always night away how] ~the_pain * [~pain_locations])
		(![where when did outside always night away how] [what which] * ~pain_locations * ~the_pain)
		(![where when did outside always night how] [single one particular] * [~pain_locations] * [~the_pain])
	])
	^factanswer("I am not sure.")

#! Is the pain sharp
u: IS_THE_PAIN_SHARP
	([
		(![where when did "how long"] ~the_pain *~2 sharp)
		(![where when "how long"] [feel experience] *~2 ~pain_descriptors  pain)
		(![where when "how long"] [feel experience] *~2 ~pain_descriptors)
	])
	^factanswer("It is difficult to say.")

#! Is the pain dull
u: IS_THE_PAIN_DULL
	([
		(![where when did "how long"] ~the_pain *~2 dull)
		(![where when "how long"] [feel experience] *~2 ~pain_descriptors  pain)
		(![where when "how long"] [feel experience] *~2 ~pain_descriptors)
	])
	^factanswer("It is difficult to say.")

#! Is the area tender
u: IS_THE_AREA_TENDER (![where when did] [~itwords area spot point] tender) ^factanswer("It is difficult to say.")

#! Is the pain in the muscle
u: IS_THE_PAIN_IN_THE_MUSCLE (![where when did] ~the_pain * muscle) ^factanswer("I am not sure.")

#! When does the pain hurt
u: WHEN_DOES_THE_PAIN_HURT (when *~3 ~pain *~3 [present hurt]) ^factanswer("It is difficult to say.")

#!!K How did it feel
u: HOW_DID_IT_FEEL (how *~3 ~the_pain *~2 [feel felt])      ^factanswer("It is difficult to say.")
