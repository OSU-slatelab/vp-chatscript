# ***************************************************************************************************

# walking- I have defined walking_problems as a concept in MedicalConcepts.top

topic: ~currentwalking keep repeat (~walking_problems sit stair step home house)

	u: ($pasthistory) respond(~pastwalking) ^fail(TOPIC)

#! Do you feel unsteady when you walk
	?: DO_YOU_FEEL_UNSTEADY_WHEN_YOU_WALK (unsteady * ~walking_problems)  ^factanswer("I can walk fine.")

#! Can you climb stairs
	?: CAN_YOU_CLIMB_STAIRS ([climb up down up_and_down] * [stairs steps])    ^factanswer("I can climb stairs.")

#! Do you have stairs at home
	?: DO_YOU_HAVE_STAIRS_AT_HOME ([stairs steps] * [home house]) ^factanswer("I have no stairs.")

#! How far can you walk
	?: HOW_FAR_CAN_YOU_WALK
		([
			(walk * [far distance ~distance_quantity long])
			([far distance ~distance_quantity long much] * walk)
		])
	^factanswer("I can walk pretty far.")

#! Are you able to walk
	?: ARE_YOU_ABLE_TO_WALK ([can able] * walk)   ^factanswer("I can walk.")

#! Are you able to sit
	?: ARE_YOU_ABLE_TO_SIT
		([
			([can able] * sit)
			(sit * straight)
		])
	^factanswer("I can sit.")

#! Did you fall
	?: DID_YOU_FALL (!asleep [did have] * [fall fallen])   ^factanswer("I did not.")

# ***************************************************************************************************

topic: ~currentnumbness keep repeat (numb numbness tingle sensation)

	u: ($pasthistory) respond(~pastnumbness)  ^fail(TOPIC)

#! Do you have numbness or tingling
	?: DO_YOU_HAVE_NUMBNESS_OR_TINGLING (!healthcare [numb numbness tingle "loss of feeling" sensation])  ^factanswer("Not that I've noticed.")

# ***************************************************************************************************

topic: ~currentweakness keep repeat (weak weakness)

	u: ($pasthistory) respond(~pastweakness) ^fail(TOPIC)

#! Do you have any weakness
	?: DO_YOU_HAVE_ANY_WEAKNESS (![back muscle] [weak weakness strength])  ^factanswer("Not that I've noticed.")

# ***************************************************************************************************

topic: ~currentdizziness keep repeat (dizzy dizziness lightheaded lightheadedness spin "passed out" faint spin fainted "blacked out")

	u: ($pasthistory) respond(~pastdizziness) ^fail(TOPIC)

#! How long have you been dizzy
	?: HOW_LONG_HAVE_YOU_BEEN_DIZZY  (how * [have experience notice] * [dizzy dizziness lightheaded lightheadedness])  ^factanswer("I have not been dizzy")

#! Have you noticed spinning
	?: HAVE_YOU_NOTICED_SPINNING ([noticed felt experienced feel] * [spin spinning])  ^factanswer("I have not felt spinning")

#! Have you fainted
	?: HAVE_YOU_FAINTED (fainted "passed out" "blacked out")  ^factanswer("I have not fainted")

#! Are you dizzy
	?: ARE_YOU_DIZZY  ([dizzy lightheaded "light headed" lightheadedness])  ^factanswer("I am not dizzy")
