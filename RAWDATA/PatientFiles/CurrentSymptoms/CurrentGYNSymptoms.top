topic: ~currentsex keep repeat (~groin_male ~sex_problems_male ~groin_female ~sex_problems_female ~contraceptives ~spouse bleeding rape force ~dating_friend ~STI ~pain)

	u: ($pasthistory) respond(~pastsex) ^fail(TOPIC)
#	u: ($family) respond(~familysex) ^fail(TOPIC) Commented this out to avoid do you and your husband use contraception - husband triggered family

# SEXUAL HEALTH

#! Does it hurt during intercourse
	?: DOES_IT_HURT_DURING_INTERCOURSE
		([
			(<< [hurt painful bother ~pain] [sex intercourse "sexual activity"] >>)
			([sex intercourse "sexual activity"] * [increase exacerbate] * [~the_pain $relevantpain?])
		])
	^factanswer("It does not.")

#! Do you have bleeding after intercourse
	?: BLEEDING_AFTER_INTERCOURSE ([blood bleeding] * [sex intercourse "sexual activity"])    ^factanswer("I do not.")

#! Do you have a sexually transmitted infection
	?: DO_YOU_HAVE_AN_STI (![had been] [any have] * ~STI)	  ^factanswer("I do not.")

#! Have you been raped
	?: HAVE_YOU_BEEN_RAPED ([rape forced]) ^factanswer("Never.")

# CONTRACEPTION

#! Do you use any other contraception
	?: DO_YOU_USE_ANY_OTHER_CONTRACEPTION (use * other * ~contraceptives) 	^factanswer("I do not.")

#! Do you use any protection every time
	?: DO_YOU_USE_PROTECTION_EVERY_TIME (<< [always "every time"] ~contraceptives >>)     ^factanswer("Of course.")

#! Have you had unprotected sex
	?: HAVE_YOU_HAD_UNPROTECTED_SEX
		([
			(unprotected [sex intercourse])
			([sex intercourse] * without * ~contraceptives)
		])
	^factanswer("I have not.")

#! Do you use any contraception
	?: DO_YOU_USE_CONTRACEPTION
		([
			(!other !additional use * ~contraceptives)
			(!other !additional [practice use] * [safe ~contraceptives] * [intercourse sex])
			(!other !additional [what which] * ~contraceptives * [use practice])
		])
	^factanswer("I do not.")

# ###################################################################################

topic: ~currentvaginal_bleeding keep repeat (~gyn_bleeding tampon cramp cramping clot cervix cervical flow heavy light abnormal normal spotting flow irregular menses first)

	u: ($pasthistory) respond(~pastvaginal_bleeding) ^fail(TOPIC)
	u: ($family) respond(~familyvaginal_bleeding) ^fail(TOPIC)

# BLEEDING

#! Tell me more about your vaginal bleeding
	?: TELL_ME_MORE_ABOUT_YOUR_VAGINAL_BLEEDING (tell * [more some bit] * [~itwords vaginal bleeding spotting])   ^factanswer("I have none")

#! Are there any clots
	?: ARE_THERE_CLOTS ([clot clots clotting])  ^factanswer("There are none.")

# PERIODS

#! When was the first day of your last period
	?: FIRST_DAY_OF_LAST_PERIOD  (!"vaginal bleeding" first day * [last recent prior previous] * ~gyn_bleeding)   ^factanswer("I don't have those.")

#! When was your last period
	?: WHEN_WAS_YOUR_LAST_PERIOD
		([
			(!"vaginal bleeding" [when "how long ago"] * [last recent prior previous stop] * [~gyn_bleeding one])
			(!"vaginal bleeding" [when "how long ago"] * [~gyn_bleeding one] * [last recent prior previous stop])
		])
	^factanswer("I don't have those.")

#! Are you currently having your period
	?: ARE_YOU_HAVING_YOUR_PERIOD  (are you [currently on] * ~gyn_bleeding)   ^factanswer("I don't have those.")

#! Are your periods irregular
	?: ARE_YOUR_PERIODS_IRREGULAR  (<<[~gyn_bleeding ~itwords] [irregular abnormal] >>)   ^factanswer("I don't have those.")

#! Are your periods regular
	?: ARE_YOUR_PERIODS_REGULAR  (<<[~gyn_bleeding ~itwords] [regular normal] >>)   ^factanswer("I don't have those.")

#! Is your period late
	?: IS_YOUR_PERIOD_LATE  ([~gyn_bleeding ~itwords] late)  ^factanswer("I don't have those.")

#! How long do your periods last
	?: HOW_LONG_DO_YOUR_PERIODS_LAST
		([
			([!ago often pad tampon] [duration "how long"] * [~gyn_bleeding flow])
			([!ago often pad tampon] [~gyn_bleeding flow] * [duration last])
		])
	^factanswer("I don't have periods. ")

#! Can you describe your periods
	?: CAN_YOU_DESCRIBE_YOUR_PERIODS  ([tell ~describe description] * [~gyn_bleeding])   ^factanswer("I don't have those.")

#! What is the flow of your period like
	?: WHAT_IS_FLOW_LIKE
		([
			(!always [what how is] * [flow ~gyn_bleeding])
			(!always [heavy light amount] * [menses menstrual blood ~gyn_bleeding])
 			(!always [menses menstrual blood ~gyn_bleeding] * [heavy light amount])
		])
	^factanswer("I am not sure.")

#! How often are your periods
	?: HOW_OFTEN_ARE_YOUR_PERIODS (<< [frequent often] [~gyn_bleeding] >>) ^factanswer("I don't have those.")

#! Are your periods painful
	?: ARE_YOUR_PERIODS_PAINFUL
		([
			([~gyn_bleeding ~itwords] * [hurt painful])
			([hurt painful] * [~gyn_bleeding ~itwords])
		])
	^factanswer("My periods are fine.")

#! Does pain change with period
	?: DOES_PAIN_CHANGE_WITH_PERIOD (pain * [change better worse increase decrease lessen worsen] * ~gyn_bleeding) ^factanswer("My periods are fine.")

#! Do you use tampons
	?: Use_tampons ([tampons pads])	^factanswer("I do not.")
		a: HOW_MANY_TAMPONS ([many number amount]) ^factanswer("I don't use those")

#! Do you have bloating with period
	?: DO_YOU_HAVE_BLOATING_WITH_PERIOD ([bloat bloating "water retention"] * [~gyn_bleeding]) ^factanswer("I do not.")

# MENSTRUAL CYCLE

#! Have you had any menstrual changes
	?: HAVE_YOU_HAD_ANY_MENSTRUAL_CHANGES
		([
			(["have you had" 'are] * ~gyn_bleeding [change different])
			(["have you had" 'are] * [change different] * ~gyn_bleeding)
		])
	^factanswer("My menstrual cycle has been normal")

#! Have you had any spotting
	?: HAVE_YOU_HAD_ANY_SPOTTING (["have you had"] * [bleeding spotting])    ^factanswer("I have not.")

#! Tell me about your menstrual cycles
	?: TELL_ME_ABOUT_YOUR_MENSTRUAL_CYCLES (["tell me" describe] * [~gyn_bleeding])   ^factanswer("They are fine.")

#! When did you have your first period
	?: WHEN_DID_YOU_HAVE_YOUR_FIRST_PERIOD
		([
			([when how] *  [began first start] * [~gyn_bleeding menstruation menstrual cycle])
			([when how] *  [~gyn_bleeding menstruation menstrual cycle] * [began first start])
		])
	^factanswer("I don't remember.")

# PAIN

#! Are you having cramps
	?: ARE_YOU_HAVING_CRAMPS (![where try when] [cramps cramping])  ^factanswer("No, None.")

#! Any vaginal pain
	?: ANY_VAGINAL_PAIN (<< [vaginal vagina] [pain discomfort] >>) ^factanswer("Not really.")

#! Any vaginal sores
	?: ANY_VAGINAL_SORES (<< [vaginal vagina] [sores spots rash] >>) ^factanswer("None.")

#! Do you have any sores on your groin
	?: DO_YOU_HAVE_ANY_SORES_ON_YOUR_GROIN (["sores * ~groin" "bleeding * ~groin_male" ~groin_male "bleeding * groin_female" ~groin_female])    ^factanswer("No.")

# ###################################################################################
topic: ~currentvaginalsymptoms keep repeat (~gyn_bleeding vagina vaginal douche vulva)

	u: ($pasthistory) respond(~pastvaginalsymptoms) ^fail(TOPIC)
	u: ($family) respond(~familyvaginalsymptoms) ^fail(TOPIC)

#! Any vaginal dryness
	?: ANY_VAGINAL_DRYNESS (<< [vaginal vagina area] dryness >>) ^factanswer("I have none of that.")

#! Do you douche
	?: DO_YOU_DOUCHE (douche) ^factanswer("I do not.")

# ###################################################################################

topic: ~current_pregnancy keep repeat (~gyn_bleeding pregnant miscarry miscarriage pregnancy vaginal vaginally c-section cesarean born)

	u: ($pasthistory) respond(~past_pregnancy) ^fail(TOPIC)
	u: ($family) respond(~family_pregnancy) ^fail(TOPIC)

#! Could you be pregnant
	?: COULD_YOU_BE_PREGNANT (!ever !past [could might think possible possibility chance] * pregnant >) ^factanswer("No.")

#! Do you want to be pregnant
	?: WANT_TO_BE_PREGNANT (!ever !past [want desire try trying] * pregnant >) ^factanswer("No.")

#! Are you pregnant
	?: ARE_YOU_PREGNANT (are you * pregnant)	 ^factanswer("I don't think so.")

#! Have you taken a pregnancy test
	?: HAVE_YOU_TAKEN_PREGNANCY_TEST ([take taken checked done "have you had" "have you"] * [pregnant pregnancy] * [test kit strip])  ^factanswer("I have not.")

#! Have you miscarried
	?: HAVE_YOU_MISCARRIED  ([miscarry miscarried miscarriage])  ^factanswer("I have not had a miscarriage.")

#! How many pregnancies
	?: HOW_MANY_PREGNANCIES (how * [many number time] * [pregnant pregnancy]) ^factanswer("I have never been pregnant.")

#! When was last time you were pregnant
	?: WHEN_WAS_LAST_TIME_YOU_WERE_PREGNANT ([when how] * "last time" * [pregnant pregnancy deliver birth]) ^factanswer("I have never been pregnant.")

#! Describe pregnancy
	?: DESCRIBE_PREGNANCY
		([
			([describe "how was"] * [pregnant pregnancy] >)
			(<< [pregnant pregnancy delivery born] [problem issues free easy normal complications go "all right" vaginal vaginally c-section cesarean] >>)
		])
	^factanswer("I have never been pregnant.")

# ###################################################################################

topic: ~current_breast keep repeat (breast ~breast_problems feed breastfeed)

	u: ($pasthistory) respond(~past_breast) ^fail(TOPIC)
	u: ($family) respond(~family_breast) ^fail(TOPIC)

#! Any breast changes
	?: ANY_BREAST_CHANGES
		([
			(~Breast_Problems * [change size tenderness])
			(~Breast_Problems)
		])
	^factanswer("No breast changes.")

#! Are you breastfeeding
	?: ARE_YOU_BREASTFEEDING ([breastfeed breastfeeding])  ^factanswer("I am not breastfeeding.")

# ###################################################################################

topic: ~menopause keep repeat (menopause perimenopause ~gyn_problems menopausal)

##	u: ($pasthistory) respond(~pastgyn) ^fail(TOPIC)    No reason to key on family or past here. Will include all menopause questions here.
##	u: ($family) respond(~familygyn) ^fail(TOPIC)

#! Do you have hot flashes
	?: DO_YOU_HAVE_HOT_FLASHES  (hot * [flash flush])   ^factanswer("I do not.")

#! Are you in menopause
	?: ARE_YOU_IN_MENOPAUSE  ([menopause perimenopause menopausal])   ^factanswer("I am not.")
