topic: ~currentsex keep repeat (~groin_male ~sex_problems_male ~groin_female ~sex_problems_female ~contraceptives ~spouse bleeding rape force ~dating_friend ~STI ~pain)

u: ($pasthistory) respond(~pastsex) ^fail(TOPIC)
#	u: ($family) respond(~familysex) ^fail(TOPIC) Commented this out to avoid do you and your husband use contraception - husband triggered family

# SEXUAL HEALTH

#! Does it hurt during intercourse
u: DOES_IT_HURT_DURING_INTERCOURSE
	([
		(<< [hurt painful bother ~pain] [sex intercourse "sexual activity"] >>)
		([sex intercourse "sexual activity"] * [increase exacerbate] * [~the_pain $relevantpain?])
	])
	^factanswer("I have not had any problems like that.")

#! Do you have bleeding after intercourse
u: BLEEDING_AFTER_INTERCOURSE ([blood bleeding] * [sex intercourse "sexual activity"])    ^factanswer("I have not had any problems like that.")

#! Do you have a sexually transmitted infection
u: DO_YOU_HAVE_AN_STI (![had been] [any have] * ~STI)	  ^factanswer("I have not had any problems like that.")

#! Have you been raped
u: HAVE_YOU_BEEN_RAPED ([rape forced]) ^factanswer("Never.")

# CONTRACEPTION

#! Do you use any other contraception
u: DO_YOU_USE_ANY_OTHER_CONTRACEPTION (use * other * ~contraceptives) 	^factanswer("I do not.")

#! Do you use any protection every time
u: DO_YOU_USE_PROTECTION_EVERY_TIME (<< [always "every time"] ~contraceptives >>)     ^factanswer("Yes.")

#! Have you had unprotected sex
u: HAVE_YOU_HAD_UNPROTECTED_SEX
	([
		(unprotected [sex intercourse])
		([sex intercourse] * without * ~contraceptives)
	])
	^factanswer("I have not.")

#! Do you use any contraception
u: DO_YOU_USE_CONTRACEPTION
	([
		(!other !additional use * ~contraceptives)
		(!other !additional [practice use] * [safe ~contraceptives] * [intercourse sex])
		(!other !additional [what which] * ~contraceptives * [use practice])
	])
	^factanswer("I do not.")

# ###################################################################################

# only use for female patients

topic: ~currentvaginal_bleeding keep repeat bot=patient4,patient5,patient6,patient7,patient11,patient12,patient13,patient15,patient19,patient20
(~gyn_bleeding tampon cramp cramping clot cervix cervical flow heavy light abnormal normal spotting flow irregular menses first)

u: ($pasthistory) respond(~pastvaginal_bleeding) ^fail(TOPIC)
# u: ($family) respond(~familyvaginal_bleeding) ^fail(TOPIC) inactivated this to avoid "they" triggering family. Are they abnormal

# BLEEDING

#! Tell me more about your vaginal bleeding
u: TELL_ME_MORE_ABOUT_YOUR_VAGINAL_BLEEDING (tell * [more some bit] * [~itwords vaginal bleeding spotting])   ^factanswer("I have not had any problems like that.")

#! Are there any clots
u: ARE_THERE_CLOTS ([clot clots clotting])  ^factanswer("I have not had any problems like that.")

# PERIODS

#! When was the first day of your last period
u: FIRST_DAY_OF_LAST_PERIOD  (!"vaginal bleeding" first day * [last recent prior previous] * ~gyn_bleeding)   ^factanswer("I am not sure.")

#! When was your last period
u: WHEN_WAS_YOUR_LAST_PERIOD
	([
		(!"vaginal bleeding" [when "how long ago"] * [last recent prior previous stop] * [~gyn_bleeding one])
		(!"vaginal bleeding" [when "how long ago"] * [~gyn_bleeding one] * [last recent prior previous stop])
	])
	^factanswer("I am not sure.")

#! Are you currently having your period
u: ARE_YOU_HAVING_YOUR_PERIOD  (are you [currently on] * ~gyn_bleeding)   ^factanswer("I am having normal periods.")

#! Are your periods irregular
u: ARE_YOUR_PERIODS_IRREGULAR
	([
		(<< ~gyn_bleeding [irregular abnormal] >>)
		(~ << ~itwords [irregular abnormal] >>) # only if already in topic
	])
  ^factanswer("I am not sure.")

#! Are your periods regular
u: ARE_YOUR_PERIODS_REGULAR
	([
		(<<~gyn_bleeding [regular normal] >>)
		(~ << ~itwords [regular normal] >>) # only if already in topic
	])
 	^factanswer("I am not sure.")

#! Is your period late
u: IS_YOUR_PERIOD_LATE  ([~gyn_bleeding ~itwords] late)  ^factanswer("I am not sure.")

#! How long do your periods last
u: HOW_LONG_DO_YOUR_PERIODS_LAST
	([
		([!ago often pad tampon] [duration "how long"] * [~gyn_bleeding flow])
		([!ago often pad tampon] [~gyn_bleeding flow] * [duration last])
	])
	^factanswer("I am not sure.")

#! Can you describe your periods
u: CAN_YOU_DESCRIBE_YOUR_PERIODS  ([tell ~describe description] * [~gyn_bleeding])   ^factanswer("I don't have those.")

#! What is the flow of your period like
u: WHAT_IS_FLOW_LIKE
	([
		(!always [what how is] * [flow ~gyn_bleeding])
		(!always [heavy light amount] * [menses menstrual blood ~gyn_bleeding])
		(!always [menses menstrual blood ~gyn_bleeding] * [heavy light amount])
	])
	^factanswer("I am not sure.")

#! How often are your periods
u: HOW_OFTEN_ARE_YOUR_PERIODS (<< [frequent often] [~gyn_bleeding] >>) ^factanswer("I am not sure.")

#! Are your periods painful
u: ARE_YOUR_PERIODS_PAINFUL
	([
		([~gyn_bleeding ~itwords] * [hurt painful])
		([hurt painful] * [~gyn_bleeding ~itwords])
	])
	^factanswer("I have not had any problems like that.")

#! Does pain change with period
u: DOES_PAIN_CHANGE_WITH_PERIOD (pain * [change better worse increase decrease lessen worsen] * ~gyn_bleeding) ^factanswer("I have not had any problems like that.")

#! Do you use tampons
u: Use_tampons ([tampons pads])	^factanswer("I do not.")
	a: HOW_MANY_TAMPONS ([many number amount]) ^factanswer("I don't use those")

#! Do you have bloating with period
u: DO_YOU_HAVE_BLOATING_WITH_PERIOD ([bloat bloating "water retention"] * [~gyn_bleeding]) ^factanswer("I have not had any problems like that.")

# MENSTRUAL CYCLE

#! Have you had any menstrual changes
u: HAVE_YOU_HAD_ANY_MENSTRUAL_CHANGES
	([
		(["have you had" 'are] * ~gyn_bleeding [change different])
		(["have you had" 'are] * [change different] * ~gyn_bleeding)
	])
	^factanswer("I have not had any problems like that.")

#! Have you had any spotting
u: HAVE_YOU_HAD_ANY_SPOTTING (["have you had"] * [bleeding spotting])    ^factanswer("I am not sure.")

#! Tell me about your menstrual cycles
u: TELL_ME_ABOUT_YOUR_MENSTRUAL_CYCLES (["tell me" describe] * [~gyn_bleeding])   ^factanswer("They are fine.")

#! When did you have your first period
u: WHEN_DID_YOU_HAVE_YOUR_FIRST_PERIOD
	([
		([when how] *  [began first start] * [~gyn_bleeding menstruation menstrual cycle])
		([when how] *  [~gyn_bleeding menstruation menstrual cycle] * [began first start])
	])
	^factanswer("I am not sure.")

# PAIN

#! Are you having cramps
u: ARE_YOU_HAVING_CRAMPS (![where try when] [cramps cramping])  ^factanswer("I have not had any problems like that.")

#! Any vaginal pain
u: ANY_VAGINAL_PAIN (<< [vaginal vagina] [pain discomfort] >>) ^factanswer("I have not had any problems like that.")

#! Any vaginal sores
u: ANY_VAGINAL_SORES (<< [vaginal vagina] [sores spots rash] >>) ^factanswer("I have not had any problems like that.")

#! Do you have any sores on your groin
u: DO_YOU_HAVE_ANY_SORES_ON_YOUR_GROIN (["sores * ~groin" "bleeding * ~groin_male" ~groin_male "bleeding * groin_female" ~groin_female])    ^factanswer("I have not had any problems like that.")

# ###################################################################################
# only for female patients
topic: ~currentvaginalsymptoms keep repeat bot=patient4,patient5,patient6,patient7,patient11,patient12,patient13,patient15,patient19,patient20
(~gyn_bleeding vagina vaginal douche vulva)

u: ($pasthistory) respond(~pastvaginalsymptoms) ^fail(TOPIC)
u: ($family) respond(~familyvaginalsymptoms) ^fail(TOPIC)

#! Any vaginal dryness
u: ANY_VAGINAL_DRYNESS (<< [vaginal vagina area] dryness >>) ^factanswer("I have not had any problems like that.")

#! Do you douche
u: DO_YOU_DOUCHE (douche) ^factanswer("I do not.")

# ###################################################################################
# only for female patients

topic: ~current_pregnancy keep repeat bot=patient4,patient5,patient6,patient7,patient11,patient12,patient13,patient15,patient19,patient20
(~gyn_bleeding pregnant miscarry miscarriage pregnancy vaginal vaginally c-section cesarean born)

u: ($pasthistory) respond(~past_pregnancy) ^fail(TOPIC)
u: ($family) respond(~family_pregnancy) ^fail(TOPIC)

#! Could you be pregnant
u: COULD_YOU_BE_PREGNANT (!ever !past [could might think possible possibility chance] * pregnant >) ^factanswer("No.")

#! Do you want to be pregnant
u: WANT_TO_BE_PREGNANT (!ever !past [want desire try trying] * pregnant >) ^factanswer("No.")

#! Are you pregnant
u: ARE_YOU_PREGNANT (are you * pregnant)	 ^factanswer("I don't think so.")

#! Have you taken a pregnancy test
u: HAVE_YOU_TAKEN_PREGNANCY_TEST ([take taken checked done "have you had" "have you"] * [pregnant pregnancy] * [test kit strip])  ^factanswer("I have not.")

#! Have you miscarried
u: HAVE_YOU_MISCARRIED  ([miscarry miscarried miscarriage])  ^factanswer("I have not had a miscarriage.")

#! How many pregnancies
u: HOW_MANY_PREGNANCIES (how * [many number time] * [pregnant pregnancy]) ^factanswer("I have never been pregnant.")

#! When was last time you were pregnant
u: WHEN_WAS_LAST_TIME_YOU_WERE_PREGNANT ([when how] * "last time" * [pregnant pregnancy deliver birth]) ^factanswer("I have never been pregnant.")

#! Describe pregnancy
u: DESCRIBE_PREGNANCY
	([
		([describe "how was"] * [pregnant pregnancy] >)
		(<< [pregnant pregnancy delivery born] [problem issues free easy normal complications go "all right" vaginal vaginally c-section cesarean] >>)
	])
	^factanswer("I have never been pregnant.")

# ###################################################################################

topic: ~current_breast keep repeat (breast ~breast_problems feed breastfeed)

u: ($pasthistory) respond(~past_breast) ^fail(TOPIC)
u: ($family) respond(~family_breast) ^fail(TOPIC)

#! Any breast changes
u: ANY_BREAST_CHANGES
	([
		(~Breast_Problems * [change size tenderness])
		(~Breast_Problems)
	])
	^factanswer("Not that I am aware of.")

#! Are you breastfeeding
u: ARE_YOU_BREASTFEEDING ([breastfeed breastfeeding])  ^factanswer("I am not breastfeeding.")

# ###################################################################################
# only for female patients

topic: ~menopause keep repeat bot=patient4,patient5,patient6,patient7,patient11,patient12,patient13,patient15,patient19,patient20
(menopause perimenopause ~gyn_problems menopausal)

##	u: ($pasthistory) respond(~pastgyn) ^fail(TOPIC)    No reason to key on family or past here. Will include all menopause questions here.
##	u: ($family) respond(~familygyn) ^fail(TOPIC)

#! Do you have hot flashes
u: DO_YOU_HAVE_HOT_FLASHES  (hot * [flash flush])   ^factanswer("I have not had any problems like that.")

#! Are you in menopause
u: ARE_YOU_IN_MENOPAUSE  ([menopause perimenopause menopausal])   ^factanswer("Not that I am aware of.")
