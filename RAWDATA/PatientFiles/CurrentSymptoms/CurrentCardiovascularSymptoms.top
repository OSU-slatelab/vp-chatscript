# ***************************************************************************************************

# Blood Pressure - I have defined blood_pressure as a concept in MedicalConcepts.top

topic: ~currentbloodpressure keep repeat ("blood pressure" high low normal hypertension ~high_blood_pressure)

	u: ($pasthistory) respond(~pastbloodpressure)  ^fail(TOPIC)
	u: ($family) respond(~familybloodpressure) ^fail(TOPIC)

#! How is your blood pressure
	?: HOW_IS_YOUR_BLOOD_PRESSURE (![urine pee] ["blood pressure" ~high_blood_pressure])   ^factanswer("My blood pressure has been normal.")

# ***************************************************************************************************

topic: ~currentchestpain keep repeat (~chest_pain chest)

	u: ($pasthistory) respond(~pastchestpain)  ^fail(TOPIC)
	u: ($family) respond(~familychestpain) ^fail(TOPIC)

#! Any chest pain
	?: ANY_CHEST_PAIN (~chest_pain)   ^factanswer("I have no chest pain.")

# ***************************************************************************************************

topic: ~currentheartproblem keep repeat (~chest_pain heart race cardiovascular)

	u: ($pasthistory) respond(~pastheartproblem)  ^fail(TOPIC)
	u: ($family) respond(~familyheartproblem) ^fail(TOPIC)

#! Any heart problems
	?: ANY_HEART_PROBLEMS (heart * [wrong condition issue problem trouble disease race])   ^factanswer("I have no heart problems.")

# ***************************************************************************************************

topic: ~currentvascular keep repeat (~cardiovascular image probe catheter ultrasound test check cholesterol)

	u: ($pasthistory) respond(~pastvascular)  ^fail(TOPIC)
	u: ($family) respond(~familyvascular) ^fail(TOPIC)

#! Any vascular problems
	?: ANY_VASCULAR_PROBLEMS
		([
			(~cardiovascular * [wrong condition issue problem trouble disease])
			([wrong condition issue problem trouble disease] * ~cardiovascular)
		])
	^factanswer("I have no vascular problems.")


#! Any vascular testing
	?: ANY_VASCULAR_TESTING
		([
			(~cardiovascular * [test image probe catheter ultrasound check])
			([test image probe catheter ultrasound check] * ~cardiovascular)
		])
	^factanswer("I have not had any vascular testing.")

#! Any clotting problems
	?: ANY_CLOTTING_PROBLEMS
		([
			([have any experience] * ["blood clot" clot])
			([blood vascular] * [clot problems thick thickness plaques embolism])
		])
	^factanswer("I have no clotting problems.")

#! How is your cholesterol
	?: HOW_IS_YOUR_CHOLESTEROL (cholesterol) ^factanswer ("My cholesterol is fine")
