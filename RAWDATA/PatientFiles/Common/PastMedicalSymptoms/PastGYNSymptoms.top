topic: ~pastsex keep repeat (problem sexual sex sexually intercourse ~groin_male ~sex_problems_male ~groin_female ~sex_problems_female infection)

#	u: ($present) respond(~currentsex) ^fail(TOPIC)
u: ($family) respond(~familysex) ^fail(TOPIC)

# SEXUAL HEALTH

#! Did it ever hurt during intercourse
u: DID_IT_EVER_HURT_DURING_INTERCOURSE
	([
		(<< [hurt painful bother ~pain] [sex intercourse "sexual activity"] >>)
		([sex intercourse "sexual activity"] * [increase exacerbate] * [~the_pain $relevantpain?])
	])
	^factanswer("It never has.")

#! Did you ever have bleeding after intercourse
u: EVER_HAVE_BLEEDING_AFTER_INTERCOURSE ([blood bleeding bleed] * [sex intercourse "sexual activity"])    ^factanswer("Never.")


# only for female patients

topic: ~pastvaginal_bleeding keep repeat bot=patient4,patient5,patient6,patient7,patient11,patient12,patient13,patient15,patient19,patient20,patient22
(~gyn_bleeding tampon cramp cramping clot cervix cervical flow heavy light abnormal normal spotting flow irregular always bloat bloating vaginal groin)

u: ($present) respond(~currentvaginal_bleeding) ^fail(TOPIC)
u: ($family) respond(~familyvaginal_bleeding) ^fail(TOPIC)

# BLEEDING

#! Have you ever had vaginal bleeding like this before
u: HAVE_YOU_HAD_VAGINAL_BLEEDING_BEFORE
	([
		(![discharge itching] [ "vaginal bleeding" vaginal bleeding spotting] * [before prior past history ever previously])
		([ever past] * ["vaginal bleeding" vaginal bleeding spotting ])
	])
	^factanswer("No, never.")

#! Were there any clots
u: WERE_THERE_CLOTS ([clot clots clotting])  ^factanswer("There were none.")

# PERIODS

#! Were your periods ever irregular
u: WERE_YOUR_PERIODS_EVER_IRREGULAR  (<<[~gyn_bleeding ~itwords] [irregular abnormal] >>)   ^factanswer("Never.")

#! Were your periods regular before
u: WERE_YOUR_PERIODS_REGULAR_BEFORE  (<<[~gyn_bleeding ~itwords] [regular normal] >>)   ^factanswer("I don't have those.")

#! Was your period ever late
u: WAS_YOUR_PERIOD_EVER_LATE  ([~gyn_bleeding ~itwords] * late)   ^factanswer("I don't have those.")

#! How long did your periods last before
u: HOW_LONG_DID_YOUR_PERIODS_LAST_BEFORE
	([
		([!ago often pad tampon] [duration "how long"] * [~gyn_bleeding flow])
		([!ago often pad tampon] [~gyn_bleeding flow] * [duration last])
	])
	^factanswer("I don't know.")

#! Describe your past periods
u: DESCRIBE_YOUR_PAST_PERIODS  ([tell ~describe description] * [~gyn_bleeding])   ^factanswer("I don't have those.")

#! What was the flow of your past periods like
u: WHAT_WAS_PAST_FLOW_LIKE
	([
		([what how were] * [flow ~gyn_bleeding ~itwords])
		(amount *~3 menstrual blood)
		([period flow] * [always before past] * [heavy light])
	])
	^factanswer("I had no flow.")

#! How often were your past periods
u: HOW_OFTEN_WERE_YOUR_PAST_PERIODS (<< [frequent often] [~gyn_bleeding] >>)      ^factanswer("I don't know.")

#! Were your past periods painful
u: WERE_YOUR_PAST_PERIODS_PAINFUL
	([
		([~gyn_bleeding ~itwords] * [hurt painful cramp])
		([~pain hurt painful cramp cramping] * [~gyn_bleeding ~itwords])
	])
	^factanswer("My periods are fine.")

#! Did pain change with past period
u: DID_PAIN_CHANGE_WITH_PAST_PERIOD (pain * [change better worse increase decrease lessen worsen] * ~gyn_bleeding) ^factanswer("My periods are fine.")

#! Did you ever menstruate
u: DID_YOU_EVER_MENSTRUATE (![sex intercourse] [have do had did] * [menstruate menstrual])  ^factanswer("I used to.")

#! Did you ever use tampons
u: EVER_USE_TAMPONS (![many number amount] [tampons pads])	^factanswer("No.")

#! How many tampons before
u: HOW_MANY_TAMPONS_BEFORE
	([
		([many number amount] * [tampons pads] * [before past])
		([many number amount] * [tampons pads] * day)
	])
	^factanswer("I never used those")

#! Ever have bloating with period
u: EVER_HAVE_BLOATING_WITH_PERIOD
	([
		([bloat bloating "water retention"] * [~gyn_bleeding])
		([ever notice feel experience] * [bloat bloating "water retention"])
	])
	^factanswer("I have never.")

# MENSTRUAL CYCLE

#! Ever had any menstrual changes
u: EVER_HAD_ANY_MENSTRUAL_CHANGES
	([
		(!mood ~gyn_bleeding [change different])
		(!mood [change different] * ~gyn_bleeding)
	])
	^factanswer("No, not really.")

#! Ever had any spotting
u: EVER_HAD_ANY_SPOTTING (["have you had" ever] * [bleeding spotting]) ^factanswer("No, not really.")

#! Tell me about your past menstrual cycles
u: TELL_ME_ABOUT_YOUR_PAST_MENSTRUAL_CYCLES (tell me * [~gyn_bleeding])   ^factanswer("They were fine.")

#! When was your first menstrual cycle
u: WHEN_WAS_YOUR_FIRST_MENSTRUAL_CYCLE
	([
		([when how] *  [began first start] * [~gyn_bleeding menstruation menstrual cycle])
		([when how] *  [~gyn_bleeding menstruation menstrual cycle] * [began first start])
	])
	^factanswer("I don't remember.")

# PAIN

#! Ever had cramps
u: EVER_HAD_CRAMPS (!better [cramps cramping])  ^factanswer("No, None.")

#! Any vaginal pain before
u: ANY_VAGINAL_PAIN_BEFORE (<< [vaginal vagina] [pain discomfort] >>) ^factanswer("Not really.")

#! Any prior vaginal sores
u: ANY_PRIOR_VAGINAL_SORES (<< [vaginal vagina] [sores spots rash] >>) ^factanswer("None.")

#! Ever had sores on your groin
u: EVER_HAD_SORES_ON_YOUR_GROIN (["sores * ~groin" "bleeding * ~groin_male" ~groin_male "bleeding * groin_female" ~groin_female])   ^factanswer("No.")

# only for female patients

topic: ~pastvaginalsymptoms keep repeat bot=patient4,patient5,patient6,patient7,patient11,patient12,patient13,patient15,patient19,patient20,patient22
(~gyn_bleeding discharge odor itch douche vulva vaginal)

u: ($present) respond(~currentvaginalsymptoms) ^fail(TOPIC)
u: ($family) respond(~familyvaginalsymptoms) ^fail(TOPIC)

#! Ever had vaginal dryness
u: EVER_HAD_VAGINAL_DRYNESS (<< [vaginal vagina area] [dryness redness] >>) ^factanswer("Not really.")

#! Ever douche
u: EVER_DOUCHE (douche) ^factanswer("No.")

# only for female patients

topic: ~past_pregnancy keep repeat bot=patient4,patient5,patient6,patient7,patient11,patient12,patient13,patient15,patient19,patient20,patient22
(~gyn_bleeding pregnant miscarry miscarriage pregnancy times vaginally C-section caesarean cesarean deliver birth born "last time" recent)

u: ($present) respond(~current_pregnancy) ^fail(TOPIC)
u: ($family) respond(~family_pregnancy) ^fail(TOPIC)

#! When were you pregnant
u: WHEN_WERE_YOU_PREGNANT
	([
		(![many times] [when how] * [pregnant pregnancies deliver 'birth])
		(![many times] [when how] * [pregnant pregnancies] * [ever prior past before previous])
	])
	^factanswer("I have never been pregnant.")

#! Did you ever want to be pregnant
u: EVER_WANT_TO_BE_PREGNANT ([want desire try trying] * pregnant >) ^factanswer("No.")

#! Have you ever taken a pregnancy test before
u: EVER_TAKEN_PREGNANCY_TEST_BEFORE ([take taken checked done "have you had" "have you"] * [pregnant pregnancy] * [test kit strip])  ^factanswer("No I have not.")

#! Have you ever miscarried before
u: EVER_MISCARRIED_BEFORE  (ever * [miscarry miscarried miscarriage])  ^factanswer("I have never miscarried.")

#! Have you ever been pregnant before
u: EVER_BEEN_PREGNANT_BEFORE (<< [ever prior past before previous many times] [pregnant pregnancies] >>) ^factanswer("I have never been pregnant.")

#! Describe past pregnancy
u: DESCRIBE_PAST_PREGNANCY
	([
		([describe "how was"] * [past previous] * [pregnant pregnancy] >)
		([pregnant pregnancy delivery deliver born] * [problem issues free easy normal complications go "all right" vaginally C-section caesarean cesarean])
	])
	^factanswer("I have never been pregnant.")


topic: ~past_breast keep repeat (breast ~breast_problems)

u: ($present) respond(~current_breast) ^fail(TOPIC)
u: ($family) respond(~family_breast) ^fail(TOPIC)

#! Any past breast changes
u: ANY_PAST_BREAST_CHANGES
	([
		(~Breast_Problems * [change size tenderness])
		(~Breast_Problems)
	])
	^factanswer("No breast changes.")
