topic: ~allergies keep repeat (allergy allergic reaction)

u: ( $$family) ^fail(TOPIC)

#! Any other allergies
u: ANY_OTHER_ALLERGIES
	([
		(<< [other else additional] [allergies allergic reaction] >>)
		])
		^factanswer("I am not allergic to anything.")

#! Do you have any food or environmental allergies
u: FOOD_OR_ENVIRONMENTAL_ALLERGIES
	(![other else additional]
		[
		(<< [food environment environmental pollen bee sting peanut hay season seasonal] [allergy allergies allergic] >>) # season and seasonal added by Lexi
		([allergy allergies allergic] * [food environment environmental pollen bee sting peanut hay season seasonal])
	])
	^factanswer("I am not allergic to anything.")
		a: WHAT_ARE_ALLERGY_REACTIONS ([react reaction symptoms happens]) ^factanswer("I am not sure.")

#! Do you take medication for allergies
u: DO_YOU_TAKE_MEDICATION_FOR_ALLERGIES
	([
		([use take on prescribed] *~4 [~itwords allergy])
		(~medicines *~3 [take prescribed] *~3 allergy)
		(allergy *~3 ~medicines *~3 [take prescribed use on])
	])
	^factanswer("I am not allergic to anything.")
		a: HOW_MUCH_ALLERGY_MEDICATION ([many much dose dosage]) ^factanswer("I don't take anything for allergies.")
		a: HOW_OFTEN_ALLERGY_MEDICATION (["as needed" often frequently]) ^factanswer("I don't take anything for allergies.")
		a: DOES_ALLERGY_MEDICATION_HELP ([help work alleviate]) ^factanswer("I don't take anything for allergies.")
		a: TAKE_ALLERGY_MEDICATION_TODAY ([take use] * ~timename) ^factanswer("I don't take anything for allergies.")
u: ([many much dose dosage] ^incontext(DO_YOU_TAKE_MEDICATION_FOR_ALLERGIES)) ^reuse(HOW_MUCH_ALLERGY_MEDICATION)
u: (["as needed" often frequently] ^incontext(DO_YOU_TAKE_MEDICATION_FOR_ALLERGIES)) ^reuse(HOW_OFTEN_ALLERGY_MEDICATION)
u: ([help work alleviate] ^incontext(DO_YOU_TAKE_MEDICATION_FOR_ALLERGIES)) ^reuse(DOES_ALLERGY_MEDICATION_HELP)
u: ([take use] * ~timename ^incontext(DO_YOU_TAKE_MEDICATION_FOR_ALLERGIES)) ^reuse(TAKE_ALLERGY_MEDICATION_TODAY)

#! how severe is your allergy
u: SEVERITY_OF_ALLERGY_REACTIONS (<< [bad severe severity] [allergy allergic reaction] >>) ^factanswer("I am not sure.")

#! How did you find out you were allergic
u: FIND_OUT_ALLERGIC
	([
		(["find out" discover realize] ~) # ~ restricts to current topic
	])
	^factanswer("I don't remember.")

#! When were you diagnosed with an iodine allergy
#! How long have you had an iodine allergy
u: WHEN_WERE_YOU_DIAGNOSED_WITH_ALLERGY ([when “how long”] * [diagnosed had] * allergy)	 ^factanswer("I am not allergic to anything.") # added by Lexi and Marisa changed the factanswer

#! What are your reactions to allergy
u: WHAT_ARE_ALLERGY_REACTIONS (what * [react reaction symptoms happens allergy]) ^factanswer("I am not sure.") 
	a: ([bad severe severity]) ^reuse(SEVERITY_OF_ALLERGY_REACTIONS)

#! Do you have any allergies
u: DO_YOU_HAVE_ANY_ALLERGIES (![other else additional] [allergy allergies allergic]) ^factanswer("I am not allergic to anything.")
  	a: WHAT_ARE_ALLERGY_REACTIONS ([react reaction symptoms happens]) ^factanswer("I am not sure.")
	a: SEVERITY_OF_REACTIONS ([bad severe severity]) ^reuse(SEVERITY_OF_ALLERGY_REACTIONS)
	a: WHEN_DIAGNOSED_WITH_ALLERGY ([when "how long"]) ^reuse(WHEN_WERE_YOU_DIAGNOSED_WITH_ALLERGY)
		
#! Any reactions to anything
u: ANY_REACTIONS_TO_ANYTHING (reaction ~) ^factanswer("I am not allergic to anything.")
