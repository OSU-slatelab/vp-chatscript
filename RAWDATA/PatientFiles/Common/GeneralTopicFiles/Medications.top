# CURRENT MEDICATIONS This is only for medications taken for the chief complaint

topic: ~currentmedications keep repeat (!opioid ~medicines "how much" drug dose strength dosage milligrams ~drugs_legal)

u: ($pasthistory) respond(~pastmedications) ^fail(TOPIC)
u: ($family) respond(~familymedicalproblems) ^fail(TOPIC) # don't have a familymedicaitons topic so will put the odd family medication question in familymedicalproblems

#!!P Are you taking any medication for the 1 $chiefcomplaint=1
u: TAKING_MEDICATION_FOR_THE_1
	(![how long other addition* beside* when ~tobacco ~OTCmedicines] # tobacco and OTCmedicines  added by Camille
	[
		([use take on try] * [alleviate get_rid_of reduce diminish "help with" for] *~2 [~itwords $chiefcomplaint? symptom pain])
		([use take on try "no"] * ~medicines * [~itwords $chiefcomplaint? symptom])
		([use take on try "no"] * [~itwords $chiefcomplaint? symptom] * ~medicines)
		([had use take try] * ~medicines)
		([take on about any know list taken] * ~medicines)
	])
	^factanswer("I don't take any medications.")
		a: DOSE_1 ([dosage dose "how much" milligram]) ^reuse(HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN) # added by Lexi
		a: WHEN_1 (when) ^reuse(WHEN_DID_YOU_TAKE_MEDICINE)
		a: WHY_DO_YOU_TAKE_THE_MEDICATION (why) ^reuse (WHY_DO_YOU_TAKE_THE_MEDICATION)
		a: HAVE_THE_MEDICATION_BOTTLE ([have bring] * "medication bottle") ^factanswer("No.") # added by Camille

#!!P Are you taking anything stronger for the 1 $chiefcomplaint=1
u: TAKING_STRONGER_MEDICATION_FOR_THE_1
	(![how long other addition* beside* when]
	[
		([use take on try] * [stronger "more potent"] * [alleviate get_rid_of reduce diminish "help with" for] *~2 [~itwords $chiefcomplaint? symptom])
		([use take on try "no"] * [stronger "more potent"] * ~medicines * [~itwords $chiefcomplaint? symptom])
		([use take on try "no"] * [stronger "more potent"] * [~itwords $chiefcomplaint? symptom] * ~medicines)
	])
	^factanswer("I don't take any medications.")

#!!P Are you taking any medication for the 2 $complaint2=2
u: TAKING_MEDICATION_FOR_THE_2
	(![how long other addition* beside* when ~tobacco ~OTCmedicines] # tobacco and OTCmedicines added by Camille
	[
		([use take on try] * [alleviate get_rid_of reduce diminish "help with" for] *~2 [~itwords $chiefcomplaint? symptom])
		([use take on try "no"] * ~medicines * [~itwords $chiefcomplaint? symptom])
		([use take on try "no"] * [~itwords $chiefcomplaint? symptom] * ~medicines)
	])
^factanswer("I don't take any medications.")
	a: DOSE_2 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#!!P Are you taking any medication for the 3 $complaint3=3
u: TAKING_MEDICATION_FOR_THE_3
	(![how long other addition* beside* when ~tobacco ~OTCmedicines] # tobacco and OTCmedicines added by Camille
	[
		([use take on try] * [alleviate get_rid_of reduce diminish "help with" for] *~2 [~itwords $chiefcomplaint? symptom])
		([use take on try "no"] * ~medicines * [~itwords $chiefcomplaint? symptom])
		([use take on try "no"] * [~itwords $chiefcomplaint? symptom] * ~medicines)
	])
^factanswer("I don't take any medications.")
	a: DOSE_3 ([dosage dose "how much"]) ^factanswer("I am not sure.")

#! Do you have medical problems you take medication before # added by Camille
u: MEDICATION_FOR_OTHER_MEDICAL_PROBLEMS ([have current prior] * ["medical problems" illness] * ~medicines) ^factanswer("This back pain is my first real issue.")

# duplicated in past medications due to "ago"
#! When did you start taking the medicine $currentmedication1=medicine
u: WHEN_DID_YOU_START_TAKING_MEDICINE ([when "how long ago"] * [start begin] * [~medicines $currentmedication1?])  ^factanswer("I haven't taken any medicine.")

#! When did you take the medicine $currentmedication1=medicine
u: WHEN_DID_YOU_TAKE_MEDICINE (when * [~medicines $currentmedication1?])   ^factanswer("I haven't taken any medicine.")

#! Any reactions to medications
u: ANY_REACTIONS_TO_MEDICATIONS (reaction to ~medicines) ^factanswer("Not that I am aware of.")

#! Do you need to take the medicine that often  $currentmedication1=medicine
u: NEED_TO_TAKE_THE_MEDICINE_THAT_OFTEN ([need "have to"] * [take use] * [$currentmedication1? ~itwords] * [often hours])   ^factanswer("I haven't taken any medicine.")

#! How often did you take the medicine $currentmedication1=medicine
u: HOW_OFTEN_DID_YOU_TAKE_THE_MEDICINE (<< [often frequently "many days"] [~medicines ~itwords $currentmedication1?]>> )   ^factanswer("I haven't taken any medicine.")
	a: DOSE_1 ([dosage dose "how much"]) ^reuse(HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)

#! How long have you been taking the medicine $currentmedication1=medicine
u: HOW_LONG_TAKING_MEDICINE
	([
		(!ago how long * [take on] * [~medicines $currentmedication1? ~itwords])
		(how many * [doses days])
	])
	^factanswer("I haven't taken any medicine.")
		a: DOSE_1 ([dosage dose "how much"]) ^reuse(HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)

#! How much medicine have you taken $currentmedication1=medicine
u: HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN
	([
		(how [much many] *~3 [~medicines $currentmedication1?])
		([dose dosage amount number milligram] * [~medicines $currentmedication1?]) # added by Lexi
	])
	^factanswer("I haven't taken any medicine.")
	a: DOSE_1 ([dosage dose "how much"]) ^reuse(WHAT_IS_THE_STRENGTH_OF_THE_MEDICINE)

#! Would you like a stronger prescription
u: WOULD_YOU_LIKE_A_STRONGER_PRESCRIPTION ([do would] * [stronger powerful] * [medication prescription])   ^factanswer("I don't know.")

#! What is the strength of the medicine $currentmedication1=medicine
u: WHAT_IS_THE_STRENGTH_OF_THE_MEDICINE
	([
		([strength strong] *~3 [~medicines $currentmedication1?])
		([what know] *~2 [strength strong])
	])
	^factanswer("I haven't taken any medicine.")

#! Did the medicine help $currentmedication1=medicine
# want to avoid "what makes it better"
u: DID_THE_MEDICINE_HELP
	([
		(![position try anything what] [~medicines $currentmedication1? ~itwords] * [help away work better])
		([how much did] * [help away work better relief] * [~medicines $currentmedication1? ~itwords])
		([how much did] * [~medicines $currentmedication1? ~itwords] * [help away work better relief anything])
	])
	^factanswer("I haven't taken any medicine.")
		a: DOSE_1 ([dosage dose "how much"]) ^reuse(HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)

#! Does anything besides the medicine help
#! Does anything help besides the medicine
u: DOES_ANYTHING_BESIDES_MEDICINE_HELP
	([
		([beside besides aside ] *~3 [~medicines $currentmedication1?] * [help better])
		([help better] *~3 [beside besides aside ] *~3 [~medicines $currentmedication1?])
		(~ [beside besides aside ] * [~itwords] * [help better])
	])
^factanswer("It is difficult to say.")

#! Did you take the medicine today $currentmedication1=medicine
u: DID_YOU_TAKE_MEDICINE_TODAY
	([
		(take *~3 ~medicines *~3 [today morning afternoon now])
		([last recent most] *~3 [~medicines $currentmedication1? anything])
	])
	^factanswer("I haven't taken any medicine.")
		a: DOSE_1 ([dosage dose "how much"]) ^reuse(HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)

#!!P Are you still taking medicine $currentmedication1=medicine
u: ARE_YOU_STILL_TAKING_MEDICINE ([still continue] *~3 [take use] *~3 $currentmedication1?)   ^factanswer("I haven't taken any medicine.")
	a: DOSE_1 ([dosage dose "how much"]) ^reuse(HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)

#! What pills did you take
u: WHAT_PILLS_DID_YOU_TAKE
	([
		(!dose !strength [what about] *~3 ~medicines did *~3 [use take])
		(!dose !strength [what about] *~3 [sort kind type] *~3 ~medicines did *~3 [use take])
	])
	^factanswer("I haven't taken any medicine.")
		a: DOSE_1 ([dosage dose "how much"]) ^reuse(HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)

#! What kind of medicine is that
u: WHAT_KIND_OF_MEDICINE_IS_THAT
	([
		([kind type] * ~medicines)
		(what * ~medicines * that)
	])
	^factanswer("I haven't taken any medicine.")
		a: DOSE_1 ([dosage dose "how much"]) ^reuse(HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)

#! Would you like some pain medication
u: WOULD_YOU_LIKE_SOME_PAIN_MEDICATION (<< [like want need] [~medicines drug] >>)   ^factanswer("I am not sure.")

#! Did your doctor prescribe the medicine
u: DOCTOR_PRESCRIBE_THE_MEDICINE (<< [Doctor physician] prescribe ~medicines >>) ^factanswer("No.")

#! Who prescribed the medicine
u: WHO_PRESCRIBED_THE_MEDICINE (<< [who anyone] prescribe ~medicines >>) ^factanswer("I am not sure.")

#!!P What is medicine $currentmedication1=medicine
u: WHAT_IS_MEDICINE (what is $currentmedication1?)    ^factanswer("I haven't taken any medicine.")
	a: DOSE_1 ([dosage dose "how much"]) ^reuse(HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)

#! Any prescription medication
#! What prescription medications are you on at the moment
u: ANY_PRESCRIPTION_MEDICATION
	(![how long other addition* beside* herbal ~vitamin ~minerals allergies allergic ~OTCmedicines not non non-prescription "over the counter" over-the-counter opioid]
    [
		([take on about any no] *~3 ~prescribedmedicines)
		([take on] *~3 ~prescribedmedicines *~3 [regular regularly daily])
		(~prescribedmedicines *~3 [take on] *~3 [regular regularly daily])
		([what any currently] * ~prescribedmedicines * [prescribed on taking "at the moment"]) # added by Camille
		])
	^factanswer("I don't take any medications.")
		a: DOSE_1 ([dosage dose "how much"]) ^reuse(HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)
		a: IS_MEDICATION_NEW (<< [~itwords ~prescribedmedicines] [new old first] >>) ^factanswer ("I don't take any medications.")

#! Why do you take the medication
	u: WHY_DO_YOU_TAKE_THE_MEDICATION (why *~3 [take use] *~3 ~medicines) ^factanswer("I don't take any medications.")

#! Are you taking any medication
# excluding not and non avoids are you taking any medicine not prescribed
# excluding besides excludes are you taking any medication besides the xxxx - same with excluding aside
u: ARE_YOU_TAKING_ANY_MEDICATION
	(![how long other addition* beside* aside herbal ~vitamin ~minerals allergies allergic reaction not non non-prescription when recreational illegal injectable opioid ~tobacco] # tobacco added by Camille
	[
		( [take taken on about any know list] * ~medicines)
		([do are] * [take prescribed know] ~medicines)
		(what * ~medicines * [take prescribed on])
		([take on know list] * ~medicines * [regular regularly daily])
		(~medicines * [take on] * [regular regularly daily])
		([any what current "tell me"] * ~medicines * [currently regularly daily take]) # added by Camille
	])
	^factanswer("I don't take any medications.")
		a: DOSE_1 ([dosage dose "how much"]) ^reuse(HOW_MUCH_MEDICINE_HAVE_YOU_TAKEN)
		a: IS_MEDICATION_NEW (<< [~itwords ~prescribedmedicines] [new old first] >>) ^factanswer ("I don't take any medications.")
		a: ANY_OTHER_MEDICATION (else other addition additional) ^reuse (~othermedications.TAKE_ANY_OTHER_MEDICATION)
		a: WHY_DO_YOU_TAKE_THE_MEDICATION (why) ^reuse (WHY_DO_YOU_TAKE_THE_MEDICATION)
		a: WHEN_1 (when) ^reuse(WHEN_DID_YOU_TAKE_MEDICINE)
		a: DO_YOU_USE_ILLEGAL_DRUGS (~drugs_illegal recreational illicit illegal street) ^reuse (~druguse.DO_YOU_USE_ILLEGAL_DRUGS)

#! Any medication not prescribed
u: ANY_MEDICATION_NOT_PRESCRIBED # repeated below in pastmedications
		([
			(![how long] [any anything ~medicines] * [not non] prescribed)
			(![how long] [any anything ~medicines] * non-prescribed)
		])
		^factanswer("Never.")

#! What were the pills called
u: WHAT_WERE_THE_PILLS_CALLED
	([
		(![~tobacco] what *~3 ~medicines *~3 [called named]) # tobacco added by Camille
		(![~tobacco] name * ~medicines) # tobacco added by Camille
		(![~tobacco] what * ~medicines >) # tobacco added by Camille
	])
	^factanswer("I am not sure.")

# This is the vitamin and mineral supplement section

topic: ~supplements keep repeat (~vitamin minerals supplement herbal OTC "over the counter" ~alternative_medicine)

#! Are you taking supplements
u: ARE_YOU_TAKING_SUPPLEMENTS (![vaccine vaccinated allergy] [mineral supplement herbal ~minerals]) ^factanswer("I don't take any medications, supplements, or vitamins.")
	a: HOW_MUCH_SUPPLEMENTS (how [many much dose]) ^factanswer("I don't know.")
	a: WHY_SUPPLEMENTS ([what why] * take) ^factanswer("I don't know.")

#! Any OTC medication
u: ANY_OTC_MEDICATION (![often long other herbal ~vitamin ~minerals allergies allergic ~prescribedmedicines] [take on about any] * ~OTCmedicines) ^factanswer("I don't take any over the counter medications.")

#! Are you taking vitamins
u: ARE_YOU_TAKING_VITAMINS (~vitamin) ^factanswer("I don't take any vitamins.")
	a: WHICH_VITAMIN (which) ^factanswer("I am not sure.")
	a: HOW_MUCH_VITAMIN (how [many much dose]) ^factanswer("I don't know.")
	a: WHY_VITAMIN ([what why] * take) ^factanswer("It just seems like a good idea.")

#! Are you taking any alternative medications
#! Are you taking any homeopathic medications
u: ARE_YOU_USING_ALTERNATIVE_MEDICINE ([take use] * ~alternative_medicine) ^factanswer("I don't use any alternative or homeopathic medications.")

# OTHER MEDICATIONS

topic: ~othermedications (~medicines)

#!!P Are you taking any other medication for 1 $chiefcomplaint=1
u: TAKE_OTHER_MEDICATION_FOR_1
	(!ever
    [
		<< [other addition* beside* aside ] ~medicines $chiefcomplaint? >>
	])
	^factanswer("I haven't taken anything.")
		a: HOW_MUCH_OTHER_MEDICATION_1 (how [many much dose]) ^factanswer("I haven't taken anything.")
		a: HOW_OFTEN_OTHER_MEDICATION_1 (how [often frequently]) ^factanswer("I haven't taken anything.")
		a: WHY_TAKE_OTHER_MEDICATION_1 (why) ^factanswer("I haven't taken anything.")
		a: WHEN_DID_YOU_START_MEDICATION_1 ([start begin])   ^factanswer("I haven't taken anything.")
		a: WHAT_DOES_MEDICATION_1_DO (!reason [do purpose])   ^factanswer("I haven't taken anything.")

#!!P Are you taking any other medication for 2 $complaint2=2
u: TAKE_OTHER_MEDICATION_FOR_2
	(!ever
    [
		<< [other addition* beside* aside ] ~medicines $complaint2? >>
	])
^factanswer("I haven't taken anything.")
	a: HOW_MUCH_OTHER_MEDICATION_2 (how [many much dose]) ^factanswer("I haven't taken anything.")
	a: HOW_OFTEN_OTHER_MEDICATION_2 (how [often frequently]) ^factanswer("I haven't taken anything.")
	a: WHY_TAKE_OTHER_MEDICATION_2 (why) ^factanswer("I haven't taken anything.")
	a: WHEN_DID_YOU_START_MEDICATION_2 ([start begin])   ^factanswer("I haven't taken anything.")
	a: WHAT_DOES_MEDICATION_2_DO (!reason [what purpose])   ^factanswer("I haven't taken anything.")

#!!P Are you taking any other medication for 3 $complaint3=3
u: TAKE_OTHER_MEDICATION_FOR_3
	(!ever
    [
		<< [other addition* beside*] ~medicines $complaint3? >>
	])
^factanswer("I haven't taken anything.")
	a: HOW_MUCH_OTHER_MEDICATION_3 (how [many much dose]) ^factanswer("I haven't taken anything.")
	a: HOW_OFTEN_OTHER_MEDICATION_3 (how [often frequently]) ^factanswer("I haven't taken anything.")
	a: WHY_TAKE_OTHER_MEDICATION_3 (why) ^factanswer("I haven't taken anything.")
	a: WHEN_DID_YOU_START_MEDICATION_3 ([start begin first])   ^factanswer("I haven't taken anything.")
	a: WHAT_DOES_MEDICATION_3_DO (!reason [what purpose function])   ^factanswer("I haven't taken anything.")

#! Are you taking any other medication
#!!P Are you taking any medication besides the ibuprofen
u: TAKE_ANY_OTHER_MEDICATION
	([
		(![pain ever try $currentmedication1? $currentmedication3? past] [other addition* beside* aside] ~medicines)
		(![pain ever does "help you with" bother bothering try $currentmedication1? $currentmedication3? know me] anything else >)
		([other addition* beside* aside] *~3 $currentmedication1?)
	])
	^factanswer("Nothing.")
		a: HOW_MUCH_OTHER_MEDICATION (![pain ever $currentmedication1? $currentmedication3?] how [many much dose dosage]) ^factanswer("I don't take anything else.")
		a: HOW_OFTEN_OTHER_MEDICATION ( ![pain ever $currentmedication1? $currentmedication3?] how [often frequently]) ^factanswer("I don't take anything else.")
		a: HOW_LONG_OTHER_MEDICATION (![pain $currentmedication1? $currentmedication3?] how [long days]) ^factanswer("I haven't taken anything.")
		a: WHY_TAKE_OTHER_MEDICATION (why) ^factanswer("I haven't taken anything.")
		a: WHEN_DID_YOU_START_MEDICATION ([start begin])   ^factanswer("I haven't taken anything.")
		a: WHAT_DOES_MEDICATION_DO (!reason [what purpose])   ^factanswer("I haven't taken anything.")

#! Are you taking any other medication
u: TAKE_ANY_OTHER_MEDICATION1
	(![pain ever ] [
		(![try why $currentmedication1? $currentmedication2?] [other addition* beside* aside] * ~medicines)
		(![try why does "help you with" bother bothering $currentmedication1? $currentmedication2?] anything else >)
	])
	^factanswer("Nothing.")
		a: HOW_MUCH_OTHER_MEDICATION_1 (![pain why $currentmedication1? $currentmedication2?] how [many much dose dosage]) ^factanswer("I don't take anything else.")
		a: HOW_OFTEN_OTHER_MEDICATION_1 (![pain why $currentmedication1? $currentmedication2?] how [often frequently]) ^factanswer("I don't take anything else.")
		a: HOW_LONG_OTHER_MEDICATION_1 (![pain why $currentmedication1? $currentmedication2?] how [long days]) ^factanswer("I haven't taken anything.")
		a: WHY_TAKE_OTHER_MEDICATION_1 (why) ^factanswer("I haven't taken anything.")
		a: WHEN_DID_YOU_START_MEDICATION_1 ([start begin])   ^factanswer("I haven't taken anything.")
		a: WHAT_DOES_MEDICATION_1_DO (!reason [do purpose])   ^factanswer("I haven't taken anything.")

#! Are you taking any other medication
u: TAKE_ANY_OTHER_MEDICATION2
	(![pain ever]  [
		([other addition* beside* aside] * ~medicines)
		(![does "help you with" bother bothering] anything else)
	])
	^factanswer("Nothing.")
		a: HOW_MUCH_OTHER_MEDICATION_2 (how [many much dose]) ^factanswer("I don't take anything else.")
		a: HOW_OFTEN_OTHER_MEDICATION_2 (how [often frequently]) ^factanswer("I don't take anything else.")
		a: HOW_LONG_OTHER_MEDICATION_2 (how [long days]) ^factanswer("I haven't taken anything.")
		a: WHY_TAKE_OTHER_MEDICATION_2 (why) ^factanswer("I haven't taken anything.")
		a: WHEN_DID_YOU_START_MEDICATION_2 ([start begin])   ^factanswer("I haven't taken anything.")
		a: WHAT_DOES_MEDICATION_2_DO (!reason [what purpose])   ^factanswer("I haven't taken anything.")

# PAST MEDICATIONS

topic: ~pastmedications keep repeat ("anything else" ibuprofen ~medicines ~prescribedmedicines antibiotics)

# u: ($present) respond (~currentmedications) ^fail(TOPIC) turned off to avoid "have you ...." because have triggers $present
#	u: ($family) respond(~familymedications) ^fail(TOPIC)

#!!P Have you ever taken any medication for 1  $chiefcomplaint=1
u: EVER_TAKEN_MEDICATION_FOR_1  (![how long] ever [use take try on] * ~medicines * $chiefcomplaint?)    ^factanswer("Never.")

#!!P Have you ever taken any medication for 2  $complaint2=2
u: EVER_TAKEN_MEDICATION_FOR_2 (![how long] ever [use take try on] * ~medicines * $complaint2?)    ^factanswer("Never.")

#!!P Have you ever taken any medication for 3  $complaint3=3
u: EVER_TAKEN_MEDICATION_FOR_3 (![how long] ever [use take try on] * ~medicines * $complaint3?)    ^factanswer("Never.")

#! Have you had any reactions to medications
u: ANY_REACTIONS_TO_MEDICATIONS (reaction to ~medicines) ^factanswer("Not that I am aware of.") # this is repeated here because of "have you had" which triggers past

#! Have you ever taken any other medication
u: EVER_TAKEN_OTHER_MEDICATION
	([
		(![how long not non non-prescription] [recently ever] * [take try on treated] * ~medicines)
		(![how long not non non-prescription] [use take try on treated] * ~medicines * [recently ever])
		(![how long not non non-prescription] [use take try on treated] * "anything else" >)
		(![how long not non non-prescription] [take try on treated prescribe] * [past before]) # added by Camille
		([past prior previous] * ~medicines) # added by Camille
	])
	^factanswer("Nothing else.")

#! Have you ever been prescribed medication before
u: EVER_PRESCRIBED_MEDICATION
	([
		(![how long not non non-prescription] [recently ever] * [take try on treated prescribe] * ~medicines)
		(![how long not non non-prescription] [use take try on treated prescribe] * ~medicines * [recently ever])
 		(![how long not non non-prescription] [use take try on treated prescribe] * "anything else" >)
	])
	^factanswer("Not that I recall.")

#! Have you ever taken any medication not prescribed
#! Ever take prescriptions that are not yours
u: EVER_TAKEN_MEDICATION_NOT_PRESCRIBED # same as any medicaiton not prescribed but repeated here due to "ever" triggering past
	([
		(![how long] [recently ever] * [take try on treated] * ~medicines * [not non] prescribed)
		(![how long] [use take try on treated] * ~medicines * [not non] prescribed * [recently ever])
		(![how long] [use take try on treated] * prescriptions * not * [you yours])
 		(![how long] [use take try on treated] * "anything else" >)
		(![how long] [use take try on treated] * prescription * [not non] prescribed ) # added by Camille
		(![how long] [use taken take treated] * [not non] * ~prescribedmedicines * ~medicines) # added by Camille
	])
		^factanswer("Never.")

	# must repeat here because of "ago"
	#! When did you start taking the medicine $currentmedication1=medicine
	u: WHEN_DID_YOU_START_TAKING_MEDICINE ([when "how long ago"] * [start begin] * [~medicines $currentmedication1?])  ^factanswer("I am not sure.")

#! Are there any medications that you cannot take
u: MEDICATIONS_YOU_CANNOT_TAKE (~medicines * [cannot cant "arent able" "should not"] * [take use try]) ^factanswer("No.")
