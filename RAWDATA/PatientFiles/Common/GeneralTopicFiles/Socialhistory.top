
concept: ~activities [hobby fun entertainment outside enjoy activities enjoyment relax "free time"  excitement "extracurricular activity" "spare time" "free time" "daily basis" "like to do" interests s weekend ]
# Activity

topic: ~normal_activity keep repeat (exercise ~exerciselist activity rest drive seatbelt seat_belt "work out" active energy bedridden shape tired walk sedentary)

u: ( $$family) ^fail(TOPIC)

#! How often do you exercise
u: HOW_OFTEN_DO_YOU_EXERCISE (<< [often lot] [exercise "work out"] >>)  ^factanswer("Not very often.")

#! Do you have any trouble exercising
u: DO_YOU_HAVE_ANY_TROUBLE_EXERCISING ([trouble problems ~breathing_problems] *~3 [exercise "workout"]) ^factanswer("I have no problems exercising.")

#! Do you exercise
u: DO_YOU_EXERCISE (["work out" exercise ~exerciselist "physical activity" shape ]) ^factanswer("I don't exercise that much.")
	a: WHEN_DID_YOU_EXERCISE (when * [last most recent]) ^factanswer("I don't remember.")

#! Are you active
u: ARE_YOU_ACTIVE
	([
		(![sexually sexual $chiefcomplaint?] are you *~3 [active sedentary])
		(![sexually sexual $chiefcomplaint?] [activity active] *~3 [normally usually level ~timename])
	])
	^factanswer("I get plenty of exercise.")

#! How is your energy
u: HOW_IS_YOUR_ENERGY ([energy tired])  ^factanswer("I have enough energy.")

#! Have you been resting
u: HAVE_YOU_BEEN_RESTING (![pain does help] rest) 	 ^factanswer("I rest enough.")
	a: HOW_MUCH_RESTING (how much) Since I can't work, about all I can do is rest.

#! Have you been bedridden
u: HAVE_YOU_BEEN_BEDRIDDEN (![pain does help] bedridden) 	^factanswer("I have not been bedridden.")

#! Are you able to drive
u: ARE_YOU_ABLE_TO_DRIVE (!home [able can do] *~3 drive) 	 ^factanswer("I am able to drive.")

#! Are you able to drive home
u: ARE_YOU_ABLE_TO_DRIVE_HOME ([able can] *~2 drive *~3 home)  ^factanswer("I can drive.")

#! Do you wear a seatbelt
u: WEAR_A_SEATBELT ([seatbelt seat_belt]) ^factanswer("I always wear a seatbelt.")

# Alcohol use

topic: ~alcoholuse keep repeat (!caffeinated_drinks ~alcoholic_beverage "eye opener" "eye-opener" drink guilty intake social socially)

u: ( $$family) ^fail(TOPIC)

#! How much do you drink
u: HOW_MUCH_DO_YOU_DRINK
	([
		(!water how *~2 [drink ~alcoholic_beverage much often many])
		(!water [drink ~alcoholic_beverage] * [day week month])
		(!water [beer wine] * [day week month]) # don't know why I need this to catch beers and wine
	])
	^factanswer("I don't drink.")

#! Has anyone thought you have an alcohol problem
u: ANYONE_THOUGHT_ALCOHOL_PROBLEM ([anyone anybody] *~3 have *~3 [drink ~alcoholic_beverage])  ^factanswer("Never.")

#! Do you feel guilty about your drinking
#! Have you ever felt the need to change your drinking habits?
u: DO_YOU_FEEL_GUILTY_ABOUT_YOUR_DRINKING
	([
		([guilt guilty change decrease reduce] *~3 [drink ~alcoholic_beverage])
		([drink ~alcoholic_beverage] * ["too much" "too frequently"])
	])
	^factanswer("I don't drink that much.")

#! Do you ever need an eye opener
u: DO_YOU_EVER_NEED_AN_EYE_OPENER
	([
		(drink * morning)
		([eye-opener "eye opener"])
	])
	^factanswer("I don't think so.")

#! Have you considered reducing your alcohol use
u: HAVE_YOU_CONSIDERED_REDUCING_ALCOHOL ([think consider need felt] * [reduce cut decrease stop] * [drink ~alcoholic_beverage]) ^factanswer("I don't really drink that much.")

#! Ever felt annoyed by people confronting you about your alcohol use
u: EVER_FELT_ANNOYED_ABOUT_ALCOHOL ([annoy frustrate] * [drink ~alcoholic_beverage])  ^factanswer("Never.")

#! Ever had any problem with alcohol
u: EVER_HAD_PROBLEM_WITH_ALCOHOL (<< [alcohol drinking] [problem trouble] >>)  ^factanswer("Never.")

#! What kind of drinks
u: WHAT_KIND_OF_DRINKS ([kind type brand] *~3 [alcohol drinks]) ^factanswer("I don't drink.")

#! How many drinks
u: HOW_MANY_DRINKS (["how many" number] * drinks) ^factanswer("I don't drink.") # I think this gets blocked by How much alcohol

#! Do you drink
u: DO_YOU_DRINK
	([
		(!~caffeinated_drinks [drink use consume any] * [social socially ~alcoholic_beverage])
		(!~caffeinated_drinks [social ~alcoholic_beverage] * [drink use consume consumption intake])
		(![~caffeinated_drinks what where when] [drink ~alcoholic_beverage])
	])
	^factanswer("I don't drink.")
	a: WHAT_ABOUT_ALCOHOL (alcohol) ^factanswer("I don't drink.")

#! When did you stop drinking
u: WHEN_DID_YOU_STOP_DRINKING
	([
		([when "how long ago"] * [stop quit])
	])
	^factanswer("I don't drink.")

#! Do you go to an alcohol support group?
u: ATTEND_AA
	([
		([attend "go to"] * [AA "alcoholics anonymous" "support group"])
	])
	^factanswer("I don't drink.")

# Caffeine use

topic: ~caffeineuse keep repeat (~caffeinated_drinks caffeinated pop soda coke caffeine drink)

u: ( $$family) ^fail(TOPIC)

#! Do you drink coffee
u: DO_YOU_DRINK_COFFEE ([~caffeinated_drinks caffeine caffeinated pop~n soda coke]) ^factanswer("I try to avoid caffeine.")

# Diet

topic: ~yourdiet keep repeat (~beverage diet drink consume eat fluid liquid hydrate fiber dairy "fast food" "junk food" food snack breakfast lunch dinner appetite fruit vegetable dietary full fill salt sodium salty)

u: ( $$family) ^fail(TOPIC)

#! Do you drink enough fluid
u: DO_YOU_DRINK_ENOUGH_FLUID
	([
		(![~caffeinated_drinks caffeinated ~alcoholic_beverage] [drink consume take] * [fluid water liquid beverage])
		(hydrate)
		(![~caffeinated_drinks caffeinated ~alcoholic_beverage] [fluid water liquid beverage] * [drink consume take])
	])
	^factanswer("I think I do.")
		a: HOW_MUCH_FLUID (how [many much dose]) ^factanswer("I drink the recommended amount of 8 glasses per day.")

#! Do you eat enough dairy
u: DO_YOU_EAT_DAIRY ([dairy milk "ice cream"]) ^factanswer("I get enough dairy.")

#! Do you get enough protein
u: DO_YOU_GET_ENOUGH_PROTEIN ([what do about] * [protein meat egg fish]) ^factanswer("I get enough protein.") # added by Mahsa

#! Do you eat enough fiber
u: DO_YOU_EAT_FIBER (fiber) ^factanswer("I get enough fiber.")

#! Do you eat fast food
u: DO_YOU_EAT_FAST_FOOD ([fast junk] food) ^factanswer("I don't go to fast food restaurants very often.")

# #! Do you eat healthy food
#	u: DO_YOU_EAT_HEALTHY_FOOD ([healthy good nutritious] *~3 [food meals]) ^factanswer("I eat pretty healthy.")

#! Do you eat healthy food
#! What healthy food do you eat
u: HEALTHY_FOOD ([healthy nutritious] food) refine ()
	a: WHAT_FOOD (what) ^factanswer("I like apples and bananas, but really I will eat almost any fruit or vegetable you put in front of me.")
	a: DO_FOOD (do) ^factanswer("I try to eat pretty healthy foods.")

#! Do you eat fruits and vegetables
u: DO_YOU_EAT_FRUITS_AND_VEGETABLES (!what [eat like] *~3 [fruit vegetable]) ^factanswer("I do eat them.") # updated by Mahsa

#! Do you eat fruits
#! What fruits and vegetables do you eat # updated section added by Mahsa
u: DO_YOU_EAT_FRUITS (eat * fruit ) refine()
	a: WHAT_FRUIT (fruit) ^factanswer("I love fruit, especially apples and bananas.")
	a: WHAT_ABOUT_VEGETABLES (vegetable) ^factanswer("I eat vegetables as well.")

#! Do you eat vegetables
u: DO_YOU_EAT_VEGETABLES (eat * vegetable) refine() # updated section added by Mahsa
	a: WHAT_VEGETABLE (vegetable) ^factanswer ("I like vegetables, especially broccoli and carrots.")
	a: WHAT_ABOUT_FRUIT (fruit) ^factanswer ("I like fruits, too.")

#! Any changes in your diet
u: ANY_CHANGES_IN_YOUR_DIET (change * [diet eating]) ^factanswer("My diet is the same.")

#! Tell me about your appetite
u: APPETITE (!weight appetite) ^factanswer("My appetite is fine.")

#! Do you have dietary restrictions
u: ANY_DIETARY_RESTRICTIONS (!how [diet dietary] *~3 [restrictions rules]) ^factanswer("I have no restrictions.")

#! Do you feel full quickly
u: DO_YOU_FEEL_FULL_QUICKLY ([full fill] * [fast quickly]) ^factanswer("I eat fine.")

#! Can you eat
u: CAN_YOU_EAT ([can able] *~3 eat) ^factanswer("Eating is not a problem.")

#! Have you been eating
u: HAVE_YOU_BEEN_EATING (![what how why do pain] eat)  ^factanswer("I have been eating.")

#! What snacks do you eat
u: WHAT_SNACKS (snack) ^factanswer("I don't snack.")

#! What were you eating before
u: WHAT_WERE_YOU_EATING_BEFORE (eat *~3 before)  ^factanswer("My diet hasn't changed.")

#! What do you eat for breakfast
u: WHAT_DO_YOU_EAT_FOR_BREAKFAST (what * eat * [breakfast morning]) ^factanswer ("I eat pretty normal breakfast foods.") # added by Mahsa

#! What do you eat for dinner # added by Mahsa
#! What do you eat for lunch
u: WHAT_DO_YOU_EAT_FOR_DINNER (what * eat * [lunch supper dinner]) ^factanswer("I try to eat a healthy diet.")

#! How much salt
u: HOW_MUCH_SALT ([how what 'do] * [salt sodium salty])  ^factanswer("Not too much.")

#! How is your diet
u: HOW_IS_YOUR_DIET
	([
		(!change !on diet)
		(!change !on eat * [healthy well])
	])
	^factanswer("My diet is fine.")

#! Describe a typical meal
u: DESCRIBE_TYPICAL_MEAL ([describe example] * [meal food eat])	^factanswer("I just eat normal stuff.")

	#! Do you like to eat Salads
	u: DO_YOU_LIKE_TO_EAT_SALADS (eat * salad) ^factanswer ("I love salads.")

	#! What is your favorite food
	u: WHAT_IS_YOUR_FAVORITE_FOOD (favorite food) ^factanswer ("I like normal stuff.")

# Tobacco Use

topic: ~tobaccouse keep repeat (!~drugs_illegal ~tobacco smoke cigarettes pipe chew smoker quit nicotine secondhand ~spouse ~family_members)

u: ( $$family) ^fail(TOPIC)

#! How much do you smoke
u: HOW_MUCH_DO_YOU_SMOKE (how [much many] *~3 [smoke tobacco cigarettes pipe]) ^factanswer("I don't smoke.")

#! How long have you smoked
u: HOW_LONG_HAVE_YOU_SMOKED (![much did] how [long many] * [smoke smoker]) ^factanswer("I don't smoke.")

#! Have you ever smoked
u: HAVE_YOU_EVER_SMOKED (<< [ever past previously history before] [smoke tobacco cigarettes pipe smoker] >>)  ^factanswer("I have never smoked.")

#! How long did you smoke
u: HOW_LONG_DID_YOU_SMOKE (!much how [long many] * [smoke smoker])  ^factanswer("I never smoked in my life.")

#! When did you start smoking
u: WHEN_DID_YOU_START_SMOKING
	([
		(when * [begin start first] * [smoke smoking cigarette])
		([what how] [old age] * [begin start first] * [smoke smoking cigarette])
	])
	^factanswer("I never started smoking.")

#! Have you quit smoking
u: HAVE_YOU_QUIT_SMOKING
	([
		([tried attempted] *~3 [quit stop stopped] *~3 [smoking smoke chew chewing])
		([tried attempted] *~2 [quit stop]>)
		(quit smoking)
	])
	^factanswer("I never smoked.")

#! Are you exposed to secondhand smoke
u: ARE_YOU_EXPOSED_TO_SECONDHAND_SMOKE
	([
		([exposed around near] * [other secondhand] * [smoker smoke])
		([~family_members ~spouse other people] * [smoker smoke])
	])
	^factanswer("I am not around smoke.")

#! Any other tobacco
u: ANY_OTHER_TOBACCO ([any other] *~3 tobacco)  ^factanswer("I haven't touched tobacco.")

#! Do you smoke
u: DO_YOU_SMOKE
	(!other
		[
		([smoke smoker tobacco] >)
		([smoke chew use try] * [tobacco cigarettes pipe nicotine])
		([tobacco cigarettes pipe nicotine] * [smoke chew use try])
		([what tried take] * [tobacco nicotine cigarette] * drugs) # added by Camille
	])
	^factanswer("I have never smoked.")

# Education

topic: ~education keep repeat (education school college study major~n minor)

u: ( $$family) ^fail(TOPIC)

#! How far did you get in school
u: HOW_FAR_IN_SCHOOL ([what how] * [education school college]) ^factanswer("I graduated from college")

#! What is your major
u: WHAT_IS_YOUR_MAJOR (what * [study studying major~n minor]) ^factanswer("I haven't decided yet.")

Table: ^equivalent() # these are all essential
HAS_THE_PAIN_AFFECTED_YOUR_ACTIVITY  HAS_THE_PAIN_AFFECTED_YOUR_ACTIVITY   # this is the base question - all similar questions map to this base
CAN_YOU_DO_NORMAL_ACTIVITIES HAS_THE_PAIN_AFFECTED_YOUR_ACTIVITY

# In relation to HPI

topic: ~inrelationtohpi keep repeat (~pain work activity afraid limit interfere concern care bother worry trouble life handle handling affect normal normally dress situation feel cook clean household particular laundry clothe housework chore challenge function)

#! How has this affected you
u: HOW_HAS_THIS_AFFECTED_YOU (!~medicines how * [~pain ~itwords symptoms injury] * [affect influence influenced handle handling interfere impact]) ^factanswer("It hasn't affected me that much.")

#! Can you do normal activities
u: CAN_YOU_DO_NORMAL_ACTIVITIES
	(!what
		[
		([day daily limit normal difficult] * [activity routine things])
		([activity routine things] * [day daily])
		([activity routine things activities routines chores] * [impacted affected 'do challenge])
		([impacted affected restrict difficult able can] * [activity routine things activities routines function])
		(daily [activity function])
		]
	)
	^factanswer("I can do normal activities.")

#! Has the pain affected your activity
#! Has this impacted your life outside of work?
#! Aside from work, has this impacted your life?
u: HAS_THE_PAIN_AFFECTED_YOUR_ACTIVITY
	([
		(![better worse ~bodyparts how work]  ~the_pain [affect inhibit impact influence alter interfere change hard] * [activity routine function functioning life])
		(![better worse ~bodyparts how work] [activity routine function functioning] * [~pain ~itwords injury])
		(![better worse ~bodyparts how work] [~the_pain injury] * [affect inhibit impact influence alter interfere change hard] * [activity routine function functioning life])
		(![better worse ~bodyparts how]  ~the_pain [affect inhibit impact influence alter interfere change hard] * [activity routine function functioning life] * [outside beside besides aside] * ~job)
		(![better worse ~bodyparts how] [outside beside besides aside] * ~job * ~the_pain [affect inhibit impact influence alter interfere change hard] * [activity routine function functioning life])
	])
	^factanswer("Not really.")

#! Has the pain affected you
u: HAS_THE_PAIN_AFFECTED_YOU
	([
		(![~bodyparts how ~job] [~pain ~itwords injury] * [affect inhibit impact influence alter interfere change hard] * you)
		(![better worse ~bodyparts how ~job] [affect inhibit impact influence alter interfere change] * [~pain ~itwords injury])
	])
	^factanswer("Not really.")

#! How have you been handling this
u: HOW_HAVE_YOU_BEEN_HANDLING_THIS
	([
		([handle handling] *~3 [~pain ~itwords situation])
		([~pain ~itwords situation] * [handle handling])
	])
	^factanswer("Just fine.")

#! What bothers you the most
u: WHAT_BOTHERS_YOU_THE_MOST
	([
		([bother worry trouble concern] * [particular most])
		([big large main most] * [afraid worry concern worried])
	])
	factanswer("Nothing bothers me.")

#! Can you care for yourself
u: CAN_YOU_CARE_FOR_YOURSELF
	([
		([care dress] * yourself)
		([problems issues trouble difficult] * [care dress])
		([can able] * [cook clean household laundry clothe housework])
	])
	^factanswer("I can care for myself.")

#! Does this bother you mentally
u: DOES_THIS_BOTHER_YOU_MENTALLY
	([
		([feel make cause] * [bother worry depress "mental health"])
		([bother affect impact] *~3 mentally)
	])
		^factanswer("I don't think so.")

# ***************************************************************************************************
# Recreational drug use

topic: ~druguse keep repeat (~drugs_illegal "bath salts" pills sure recreational drug illicit illegal street experiment use)

u: ( $$family) ^fail(TOPIC)

#! Do you use illegal drugs
u: DO_YOU_USE_ILLEGAL_DRUGS
	([
		(![other ever] [use take do indulge] * [drugs substances])
		(![taste reflux feel ever] [~drugs_illegal "pcp" MDMA Adam])
		(!ever ["bath salts" bathsalts])
		(!ever ["recreational drug" "street drug" "drug use" "IV drugs" "intravenous drugs"])
		(<< !ever drug [recreationally recreational illicit illegal] >>)
		(!ever [used experiment] * [~drugs_illegal drug])
		(!ever illegal)
		([drug ~drugs_illegal substance] *~3 use)
	])
	^factanswer("Never.")
		a: ARE_YOU_SURE (are you [sure certain])  ^factanswer("I am sure.")

#! Have you ever used illegal drugs
u: HAVE_YOU_EVER_USED_ILLEGAL_DRUGS
	([
		([use take do indulge experiment] [drugs substances] {ever} >)
		(![taste reflux feel] [~drugs_illegal "pcp" MDMA Adam])
		(["bath salts" bathsalts])
		(["recreational drug" "street drug" "drug use" "IV drugs" "intraveous drugs" "recreational substances"])
		(<< drug [recreationally recreational illicit illegal] >>)
	])
	^factanswer("I have never used any illegal drugs.")

# Tattoos

topic: ~tattoo keep repeat (tattoo "body art")

u: ( $$family) ^fail(TOPIC)

#! Do you have a tattoo
u: DO_YOU_HAVE_A_TATTOO (do you * [tattoo "body art"]) ^factanswer("I have no tattoos.")

#! Have you ever had a tattoo
u: EVER_HAD_A_TATTOO (ever * [tattoo "body art"]) ^factanswer("I have never had a tattoo.")

# Travel

topic: ~travel keep repeat (travel visit country plane airplane trip)

#! Have you traveled anywhere
u: HAVE_YOU_TRAVELED_ANYWHERE ([have did recent recently] * [travel travels country trip]) ^factanswer("I have not traveled.")

#! Have you been on a plane
u: HAVE_YOU_BEEN_ON_A_PLANE
	([
		([have did recent recently] * [plane airplane])
		([plane airplane] * [recently "not long ago" ~timename])
	])
	^factanswer("I haven't been on a plane.")

# Hobby

topic: ~hobbyquestions keep repeat (hobby fun entertainment outside besides work job occupation enjoy "free time" "extracurricular activity" relax outside activities "spare time" "daily basis" "like to do" interests "social life" "personal life")



u: ( $$family) ^fail(TOPIC)

#! Do you have any hobbies
u: DO_YOU_HAVE_ANY_HOBBIES
	([
		(hobby)
		(![daily normal] [activity hobby] * [enjoy like participate involve 'do])
		(![daily normal] [involve enjoy like participate 'do] * [activity hobby])
	])
	 ^factanswer("I don't have any specific hobbies.")

#! What do you do for fun
#! Are you involved with groups outside of work
u: WHAT_DO_YOU_DO_FOR_FUN
	([
		([what how anything] * ~activities)
		([what anything group] * [outside when after besides] * [work job occupation])
		([outside when after besides] * [work job] * [what anything group])
	])
	^factanswer("I do normal stuff like reading and going to a movie.")

	#! How is your social Life
	u: HOW_IS_YOUR_SOCIAL_LIFE ("social life") ^factanswer("My social life is fine.")

	#! How is your personal Life
	u: HOW_IS_YOUR_PERSONAL_LIFE ("personal life") ^factanswer("My personal life is fine.")

# Religion

topic: ~religion keep repeat (religion denomination church spiritual religious)

u: ( $$family) ^fail(TOPIC)

#! What is your religious preference
u: WHAT_IS_YOUR_RELIGIOUS_PREFERENCE  ([religion religious spiritual denomination church])  ^factanswer("I don't have a preference.")

# Advanced Directive

topic: ~advanced_directive keep repeat ("living will" "end of life" "advanced directive" "power of attorney" DNR CPR "do not resuscitate" ventilator)

u: ( $$family) ^fail(TOPIC)

#! What is in your living will
u: WHAT_IS_IN_YOUR_LIVING_WILL
	([
		([how what] * [contain cover] * ["living will"])
		([consider want thought] * [DNR "do not resuscitate" CPR ventilator die dying])
		(what * "living will")
	])
	^factanswer("I don't have one.")

#! Do you have a living will
u: DO_YOU_HAVE_A_LIVING_WILL ("living will") ^factanswer("I do not have one of those.")

#! Do you have an advanced directive
u: DO_YOU_HAVE_AN_ADVANCED_DIRECTIVE  (["advanced directive" "end of life"]) ^factanswer("I do not.")

#! Do you have a medical power of attorney
u: DO_YOU_HAVE_A_MEDICAL_POWER_OF_ATTORNEY  (<< [proxy agent "power of attorney"] [medical health care] >>)  ^factanswer("I have none.")

# General Health

topic: ~generalhealth keep repeat (health healthy quality everyday "social history" sick ill "feeling poorly" "under the weather" feel)

u: ( $$family) ^fail(TOPIC)

#! Were you a healthy child
u: WERE_YOU_HEALTHY_CHILD (!are !diet [healthy sick ill health] * [past childhood child])   ^factanswer("I have always been healthy.")

#! Are you healthy
#! How would you say your health is
#! Would you describe yourself as a healthy person
u: ARE_YOU_HEALTHY
	([
	(![diet food eat mental ~parents sex sexual] << [do be] you  [health healthy] >>)
	(healthy person)
	])
	^factanswer("I am pretty healthy.")

#! Do you feel well
u: DO_YOU_FEEL_WELL
	([
		(feel * [well OK allright])
		(["are you" "you are"] * [well OK allright okay ok alright])
	])
	^factanswer("I feel fine.")

#! What is your quality of life
u: WHAT_IS_QUALITY_OF_LIFE (what * [current everyday daily quality] * life) ^factanswer("It is good.")

#! May I take your social history
u: SOCIAL_HISTORY (social history) ^factanswer("OK")

topic: ~feelingquestions keep repeat (frustrate nervous )

	#! Are you frustrated
u: ARE_YOU_FRUSTRATED ('frustrated)  ^factanswer("Not really.")

	#! Are you nervous
u: ARE_YOU_NERVOUS ([nervous worried scared worries]) ^factanswer("Not really.")
