
# This is the Vaccine Section

topic: ~vaccines keep repeat (vaccine vaccinate vaccinated vaccination vaccinations shot booster up-to-date "up to date" immunized immunization check)

u: ($family) ^fail(TOPIC)

#! Have you been vaccinated for MMR
u: HAVE_YOU_BEEN_VACCINATED_FOR_MMR ([MMR mumps measles rubella])	^factanswer("I am not sure.")

#! Have you been vaccinated for hib
u: HAVE_YOU_BEEN_VACCINATED_FOR_HIB  (["Haemophilus Influenza b" hib])  ^factanswer("I am not sure.")

#! Have you been vaccinated for Hepatitis A
u: HAVE_YOU_BEEN_VACCINATED_FOR_HEPATITIS_A (["Hepatitis A" "hep A"])  ^factanswer("I am not sure.")

#! Have you been vaccinated for Hepatitis B
u: HAVE_YOU_BEEN_VACCINATED_FOR_HEPATITIS_B (["Hepatitis B" "hep B"])  ^factanswer("I am not sure.")

#! Have you been vaccinated for Hepatitis
u: HAVE_YOU_BEEN_VACCINATED_FOR_HEPATITIS (hepatitis) ^factanswer("I am not sure.")

#! Have you had the hpv vaccine
u: HAVE_YOU_HAD_THE_HPV_VACCINE ([hpv "human papilloma virus"]) ^factanswer("I am not sure.")

#! Have you had your flu shot
u: HAVE_YOU_HAD_FLU_SHOT (<<[flu influenza][vaccine shot vaccines vaccination vaccinated]>>)  ^factanswer("I get one every year.")

#! Have you had the pneumonia vaccine
u: HAVE_YOU_HAD_THE_PNEUMONIA_VACCINE (<<[pneumonia pneumococcal][vaccine shot vaccines vaccination vaccinated]>>)  ^factanswer("I am not sure.")

#! Have you received your shingles vaccine
u: HAVE_YOU_RECEIVED_YOUR_SHINGLES_VACCINE (<< shingles [vaccine shot vaccines vaccination vaccinated]>>)   ^factanswer("I am not sure.")

#! When was your last tetanus shot
u: WHEN_WAS_YOUR_LAST_TETANUS_SHOT (<<tetanus [vaccine shot vaccines vaccination vaccinated booster]>>)    ^factanswer("I am not sure.")

#! Are you current on vaccinations
u: ARE_YOU_CURRENT_ON_VACCINATIONS
	([
 		([current recent recently had up-to-date "up to date" all been] * [vaccine vaccination immunized immunization immunizations shots])
		([vaccine vaccination immunization immunizations shots] * [current recent recently had up-to-date "up to date" all])
		([vaccine vaccination immunization immunizations shots]>)
	])
	^factanswer("I am not sure.")

# This is the physician/Doctor's office section

topic: ~seeingadoctor keep repeat

	(doctor physician find locate practice last hard recent location office
	us work park patient parking here wait ~medical_professionals physical exam
	gynecologist counseling sought help seen discuss)

#! When was the last time you saw a doctor
u: WHEN_DID_YOU_SEE_A_DOCTOR_LAST (!pap [when do] *~3 [recent most last regular] *~4 [doctor physician physical exam checkup]) ^factanswer("I am not sure.")

#! When was the last time you saw a gynecologist
u: WHEN_DID_YOU_SEE_A_GYNECOLOGIST_LAST (["have you" when] * [recently recent most last] * [gynecologist])  ^factanswer("I am not sure.")

#! Do you have annual exams
u: DO_YOU_HAVE_ANNUAL_EXAMS
	([
		([annual yearly regular] [exam appointment physical])
		([doctor physical exam appointment] *~3 [year yearly regularly annually regular])
	])
	^factanswer("I try to see the doctor every year.")

#! Have you seen a doctor
#! Do you want to see a specialist
u: MEDICAL_PROFESSIONAL
	([
		(![any other else] ~medical_professionals)
		(![any other else] [been see sought discuss] *~3 [~medical_professionals anyone counseling help])
	])
	^refine()
		a: HAVE_YOU_SEEN_A_DOCTOR (have) ^factanswer("I have not.")
		a: DO_YOU_WANT_TO_SEE_A_SPECIALIST ([do any]) ^factanswer("If you think I should.")

# #! Do you want to see a specialist
#	u: DO_YOU_WANT_TO_SEE_A_SPECIALIST (![any other else] do * [~medical_professionals anyone counseling help])  ^factanswer("I have not.")

# #! Have you seen a doctor
#	u: HAVE_YOU_SEEN_A_DOCTOR (![any other else] [been see sought discuss] *~3 [~medical_professionals anyone counseling help])  ^factanswer("I have not.")

#! Did you have any trouble finding us
u: DID_YOU_HAVE_ANY_TROUBLE_FINDING_US ([trouble problem hard] *~3 [get locate find] *~3 [us office here in])  ^factanswer("I had no trouble.")

#! Did you find a parking spot
u: DID_YOU_FIND_A_PARKING_SPOT ([park~v parking])  ^factanswer("I had no problems parking")

#! How did you get here today
u: HOW_DID_YOU_GET_HERE_TODAY (how *~3 ["get here" arrive travel])  ^factanswer("I drove")

#! How did you come to find us
u: HOW_DID_YOU_COME_TO_FIND_US (!trouble find us)  ^factanswer("I have been here before.")

#! How did you learn about this office
u: HOW_DID_YOU_LEARN_ABOUT_THIS_OFFICE ([learn discover] * [this our] * [office practice location]) ^factanswer("I have been here before.")

#! Have you been seen at this office before
u: HAVE_YOU_BEEN_SEEN_AT_THIS_OFFICE_BEFORE
	([
		(office [before prior])
 		(first *~3 visit)
		(first *~3 time *~3 office)
	])
	^factanswer("I have been here before.")

#! Are you a new patient
u: ARE_YOU_A_NEW_PATIENT (new patient)  ^factanswer("I am not a new patient.")

#! Where are you usually seen
u: WHERE_ARE_YOU_USUALLY_SEEN (where *~3 [usually normally regularly typically] *~3 seen)  ^factanswer("Only here")

#! Have you been waiting long
u: HAVE_YOU_BEEN_WAITING_LONG (wait *~3 long)  ^factanswer("I have not been waiting very long")

#! Do you have a doctor
u: DO_YOU_HAVE_A_DOCTOR (![any other else] [visit have see] *~3 [doctor physician]) ^factanswer("I do not have a doctor.")

#! Are you missing work to see me
u: ARE_YOU_MISSING_WORK_TO_SEE_ME ([missing time] * ~job * [see visit]) ^factanswer("I am not working today.")

# This is the mental health section note - we have included past and family mental health questions here instead of having separate topics for those.

topic: ~mentalhealth keep repeat

(depressed depression blue mood moody stress stressor sad lonely suicide suicidal harm intend mental cry anxiety
 image self "self image" down hopeless worry hurt distracted concentrate think "panic attack"
 tearful teary "tear up" "not worth living" "better off dead" empty helpless worthless guilt guilty blame
 happy happier satisfied pleasure ~weapons bed morning death dying die "mind racing" "stopping your mind" thoughts
 relax anxious swing grumpy overwhelmed emotion emotional mind "loss of control" interest)

#! Ever have postpartum depression
u: EVER_HAVE_POSTPARTUM_DEPRESSION
	([
		([history diagnosed diagnosis suffer treat have] *~3 postpartum depression)
		([history diagnosed diagnosis suffer treat have feel] *~3 ~depression *~3 [postpartum birth])
	])
	^factanswer("I was never depressed")

#! Family history of depression
u: FAMILY_HISTORY_OF_DEPRESSION
	([
		([~family_members family] *~3 [history diagnosed diagnosis suffer treat] *~3 ~depression)
		([history diagnosed diagnosis suffer treat] *~3 ~depression *~3 [~family_members family])
	])
	^factanswer("No one in my family has any mental health issues that I know of.")

#! Do you have a history of depression
u: DO_YOU_HAVE_HISTORY_OF_DEPRESSION  ([history diagnosed diagnosis suffer treat ever] *~3 ~depression)  ^factanswer("I have never been depressed")

#! Do you have a history of anxiety
u: DO_YOU_HAVE_HISTORY_OF_ANXIETY  ([history diagnosed diagnosis suffer treat] *~3 [anxiety anxiousness anxious])  ^factanswer("I have never been anxious.")

#! Family history of anxiety
u: FAMILY_HISTORY_OF_ANXIETY
	([
 		([~family_members family] *~3 [history diagnosed diagnosis suffer treat] *~3 [anxiety anxiousness anxious])
 		([history diagnosed diagnosis suffer treat] *~3 [anxiety anxiousness anxious] *~3 [~family_members family])
	])
	^factanswer("No one in my family has anxiety.")

#! Are you depressed
u: ARE_YOU_DEPRESSED (![bend crouch lean] [depressed depression sad blue lonely "mental health" down~8 hopeless])  ^factanswer("I am fine.") # down~8 is depressed

#! Having mood changes
u: HAVING_MOOD_CHANGES
	([
 		([have experience notice] * [moody mood grumpy emotion emotional] * [swing change fluctuation behavior times swing])
		([have experience notice] * [change fluctuation behavior times] * [grumpy moody mood])
 		([moody mood grumpy] * [~timename])
	])
	^factanswer("My mood is fine.")

#! When did you cry last
u: WHEN_DID_YOU_CRY_LAST
	([
 		(when *~3 [recently last] *~3 [cry sob])
 		(when *~3 [cry sob] *~3 [recently last])
	])
	^factanswer("I hardly ever cry.")

#! How often do you cry
u: HOW_OFTEN_DO_YOU_CRY
	([
		([when what how] *~3 [often frequently much longest] *~3 [cry sob])
		([cry sob] more [often frequently longer] [now before])
	])
	^factanswer("I do not cry very much.")

#! Do you cry often
u: DO_YOU_CRY_OFTEN
	([
		([cry sob] * [often frequently "all the time"])
		([often frequently "all the time"] * [cry sob])
		([tearful teary "tear up"])
	])
	^factanswer("I don't cry very often.")

#! Do you feel like your mind is racing
u: DO_YOU_FEEL_LIKE_YOUR_MIND_IS_RACING ([feel trouble problems] *~3 ["mind is racing" "stopping your mind" thoughts relax])  ^factanswer("I do not.")

#! Do you feel hopeless
u: DO_YOU_FEEL_HOPELESS ([hopeless "not worth living" "better off dead" empty helpless worthless overwhelmed])  ^factanswer("I am hardly ever sad.")

#! Do you feel anxious
u: DO_YOU_FEEL_ANXIOUS (![~family_members ~spouse anyone anybody pain else] [anxious anxiety anxiousness worry "panic attack"])  ^factanswer("Not really.")
	# want to avoid is there anything else you are worried about

#! Do you feel loss of control
u: DO_YOU_FEEL_LOSS_OF_CONTROL ([loss lack out] *~2 [control])  ^factanswer("I do not.")

#! Have you lost interest in activities
u: HAVE_YOU_LOST_INTEREST_IN_ACTIVITIES
	([
		([lost lack] * [interest enjoyment enthusiasm pleasure] * [activity things])
		([interest enthusiastic pleasure] * [activities things work sex])
	])
	^factanswer("I have not lost interest.")

#! Do you feel guilt
u: DO_YOU_FEEL_GUILT ([guilt guilty blame])  ^factanswer("I do not.")

#! Are you stressed
u: ARE_YOU_STRESSED (![who work job] [stress overworked stressful stressors])  ^factanswer("I am not.")

#! Family history of suicide
u: FAMILY_HISTORY_OF_SUICIDE
	([
		([~family_members family] * [commit attempt intend consider thought intention] * [suicide kill harm suicidal])
		([~family_members family] * [suicide suicidal])
	])
	^factanswer("No one in my family has committed suicide.")

#! Are you suicidal
u: ARE_YOU_SUICIDAL
	([
 		([consider thought intend intention feel felt] * [suicide harm suicidal hurt])
		([harm hurt] yourself)
		(!~family_members suicidal)
	])
	^factanswer("I have never felt that way.")

#! Do you think about death
u: DO_YOU_THINK_ABOUT_DEATH  ([think thought] * [death dying]) ^factanswer("I try not to.")

#! Do you have a weapon
u: DO_YOU_HAVE_A_WEAPON ([~weapons]) ^factanswer("I don't own any weapons.")

#! What is your self image
u: WHAT_IS_SELF_IMAGE ([what how] * ["self image" self image ]) ^factanswer("I have no issues there.")

#! Are you happy
u: ARE_YOU_HAPPY ([happy happier satisfied pleasure "mental health"])  ^factanswer("I am happy.")

#! What is your emotional state
u: WHAT_IS_YOUR_EMOTIONAL_STATE ("emotional state") ^factanswer("My emotional state is fine.")

#! How is your mood
u: HOW_IS_YOUR_MOOD (how *~3 mood) ^factanswer("My mood is fine.")

# This is the relationship health section note - we have included past and family relationship health questions here instead of having separate topics for those.

topic: ~relationshiphealth keep repeat (relationship relation "get along" "getting along" ~spouse ~family_children ~family_members sexual sex anyone anybody married)

#! How is your relationship with children
u: HOW_IS_RELATIONSHIP_WITH_CHILDREN (![spouse partner husband wife] [relationship relation "getting along"] * [~family_children children kid])  ^factanswer("We have a good relationship.")

#! How is your relationship with your spouse
u: HOW_IS_RELATIONSHIP_WITH_SPOUSE
	([
		(![~family_children children kid sex sexual] [relationship relation "getting along" "get along"] *~3 [~spouse])
		(![~family_children children kid sex sexual] ~spouse *~3 [relationship relation "getting along" "get along"])
	])
	^factanswer("We have a good relationship.")

#! Is your family worried about you
u: IS_YOUR_FAMILY_WORRIED_ABOUT_YOU (!health [~family_members ~spouse anyone anybody] *~3 [concern worry])  ^factanswer("I don't think anyone worries about me.")
	a: WHY_WORRIED ([what why] *~3 [concern worry]) ^factanswer("I don't know.")

#! Do you wish you were not married
u: DO_YOU_WISH_YOU_WERE_NOT_MARRIED
	([
 		([wish think] * [not "no longer"] * [married together relationship])
		([wish think] * [divorce divorced])
	])
	^factanswer("Well, no.")

#! How is your sexual relationship
u: HOW_IS_YOUR_SEXUAL_RELATIONSHIP
	([
		(how *~4 [sex sexual] *~3 [relationship life relations])
		([change decrease increase] *~3 [sex sexual] *~3 [relationship life relations])
	])
	^factanswer("Fine.")
