tablemacro: ^equivalent ($_label1 $_label2) # credit for only one in the open/closed world
createfact( $_label1 equivalent $_label2)

tablemacro: ^grade_sheet ($_patient $_section.KEEP_QUOTES)
createfact($_section $_patient grade_items)

tablemacro: ^section variable TAB ($_sectionname.KEEP_QUOTES $_entry.KEEP_QUOTES $_rule $_junk $_junk1  $_junk2  )
$_sectionname = ^pos(allupper $_sectionname)
# $_junk is the tab
if ($_entry != *)
{
  $$entry = $_entry # track this column's value for future lines
  ^createfact($_entry section $_sectionname)
}

# validate that table line was good (tabs instead of spaces)

$_error = ^"Bad Table data in ^Section $_sectionname $_entry ($$entry) $_rule $_junk $_junk1 $_junk2 \n"
if ($_entry != * AND $_rule != *) # not allowed both
{
    ^Bug($_error)
}
else if ($_entry == * AND $_rule == *) # not allowed neither
{
    ^Bug($_error)
}
else if ($_junk != * OR $_junk1 != * OR $_junk2 != * ) # excess data
{
    ^Bug($_error)
}
if (^extract(%tableinput 0 1) == " ") 
{
    ^Bug(^"spaces at start of table entry $_error")
}

# actual data save

if ($_rule != *)
{
    $_rule = ^pos(allupper $_rule)
    ^createfact($_rule question $$entry)
}

table: ^section("History of Present Illness")
Location
	Where_is_the_pain
	Where_is_the_1
	Is_the_pain_only_in_your_lower_back
	Is_the_pain_only_in_your_back
	Is_the_pain_in_your_upper_or_lower_back
