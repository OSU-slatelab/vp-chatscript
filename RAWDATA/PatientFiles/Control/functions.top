outputmacro: ^TrackOpenQuestions()
	if (!$open) {$open = 0}

	# track how many open questions asked in early stages
	if (%input == 5) {$first5open = $open}
	else if (%input == 10) {$first10open = $open}
	else if (%input == 15) {$first15open = $open}

outputmacro: ^ProcessProperties($_label)
	# get this label's properties for this bot's diagnosis 
	@1 = query(direct_sv $_label $cs_bot ?) # get this label's properties 
	loop () # process each property
	{
		$$tmp = first(@1object) # DATA IS: ( What_is_your_goal Patient1a Open ) - so this gets property Open or whatever
		if (query(direct_sv $_label equivalent ?)) # this label is considered equivalent to some other one
		{
			$_label = ^first(@1object)  # get std label for this
		}

		if ($$tmp == summary )  # summary questions track from what topic user was when asking
		{
			Createfact($_label summary $currenttopic) # from what topic did they ask this question
		}
		
		$$kind = join('$ $$tmp) # get property like "open" making it $open
		^$$kind += 1 	# increment count
	}

outputmacro: ^factanswer(^failmsg) # this is the code that queries the table of labels and answers and returns the answer if one is available.
	$$label = GetRule(LABEL ~)
	if (^query(direct_sv $$label $vpatient ? 1))
	{
		print (OUTPUT_NOQUOTES  @0object)
	}
	else
	{
		print(OUTPUT_NOQUOTES ^failmsg)  # print needed to strip quotes off the variable value
	}
	$$output = ^response(1)
	if ($regression) { Log (FILE LOGS/VP.txt  $$label ^" : "  $$rawinput : $$output \n) }

	^nofail(TOPIC respond(~score)) # compute current scoring information - this runs for every question 
