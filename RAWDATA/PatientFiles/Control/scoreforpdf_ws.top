
topic: ~score ()
u: () $totalQueries = $q_num_external

	# get this label's properties for this bot's diagnosis 
	@1 = query(direct_sv '$$label $cs_bot ?) # get this label's properties 
	loop () # process each property
	{
		$$tmp = first(@1object) # DATA IS: ( What_is_your_goal Patient1 Open ) - so this gets property Open or whatever
		if (query(direct_sv '$$label equivalent ?)) # this label is considered equivalent to some other one
		{
			$$label = ^first(@1object)  # get std label for this
		}

		if ($$tmp == summary )  # summary questions track from what topic user was when asking
		{
			Createfact($$label summary $currenttopic) # from what topic did they ask this question
		}
		
		$$kind = join('$ $$tmp) # get property like "open" making it $open
		^$$kind += 1 	# increment count
	}

	# now note this new question was asked
	$$tmp = $$rawinput 
	
	# find out if this is a retry query by checking for another query with the same number; 
	# if so we want to keep the old form and replace the label in the fact set.
	@17 = ^query(direct_vo ? asked $totalQueries )
	if (!@17)
	{
		^createfact($$tmp form $totalQueries) # Only store the form if this is the first time this query num was seen
	} 
	else {
		^delete(^first(@17fact))
	}

	^createfact($$label asked $totalQueries FACTDUPLICATE) # store label; this was deleted if there was a previous label 

	if (!$open) {$open = 0}

	# track how many open questions asked in early stages
	if (%input == 5) {$first5open = $open}
	else if (%input == 10) {$first10open = $open}
	else if (%input == 15) {$first15open = $open}

topic: ~finalscore system repeat()
u: () 
#	 createfact(^"/openCurly/\"name\": \"$login\", \"sections\": /openSquare/" scorecard x FACTTRANSIENT)
	 createfact(^"/openCurly/\"name\": \"$cs_login\", \"sections\": /openSquare/" scorecard x FACTTRANSIENT)

	@4 = query(direct_vo ? $cs_bot grade_items)  # get sections to grade for this patient, eg ("Family History" zone grade_items)
	
	$$sectionnumber = 0
	
	# handle each section of grading
	loop()
	{
		$$section = ^last(@4subject)  # get a section

		if ($$sectionnumber == 0)
		{
			createfact(^"/openCurly/\"name\": \"$$section\", \"categories\": /openSquare/" scorecard x FACTTRANSIENT)
		}
		else {createfact(^", /openCurly/\"name\": \"$$section\", \"categories\": /openSquare/" scorecard x FACTTRANSIENT)}
		$$sectionnumber += 1		

		$$categoryCount = 0
		$$didasksection = 0

		@3 = query(direct_vo ? section $$section) # get the types of questions to ask, eg (Location section "History of Present Illness")
		loop()
		{
			$$category = ^last(@3subject)

			if ($$categoryCount == 0)
			{
				createfact(^"/openCurly/\"name\": \"$$category\", \"questions\": /openSquare/" scorecard x FACTTRANSIENT)
			}
			else {createfact(^"/closeSquare//closeCurly/, /openCurly/\"name\": \"$$category\", \"questions\": /openSquare/" scorecard x FACTTRANSIENT)}

			$$categoryCount += 1		# questions he should ask
			$$didask = 0

			@2 = query(direct_vo ? question $$category) # these are questions we recognize he should ask - he gets credit in the section if he asks at least one of these though we show all he asked
			loop()
			{
				$$question = ^last(@2subject)
	
				# did he ask this?
				if (^query(direct_sv $$question asked ?)) # he asked this 
				{	
					$$q_num = ^last(@0object)
					@18 = ^query(direct_vo ? form $$q_num)
					$$form = ^last(@18subject)
					if ($$didask == 0)
					{	
						createfact(^"\"$$form\"" scorecard x FACTTRANSIENT)
					}
					else {createfact(^", \"$$form\"" scorecard x FACTTRANSIENT)}
					$$didask += 1
				}
			}
			if ($$didask == 0) 
			{
				createfact(^"\"No question asked.\"" scorecard x FACTTRANSIENT)
			}
			else {$$didasksection += 1}
		}
		createfact(^"/closeSquare//closeCurly//closeSquare/, \"summary\": \"$$didasksection of $$categoryCount essential questions asked.\"/closeCurly/" scorecard x  FACTTRANSIENT)
	}

	createfact(^"/closeSquare/, \"total questions\": \"$totalQueries\"" scorecard x  FACTTRANSIENT)


	# open information
	 if (!$open) {$open = 0}
	 createfact(^", \"open questions\": \"$open\"" scorecard x  FACTTRANSIENT)


	 $$tmpfile1 = $open * 100 / $totalQueries
	 createfact(^", \"open question percent\": \"$$tmpfile1\"" scorecard x  FACTTRANSIENT)


	 if (%input <= 5) { $first5open = $open}
	 createfact(^", \"open in first 5\": \"$first5open\"" scorecard x  FACTTRANSIENT)


	 if (%input <= 10) { $first10open = $open}
	 createfact(^", \"open in first 10\": \"$first10open\"" scorecard x  FACTTRANSIENT)


	 if (%input <= 15) { $first15open = $open}
	 createfact(^", \"open in first 15\": \"$first15open\"" scorecard x  FACTTRANSIENT)


	# closed information
	 if (!$closed) {$closed = 0}
	 createfact(^", \"closed questions\": \"$closed\"" scorecard x  FACTTRANSIENT)


	 $$tmpfile1 = $closed * 100 / $totalQueries
	 createfact(^", \"closed question percent\": \"$$tmpfile1\"" scorecard x  FACTTRANSIENT)
	 
	 $$tmpfile1 = $open * 100 / $closed # MULTIPLIED BY 100 TO GET CORRECT RATIO
	 createfact(^", \"open/closed ratio\": \"$$tmpfile1\"" scorecard x  FACTTRANSIENT)

	 # for summarizing questions, show the question and from what topic it was asked
     nofail(rule query(direct_v ? summary ?))
	 createfact(^", \"summarizing questions\": /openSquare/" scorecard x  FACTTRANSIENT)

	if (^length(@0) == 0) 
	{ 
	 	createfact(^"\"none\"" scorecard x  FACTTRANSIENT)
	}
	
 	 $$count = 0
     loop()
	 {  
		$$count += 1
         _0 = last(@0all)
		if ($$count == 1)
		{
		createfact(^"\"$$count. Asked: _0 \( from topic _2 \ at question" scorecard x  FACTTRANSIENT)
		}
		else {createfact(^", \"$$count. Asked: _0 \( from topic _2 \ at question" scorecard x  FACTTRANSIENT)}
		@1 = query(direct_sv _0 asked ? 1 )
		@19 = ^query(direct_vo ? form @1object)
 	 	createfact(^"@1subject \)\"" scorecard x  FACTTRANSIENT)
    }
	 
	 # here is the history of what they asked
 	createfact(^"/closeSquare/, \"transcript\": /openSquare/" scorecard x  FACTTRANSIENT)

	$$whichquestion = 0

	 nofail(rule query(direct_v ? form ?))
     loop()
	 {  
        _0 = last(@0all)
		if ($$whichquestion == 0)
		{
			$$whichquestion += 1
  			createfact(^"\"_2. _0\"" scorecard x  FACTTRANSIENT)
		}
		else {createfact(^", \"_2. _0\"" scorecard x  FACTTRANSIENT)}
   }

	createfact(^"/closeSquare//closeCurly/" scorecard x  FACTTRANSIENT)

   # output all answers to user and file
	$$tmpfile = join(TMP/ $cs_login .txt)
#	$$tmpfile = join(C:\inetpub\wwwroot/Scores/ $cs_login .txt)
	^log(FILE $$tmpfile NEW)  # start file from scratch
	@0 = query(direct_vo ? scorecard x)
	loop()
	{
		$$tmp = last(@0subject)
		$$tmp
		^log(FILE $$tmpfile $$tmp)

	}