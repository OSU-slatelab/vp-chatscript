###### THIS FILE SHOULD GENERATE NO USER OUTPUT

topic: ~XQUESTION system repeat ()

s: QUESTION () ^refine()		# determine question/command type
	
	a: (\!) # leave ! and dont set question if not already set

	#! What a beautiful day
	a: (< that is ~determiner  ) # what a xxx becomes that is a xxx in substitutes
	
	#! be dark
	a: (< ['be 'let] !you ) # a command

	#! where
	a: (< ~qwords *~2 >) SetTokenFlags(#QUESTIONMARK)  

	#! how are you
	a: (~emohowzit) SetTokenFlags(#QUESTIONMARK)  

	#! Have you seen a poem
	#! Would the man go home soon
	#! What are birds
	#! What kind of man are you
	a: (<  [ ~qwords  ~auxverblist ] ) SetTokenFlags(#QUESTIONMARK) 

	#! Tell me about birds.
	a: (< *~2 [~describe ~list ~explain])  SetTokenFlags(#QUESTIONMARK)  

	#! French is the language of what country
	a: ( ~preposition what ~noun ) SetTokenFlags(#QUESTIONMARK) 

	#! Joan is married to whom
	a: (~preposition whom > ) SetTokenFlags(#QUESTIONMARK) 

	#! John was born when
	a: ( [where when how why what] > ) SetTokenFlags(#QUESTIONMARK) 

	#!!B Tell me about sex5
	a: ( < ~tell I ) ^SetTokenFlags(#QUESTIONMARK)  # treat command as a question				

	#!  if you could do anything, what would you do
	a: ( < if ) ^SetTokenFlags(#QUESTIONMARK)	 	

	#! the moon is what shape?
	a: ( * be ~qwords  ) ^SetTokenFlags(#QUESTIONMARK)	 	
	
